"Quotient, The Game" by "Gregory R. Simpson"

[****************

This game is based on the books The Quantum Contingent and Quantum Time by Gregory R. Simpson. (C) 2022, 2024.

The text, characters and storyline are all protected by copyright and may not be used in derivative works without permission, but the game mechanics and code to accomplish an IF adventure is free for you to use without the embedded story text.

*****************]

Book 1 - Getting Started

Chapter 1 Setup

Use scoring.
Use American dialect and the serial comma.
The maximum score is 214.
The release number is 107.
The story description is "Quotient the Game! Play an old fashioned interactive fiction adventure based on the books 'The Quantum Contingent' and 'Quantum Time'. Meet many of the characters and visit places from the books. As in any good interactive fiction game, there are puzzles and a secret mission.".
The story creation year is 2024.
The story genre is "Science Fiction".
The story headline is "An interactive fiction, based on the books [italic type]The Quantum Contingent[roman type] and [italic type]Quantum Time[roman type]"

Index map with room-shape set to "square" and room-size set to 36 and room-name-size set to 9 and room-name-length set to 13 and route-thickness set to 15 and font set to "Trebuchet-MS-Regular" and EPS file.

Chapter 2 Extensions

Include Locksmith by Emily Short.
Include Basic Literacy by Bart Massey.
Include Small Kindnesses by Aaron Reed.
Include Extended Grammar by Aaron Reed.
Include Basic Screen Effects by Emily Short.
Include Swearing Reloaded by Shin.

Chapter 3 Some helpful help tools from Emily Short's Bronze

Taking inventory is acting confused. Looking is acting confused.

After acting confused for the third turn:
	say "(If you are feeling lost, try HELP.)"

After waiting for more than four turns:
	say "Matters will not mend themselves on their own."
After waiting for more than six turns:
	say "(If you are feeling lost, try HELP.)"
	
Understand "who" or "what" or "when" or "where" or "why" or "how" or "who's" or "what's" or "when's" or "where's" or "why's" or "how's" as "[query]".

Understand "lay [something]" as dropping.

After reading a command:
	if the player's command includes "[query]" begin;
		say "[story title] understands commands, such as '[command prompt]examine [a random thing that can be seen by the player]', but not questions. You can get some information about the current game state with commands such as LOOK, EXAMINE, INVENTORY.";
		reject the player's command;
	end if.
	
Understand "rooms" or "places" as seeking room list. Seeking room list is an action out of world. Carry out seeking room list: say "So far you have visited [the list of visited rooms]."

Understand "objects" or "things" as seeking object list. Seeking object list is an action out of world. Carry out seeking object list: say "So far you have seen [a list of listable things]."

A thing can be seen or unseen. A thing is usually unseen. The player is seen. Before printing the name of something (called target): now the target is seen. 

[We also need to deal with the question of whether the player has examined an object, for those objects whose descriptions carry vital information:]

A thing can be examined or unexamined. A thing is usually unexamined. 
[Carry out examining something: now the noun is examined.]
	
After taking something examined:
	say "You take [the noun].[line break]";
	
After taking something: 
	if the noun is a treasure:
		if the noun is unscored:
			increase the score by 2;
			now the noun is scored;
			say "You take [the noun]. and examine it carefully.";
			try examining the noun;
			now the noun is examined;
		otherwise:
			say "hmmmm. You take [the noun] and examine it carefully.";
			try examining the noun;
			now the noun is examined;
	otherwise:
		say "You take [the noun] and examine it carefully.";
		try examining the noun;
		now the noun is examined;

Definition: a thing is listable:
	if it is the player, no;
	if it is unseen, no;
	if it is scenery, no;
	yes.
	
Section 1  thanks 

Understand "thank [someone]" or "thanks [someone]" or "say thank you to [someone]" or "say thanks to [someone]" as thanking. Thanking is an action applying to one visible thing.

thanking to default is an action applying to nothing. Understand "thanks" or "say thanks" or "say thank you" or "thanks" or "thanks again" or "thanks a lot" or "thank you" or "bless you" or "cheers" or "much obliged" as thanking to default.

Instead of thanking someone (this is the standard thanks redirection rule):
	say "[one of]You are very welcome[or]Thank you too![or]No Problem[or]Sure thing[at random]";
	
Instead of thanking to default:
	say "[one of]No, thank you[or]Thank you too![or]You are very welcome![or]Sure thing![at random]";
	
Chapter 4 Status bar 	

To say current rank:
	let result be "plain ole you";  [Default value]
	repeat through the Table of Rankings:
		if the score is less than the score entry, break;
		now result is the rank entry;  [Assign rank]
	say result.

Table of Fancy Status
	left	central	right 
	" [if in darkness]Darkness[otherwise][location][end if]"	""	"Score: [score]"
	""	""	""
	" Rank: [current rank]"	""	"Moves: [turn count]"
	
Rule for constructing the status line:
	fill status bar with Table of Fancy Status;
	rule succeeds.
 
 	
Chapter 5 Help 

Helping is an action out of world.
Understand "help" as helping. 


Carry out helping:
	say "[story title] understands commands, such as '[command prompt]examine [a random thing that can be seen by the player]'. You can get some information about the current game state with commands such as LOOK, EXAMINE, INVENTORY and the special commands PLACES and OBJECTS.[line break] Commands are typically in the format of VERB, VERB NOUN, or VERB with/to NOUN. As in TAKE, TAKE THE SHOVEL, or TALK TO SARAH. In some cases, you may do something more complex like this. >HIT THE NAIL WITH THE HAMMER, or >ASK MARTIN ABOUT THE COMPUTER.[paragraph break]In Quotient, if you take an item, it will automatically be examined for you. Note that some items may also be read, as in READ item.[paragraph break]You move from place to place with commands like NORTH, UP, etc. If you stumble across the TARDIS communicator, take it! You can examine it again and again to hear the Quotient quantum computer talk to you about things that might be of interest near you.[line break]And remember, commands can be abbreviated. For example, n is NORTH and x is EXAMINE. You get points from collecting treasures and solving puzzles. As your points go higher, so does your rank! Type POINTS for more info, or SCORE to see your score. You should figure out the true mission of the game as you go along. [paragraph break]Remember, in addition to EXAMINE, you can READ certain objects as well. Finally, ABOUT will tell you what the game is based on, and CREDITS gives credit where credit is due![paragraph break]Now, let's get adventuring!";

Hinting is an action out of world.
Understand "hint" or "hints" or "clue" or "clues" as hinting.

Carry out hinting:
	say "Your best chance for context sensitive hints is to find the TARDIS communicator and examine it. It will talk to you everytime you examine it, and it will say different things in different areas of the game.[paragraph break]Your one hint is that the Tardis communicator is near the farmhouse, so I'd recommend you find it before flying off to far away places! Good luck and enjoy the journey!";
	say "For basic help, type HELP.";

	
Chapter 6 Credits / About 

Understand "about" or "info" or "information" as showing about.

Showing about is an action out of world.

Carry out showing about:
	say "This game was created by Gregory R. Simpson. Special thanks to Karen for early alpha testing!";
	say "Based on the books, [italic type]The Quantum Contingent[roman type] and [italic type]Quantum Time[roman type].";
	say "For detailed credits, type CREDITS.";

Understand "points" as showing points.

Showing points is an action out of world.

Carry out showing points:
	say "You can score 107 points by collecting treasures and solving the puzzles leading to the endgame. You get another 107 points when you take the final move to win the game.[paragraph break]Treasures are worth 2 points. There are a few actions that are not required to win the game but may provide hints, or are just fun asides that are worth 2-5 points. Puzzles to win are worth 5-10 points. Generally speaking, all treasures provide a [italic type]hint[roman type] unless it is a food/drink. Most food and drink is just for fun, but not all![paragraph break]Good Luck!";


Understand "credits" or "credit" as showing the credits.

Showing the credits is an action out of world.

Carry out showing the credits:
	say "[italic type]Quotient, The Game[roman type] is written in Inform7. Inform7 is the work of Graham Nelson. First a shout-out to the people on Intfiction.org. They were enormously helpful. If I ever got stuck on how to code something, they would help me out! I also experimented with using ChatGPT to help me figure out some coding challenges. It did pretty well, but the people at Intfiction.org were better.[paragraph break]Many of the description passages were adapted from my books. This game is a tongue-in-cheek adaptation of that world and it's characters.[paragraph break]This was my first Inform7 creation. I enjoyed making it and I hope you enjoy playing it. Now let's get adventuring![paragraph break]";
	repeat through the Table of Credits:
		say "[The Role entry]: [The Contributor entry][line break]";

Table of Credits
Role	Contributor
"Author"	"Gregory R. Simpson"
"My Love and First Tester"	"Karen"
"Beta Testers"	"P-Tux7 and johnbrown (thanks for all the help!)"
"My Inspiration"	"Bridget and Jason"
"Game Inspiration"	"Zork"
"Book Inspiration"	"The Quantum Contingent"
"Second Book Inspiration"	"Quantum Time"
"Cover Photo"	"I took this photo of the Rock Church of St. George in Ethiopia and then used smartphone app Prisma to give it the wild color look."
"In Game Photos"	"All in game photos are personal photos that I took. (there are only 8, it's a 'text' adventure.)"
"Zorkmid ASCII Image"	"This was from Dean Menezes's port of Zork for Inform7."
"Release"	"107.RC13"


Chapter 7 Magic Movements, Xyzzy, Magic Words

xyzzying is an action applying to one thing. Understand "xyzzy [any room]" as xyzzying.

xyzzying to default is an action applying to nothing. Understand "xyzzy" as xyzzying to default.

Carry out xyzzying to default:
	say "You mutter the magic word, and winds whirl around you until you appear at headquarters.";
	move the player to headquarters;
	now the player has the flashlight;
	now the player has the keycard;
	now the player has the tardis communicator;
	now the player has the scroll of treasure;
	move jet to airstrip;    

Instead of xyzzying [noun]:
	say "You mutter the magic word, and with a flash, you find yourself in [noun].";
	move the player to noun;
	
The previous location is a room that varies.

Understand "plugh" as a mistake ("Is your voice hollow?")

Understand "leviosa" as a mistake ("It's LeviOsa! Not LevioSA...")

Chapter 8 Some ZORK Adds from Dean Menezes 

[Copyright (c) 2008 Dean Menezes

Permission is hereby granted, free of charge, to any person
obtaining a copy of this software and associated documentation
files (the "Software"), to deal in the Software without
restriction, including without limitation the rights to use,
copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the
Software is furnished to do so, subject to the following
conditions:

The above copyright notice and this permission notice shall be
included in all copies or substantial portions of the Software.]

Greeting is an action applying to nothing. Understand "hello" and "hi" and "greetings" as greeting. Carry out greeting: say "[one of]Hello[or]Good day[or]Yo![or]Nice weather we've been having lately[or]Goodbye[at random]."

To say banknotes:
say line break;
say fixed letter spacing;
say " _______________________________________________________________[line break]";
say "| 1  0   0         GREAT UNDERGROUND EMPIRE           1  0   0  |[line break]";
say "| 1 0 0 0 0                                           1 0 0 0 0 |[line break]";
say "| 1 0 0 0 0                                           1 0 0 0 0 |[line break]";
say "| 1  0   0                  DIMWIT                    1  0   0  |[line break]";
say "|                      ||||||||||||||||                         |[line break]";
say "|                      ||   __  __   ||              B30332744D |[line break]";
say "|                      ||  -OO  OO-  ||                         |[line break]";
say "|       IN FROBS       [unicode 92]||    >>    ||/       WE TRUST          |[line break]";
say "|                       ||  ______  ||                          |[line break]";
say "| B30332744D             |  ------  |                           |[line break]";
say "|                        [unicode 92][unicode 92]________//                           |[line break]";
say "| 1  0   0    Series       FLATHEAD     LD Flathead   1  0   0  |[line break]";
say "| 1 0 0 0 0   719GUE                     Treasurer    1 0 0 0 0 |[line break]";
say "| 1 0 0 0 0                                           1 0 0 0 0 |[line break]";
say "| 1  0   0        One Hundred Royal Zorkmids          1  0   0  |[line break]";
say "|_______________________________________________________________|[line break][variable letter spacing]";
say "(this is just here as a tribute to the old Zork game... for the old geeks who remember it. I gave you 2 points for the trouble.)";

Understand "foo" and "bar" and "bletch" as a mistake ("Oh yeah, well FOO, BAR, and BLETCH to you too.").

Digging into it with is an action applying to one thing and one carried thing.
Understand "dig into/in [something] with/using [something]" as digging into it with.
Understand "dig [something] with/using [something]" as digging into it with.
Understand "dig with/using [something] into/in [something]" as digging into it with (with nouns reversed).

Check digging into it with:
if the noun is not the ground, say "Digging into [a noun] is silly." instead;
if the second noun is not the shovel, say "Digging with [the second noun] is slow and tedious." instead;
if the noun is the ground and the player is in Peaceful Meditation, say  "You may not dig virtual sand. It is not peaceful to do so." instead;
if the noun is the ground and the player is not in Edgewater Park, say  "The ground is too hard for digging here.".

A stack of zorkmid bills is nowhere. "On the ground sit 200 neatly stacked zorkmid bills." Understand "notes" or "banknotes" or "bank notes" or "zorkmids" as the bills. The description of the bills is "[banknotes]".

The ground is a backdrop. The ground is everywhere. Understand "sand" and "earth" and "here" and "hole" as the ground. The description of the ground is "yep, that's the earth we walk on.".

The weather is a backdrop. The weather is everywhere. The description of the weather is "Another beautiful day in paradise!".

Check digging into the ground with the shovel when the player is in Edgewater Park for the first time:
	say "You seem to be digging a hole here." instead.
	
Check digging into the ground with the shovel when the player is in Edgewater Park for the second time:
	say "The hole is getting deeper, but that's about it." instead.
	
Check digging into the ground with the shovel when the player is in Edgewater Park for the third time:
	say "You are surrounded by a wall of sand on all sides." instead.
	
Check digging into the ground with the shovel when the player is in Edgewater Park the fourth time:
	say "You dig up a stack of 200 banknotes .";
	increase score by 2;
	move the zorkmid bills to Edgewater Park instead.
	
Check digging into the ground with the shovel when the player is in Edgewater Park more than 4 times:
	say "The hole collapses, and almost kills you. You manage to eventually work your way out with some help from other beach goers." instead.
	
Counting is an action applying to one visible thing.  Understand "count [something]" as counting.

Check counting:
if the noun is the pile of leaves, say "There are 69,105 leaves here." instead;
[* It is absurd that the adventurer would be able to count so many leaves with such precision, especially in the minute or so time that a normal turn supposedly takes.]
[* Decimal 69 is equal to octal 105, and hexadecimal 69 is equal to decimal 105.] 
if the noun is the bills, say "Don't you trust me?  There are 200 bills." instead;

Carry out counting:
say "You have lost your mind."
Understand "count blessings" as a mistake ("Well, for one, you are playing a text adventure, more than 40 years after the release of Zork....").

Before swearing obscenely:
  say "[one of]Such language in a high-class establishment like this![or]Oh, dear. Such language from a supposedly winning adventurer![or]You ought to be ashamed of yourself.[or]It's not so bad. You could have been killed already.[or]Tough guano, this isn't helping.[at random]" instead.

Hi-speaking is an action applying to one visible thing. Understand "hello [something]" and "hi [something]" as hi-speaking.
Check hi-speaking:
	if the noun is the Kara, say "Hello there." instead;
	if the noun is the Sarah, say "Hi! Good to see you." instead;
	if the noun is the Martin, say "Welcome, Agent." instead;
	if the noun is not a person, say "I think that only schizophrenics say 'Hello' to [a noun]." instead.
Carry out hi-speaking:
	say "[The noun] smiles and nods to you in greeting."

Book 2 - Play Begins

Chapter 1 When Play Begins

When play begins:
	say "Do you want to become an agent of Quotient, the global, independent spy agency? You are at the entrance to their top secret headquarters. Proceed down the lane to begin your mission and earn the rank of Secret Agent! If this is your first text adventure, just type help."

Chapter 2 - Ports

A port is a kind of room. Definition: a room is unflyable if it is not an port.
Instead of going by a vehicle (called the flight) to somewhere unflyable:
    say "You can't drive the [the flight] off-road."

Chapter 3 - player marks, initals on foot if in metaverse

The mark is a thing. The description is "A small, faint mark on your foot." 
The mark is part of the player. The mark is scenery.
The read text of the mark is "You know... your initials".

Understand "foot" or "appendage" or "left foot" as mark.

Instead of examining the player:
    say "You are, as always, you, except when you aren't. If you want to examine your inner self further, I'm afraid that will cost more than 200 Zorkmids.[if player is in VR Area][line break]You notice a small, faint mark on your foot.";

Instead of examining the mark:
    say "[if player is in VR Area]You look at the bottom of your left foot. What appear to be your initials are written there in black sharpie.";

Chapter 4 Text Messages

After going to the Hyperloop Station for the first time:
	if the player has the Tardis Communicator:
		continue the action;
	else:
		say "A text message arrives on your watch. It is from Martin.[line break][italic type]You should take the Tardis communicator with you if you leave HQ. It's around the farmhouse somwhere.[roman type][paragraph break]";
		continue the action.

After taking the flashlight for the first time:
	the flashlight-timer bings in 3 turns from now;
	continue the action.

At the time when flashlight-timer bings:
	say "Your watch vibrates. You have an incoming text from Martin. [line break][italic type]Nice work. The flashlight should put some light on the situation.[roman type][paragraph break]".
	
After taking the gun for the first time:
	the gun-timer bings in 2 turns from now;
	continue the action.

At the time when gun-timer bings:
	say "Your watch vibrates. You have an incoming text from Florin. [line break][italic type]I got an alert that the gun was removed from the White House by you. Good work. Get over here and let's catch Robert.[roman type][paragraph break]".
	
After taking the Mirrored Ball for the first time:
	the mirrorball-timer bings in 2 turns from now;
	continue the action.

At the time when mirrorball-timer bings:
	if the player carries the Tardis Communicator: 
		say "The TARDIS Communicator vibrates and lights up and then just starts speaking. [line break][italic type]Hey, I know I normally only talk to you when you examine me, but I just love that Mirrored ball, and for some reason, I think it could be important to our mission! Good work![roman type][paragraph break]";
	otherwise:
		say "Your watch vibrates a haptic rhythm. You have an incoming text from TARDIS?[line break][italic type]You really need to find the TARDIS COMMUNICATOR, it's not far from you now. When you find it, I'll be able to talk to you a lot more, and I know you'll enjoy that.[paragraph break]Anyway, since you haven't found it yet, I thought I'd text you because I just LOVE that Mirrored Ball. For some reason, I think it could be important to our mission! So, congrats on the ball, now go find the communicator so I can help you on your journey![roman type]".
	
At the time when the lightsaber-timer bings:
	say "Your watch vibrates, You have an incoming text from Cassie. [line break] [italic type]My instruments detected a powerful pulse of energy near your location. If it's what I think it is, it could be key to getting my console operational.[roman type][paragraph break]".
	
At the time when the laser-timer bings:
	say "Your watch vibrates, You have an incoming text from Jack. [line break] [italic type]Hey! The sky is red all over the city! You must have found and activated the laser. Great work![roman type][paragraph break]".
	
At the time when the tardis-timer bings:
	say "Your watch vibrates, You have an incoming text from Sarah. [line break] [italic type]:) :) Tardis is working! I'm sure it must have been you! Thanks! You are becoming quite the spy![roman type][paragraph break]".

Book 4 - Places

Chapter 1 Farmstead

Farmstead Area is a region. Security Checkpoint, Lane, The Kitchen, The Gate, Headquarters Entrance, The Farmhouse, Old Cellar, Yard, Smokehouse, Fire Pit, Old Barn, Parking are in the Farmstead Area.

Security Checkpoint is a room. "The facial recognition [italic type]cameras[roman type] at the perimeter of the property scan visitors meticulously. Hidden defenses lower as you pass, allowing you to proceed casually up the lane to the west."

The cameras are scenery in the Security Checkpoint. "These cameras are state-of-the-art. Infrared, motion detecting, high res, any buzz word you can think of, these have it.";

Understand "camera" or "cam" as cameras.

A Mailbox is a an openable closed container in the Security Checkpoint. The Mailbox is fixed in place. The description of the mailbox is "It's a non-descript mailbox mounted on an old cedar post.".
The Leaflet is a thing in A Mailbox.
The description of the leaflet is "It's a tri-fold leaflet addressed to 'secret agent'[paragraph break]".

Understand "mail" or "box" as Mailbox.
	
The read text of the The Leaflet is "WELCOME Agent![line break]Congratulations on being selected to join Quotient. [italic type]Why am I at the end of a lane leading to an old farmhouse?[roman type] Well, this is no ordinary farmhouse. Your first step is to find your way into Quotient Headquarters! From there you will be given more information about your first mission. Good Luck! [paragraph break]'Quotient, The Game' is a text adventure game based on the books [italic type]The Quantum Contingent[roman type] and [italic type]Quantum Time[roman type]. 'No computer should be without one!'[paragraph break]
[italic type]    *leaflet and mailbox based on the original ZORK, one of the first interactive fiction adventures[roman type][paragraph break]Type HELP for basic instructions and HINT for your one hint."


Lane is west of The Security Checkpoint. "The long lane is lined with [italic type]large willow trees[roman type] whose branches sway smoothly in the breeze, their branches sway smoothly, calming one’s nerves with only a glance. These massive centurions are actually part of the sophisticated security system guarding the route to this remote, hidden fortress near Greenville, Ohio.[paragraph break]You can go east or west on the lane."

The large willow trees are scenery in the Lane. "These large oaks separate the path from the massive [italic type]cornfield[roman type] to the north."

Understand "trees" as the large willow trees.
Understand "tree" as the large willow trees.

The cornfield are scenery in the Lane. "The cornfield seems to go on for ever. You can enter the cornfield via a small path through the trees to the north."

Understand "corn" as cornfield.

The Gate is west of the Lane. "The gate is open. There is a [italic type]sign[roman type] mounted by the gate[paragraph break]The lane goes west toward the house and east back toward the Security Checkpoint."

The Sign is scenery in The Gate. "Let em grow up."
The read text of the sign is "Let em grow up."

Figure of sign_image is the file “sign.png”

Instead of examining the sign:
	Display the figure of sign_image;
	say "Let em grow up"

Headquarters Entrance is west of The Gate. "You stand before a massive old home with tall clay-shingled clapboard [italic type]turrets[roman type] stretching up into the sky. An expansive old-fashioned porch sprawls across the entire front of the mansion. Wooden [italic type]porch swings[roman type] dangle from chains, rocking gently in the sweet-smelling breeze scented by the surrounding [italic type]old-growth lilac and magnolia bushes[roman type].[paragraph break]The house is directly in front of you to the west. There are steps down into a cellar here, a yard to the north, and some parking to the southwest."

The Turrets are scenery in the Headquarters Entrance. "Tall Circular towers, like you'd see on an old Victorian house, or as defensive towers on a castle!".

The old-growth lilac are scenery in the Headquarters Entrance. "The massive lilac bushes are filled with fragrant bunches of flowers like grapes, whereas the magnolia flowers look more like stars filling a green bed of leaves.".

Understand "lilac" or "magnolia" or "bushes" or "old-growth" as old-growth lilac.

The porch swing is an enterable scenery supporter in the Headquarters Entrance. "The porch swing looks like a great place to read a book or unwinding after a long day of spy work."

Understand "swings" or "porch swings" as porch swing.

Report entering the porch swing:
	say "Oh, this is so relaxing. A great place for coffee and connections."

The Farmhouse is west of The Headquarters Entrance. "You cross the porch of the old victorian home and enter the dining room. A [italic type]grape chandelier[roman type] hangs above the long table. From the [italic type]north window[roman type], you can see the [italic type]lawn[roman type], the hammock and the fields past the hammock to the north.[paragraph break]You can go east to exit the house, or continue west into the kitchen you see in the next room."

Parking is southwest of The Headquarters Entrance. Parking is south of Fire Pit. "There is some parking here off the driveway, just past the house.[paragraph break]A fire pit is to the north, or you can return northeast to the entrance."

The north window is scenery in the Farmhouse. The outdoors is scenery in the Farmhouse. Understand "lawn" and "field" as the outdoors. The description of the outdoors is "Beyond a short stretch of clear ground, past the hammock, the Cornfield is thick and uninhabited for as far as you can see." Instead of searching the north window, try examining the outdoors. The description of the north window is "It gives a view of the cornfield and hammock."

The grape chandelier is scenery in the Farmhouse. "The glass grapes are grouped into bunches. The are purple and stretch along a long cast-iron frame."

An oriental rug is in the Farmhouse. "To one side of the room is [if the escape tunnel is not revealed]a large oriental rug.[otherwise]an escape tunnel." Understand "carpet" as the rug. The description of the oriental rug is "This beautiful oriental rug has a red hue and two piecing blue strips eminating out from the center like hands on a clock. Silver circles adorn the edges.".

Check looking under the rug: say "There's an escape tunnel under the rug." instead.
Check taking the rug: say "The rug is too heavy to lift." instead.
After pushing or pulling the rug for the first time: say "With a great effort, the rug is moved to one side of the room. With the rug moved, the dusty cover of an escape tunnel appears."; now the Escape Tunnel is revealed.
After pushing or pulling the rug: say "Having moved the carpet previously, you find it impossible to move it again."

The Kitchen is west of the Farmhouse. "A large kitchen with attached family room. A [italic type]zebra head[roman type] is mounted over a [italic type]river rock fireplace[roman type]."

The Zebra Head is scenery in the Kitchen. "The zebra head is mounted as you might expect a wild animal head might be mounted in a game lodge. Surprisingly, this one appears to have a smile.".

The River Rock Fireplace is scenery in the Kitchen. "The fireplace is made up of smooth, gray river rocks. The edges of the rocks protrude from the light colored morter. A stunning piece of art and craftsmanship in its own right.".

Old Cellar is a room below The Headquarters Entrance. "On the side of the house, two old-fashioned cellar doors angle off the ground. After humming a quick tune and smiling for the hidden cameras, the vault-like locks on the doors release. Heavy tempered steel doors swing open to reveal rickety steps leading down to an old brick and moss-lined cellar.[paragraph break]You cautiously head down into the cellar."

Paint cans are things in the Old Cellar. "Two old [italic type]paint cans[roman type] are here side-by-side like a set of dilapidated bongos."
The Paint cans are fixed in place.
Understand "cans" as Paint cans.
Understand "can" as Paint cans.
The description of the paint cans is "These look like they'd make drum sounds if you tapped on them.".

Set of Shelves is a lockable door. "[if location is old cellar]An old set of [italic type]shelves[roman type] is here, filled with [italic type]seashells[roman type].[else]You see the back of a set of shelves."
Set of Shelves is below the old cellar and above the Secret Passage. Set of Shelves is locked. The description is "A heavy set of shelves that looks like it might conceal something."

The Seashells are a scenery in the old cellar. "A beautiful selection of old conk shells, lightning whelks, alphabet cones and lion's paws."

Understand "shells" or "sea shells" as Seashells.
	
Tapping is an action applying to one thing. 
Understand "tap [something]" as tapping.
Understand "drum [something]" as tapping.
Understand "tap on [something]" as tapping.
Understand "drum on [something]" as tapping.
Understand "play [something]" as tapping.

Carry out tapping:
	if the noun is Paint Cans:
		say "You tap the top of the old paint cans with a rhythmic sequence[if the set of shelves is locked], causing the set of shelves to slide sideways, allowing you to pass down to a secret passage.[else]. But the shelves have already been opened";
		now the Set of Shelves is open;
		now the Set of Shelves is unlocked;
	otherwise:
		say "You play a paradiddle on the [noun]."
		
After going to the Secret Passage from the Old Cellar:
	try looking;
	say "The secret door locks behind you.";
	now the Set of Shelves is closed;
	now the Set of Shelves is locked;

Chapter 2 HQ

The HQ Area is a region. Secret Passage, Inner Sanctum, Conference Room, Martin's Office, Hallway, Medical Center, Hyperloop Station, Airstrip are in the HQ Area.

Understand "HQ" as Airstrip.

Secret Passage is a dark room below the Set of Shelves. "You are in a narrow secret passageway that appears to be carved out of stone. A short hallway is to the west."

Inner Sanctum is west of the Secret Passage. "After passing through two sets of automatic reinforced sliding doors, you walk into the bowels of [italic type]Quotient[roman type]. The interior is sleek and modern, contrasting sharply with the old-fashioned exterior. This antechamber is filled with [italic type]advanced technology[roman type], powered by its own solar farm and connected by a low earth orbit (LEO) satellite network. This is the nerve center of [italic type]Quotient[roman type], the highly secretive, fully independent spy agency.[paragraph break]A conference room is to the west."

Advanced Technology is scenery in the Inner Sanctum. The description of the Advanced Technology is "This is some serious tech. I'm not talking about a smartphone and a PC."

Quotient is scenery in the Inner Sanctum. The description of Quotient is "Quotient was founded by Martin Austin and is a global, independent spy agency. Their secret power is their Quantum Computer, known as TARDIS. It gives them amazing surveillance and intelligence capabilities by rendering most forms of encryption useless."

Conference Room is west of The Inner Sanctum. "A stark polished [italic type]stainless steel table[roman type] dominates the center of the room, surrounded by comfortable chairs. [italic type]Monitors[roman type] and [italic type]touch panels[roman type] line the walls. This is where the most critical missions are planned and executed.[paragraph break]Martin's office is to the north and a hallway is to the west."

The monitors are scenery in the Conference room. "The monitors show many different things. One shows a map of the world with markers on different points around the globe. The other shows a 24-hour news station. A third is streaming Rocket League; the car with the succulent on top just scored!"

The stainless steel table is scenery in the Conference room. "Highly polished and pristine. Otherwise, it's just a table."

The touch panels are scenery in the Conference room. "The touch panel's control things like monitors that drop from the ceiling, mood music, and many other spy-like things, but they only respond to Martin's touch, so they aren't much use to you.".

Instead of touching the touch panels the first time:
	say "The touch panels only respond to Martin's touch.";
	
Instead of touching the touch panels:
	say "Um, I think I told you this... The touch panels only respond to Martin's touch.";

Understand "monitor" or "mon" or "display" as monitors.

A chair is a kind of supporter. A chair is always enterable. 
A meeting chair is a chair. It is in the Conference Room. 
The description of the meeting chair is "It's a contemporary looking office chair."

Report entering the meeting chair:
	say "You sit in the chair and take in the view of the conference room. It's quite a room!"

Martin's Office is north of the Conference Room. "This is Martin's office. Martin is the founder of Quotient. The office contains a beautiful [italic type]teak desk[roman type] with a leather chair and has a built in [italic type]bookcase[roman type]. The room has a [italic type]coffered ceiling[roman type]. There is a [italic type]photo[roman type] on the wall."

After going to Martin's Office:
	try looking;
	if Martin is in Martin's office, say "[if aslan is magic]Congratulations Agent, you'c completed the mission![else if martin is unspoken]Martin smiles when you walk in. [paragraph break]'Welcome to the team. Your training mission involves two types of things. One is simply treasure hunting. This will earn points toward your rank. While we were setting up your training mission, a real mission came in. The Lion escaped and interfered with Cassie's time experiment. We'll need your help on this mission. All of our agents are already working it. You'll have to figure out the rest. HQ has been locked down for security reasons. When you are ready to leave, you'll need to leave by Hyperloop or by the Escape Hatch. Good luck.'[else]Martin looks up from his desk, 'Back so soon?'";
	if Martin is in Martin's office, now Martin is spoken;
		
A leather chair is a chair. It is in the Martin's Office. 
The description of the leather chair is "It's a sumptuous leather chair with thick memory foam padding, making it appear very comfortable."

The coffered ceiling is scenery in the Martin's Office. "The coffered ceiling has many ornate squares, each the size of a trap door."

The bookcase is scenery in the Martin's Office. "There is one book that catches your eye. [italic type]Dreamcatchers[roman type]."

Understand "built-in" or "builtin" or "shelf" as bookcase.

Report entering the leather chair:
	say "Oh yes, it is very comfortable. You swivel back and forth in the chair, pretending to be Martin."

The photo is scenery in the Martin's Office. "This photo appears to be of the hatch from the TV Show Lost. [italic type]Perhaps inspiration for a code?[roman type]"

Hallway is west of the Conference Room. "A stark hallway, with a modern flair and a captivating [italic type]abstract painting[roman type]. You can go south, east, or west."

The abstract painting is scenery in the Hallway. "It is a beautiful painting with bright teal and orange accents. Some say it reminds them of a skyline. Whether it is the skyline of a city or a type of chili is unknown."

Medical Center is south of The Hallway. "A facility for treating injuries and conducting medical research."

Hyperloop Station is west of The Hallway. "A sleek, futuristic station with a pod that can whisk agents to distant locations in minutes. The hyperloop connects to a secluded airstrip, ensuring rapid deployment for urgent missions."

Airstrip is a port. "This hidden airstrip is exclusively for Quotient. The Quotient Jet is housed here. It is the fastest non-military jet in the world."

Yard is north from The Headquarters Entrance. "A large yard with a hammock, [if bonfire is nowhere]and a place were a great bonfire once stood.[else] and a huge roaring bonfire.[end if][paragraph break]You can go west or south. A cornfield lies to the north." 

Hammock is an enterable scenery supporter in the Yard. "A hammock hangs between two trees."  

Instead of examining the hammock:
    say "A hammock! A great place to rest after sleuthing."

Report entering the hammock:
	say "Even super spies need rest."

Section 1 bonfire making

The bonfire is a thing. The bonfire is nowhere. "The roaring bonfire is at least 10 feet tall." Instead of pushing, pulling, turning, tasting, or touching the bonfire, say "The fire is too hot!" Understand "fire" as the bonfire.

The description of the bonfire is "The roaring bonfire is good protection against a cool autumn night, and a place to make s'mores!".

Instead of listening in the presence of the bonfire: say "All you hear are the reassuring snaps and cracks of the sticks in your fire." 

The flint is a thing.   
The description of the flint is "A flat grey stone with flaked edges." 
The steel striker is a thing. 
The description of the steel striker is "It is curved so you can wear it over the knuckles of your hand and strike it against the flint." 
The steel striker is wearable. 

pile of leaves is a thing in The Gate. The description of the pile of leaves is "Um, a pile of leaves, do you want to count them?" 

smore is a thing. the smore is nowhere. The scent of the smore is "Yum, melted chocolate and gooey marshmellow."
The description of the smore is "The golden brown marshmellow has been smashed into the melted chocolate with two graham cracker slabs, creating the perfect backyard campfire delicacy.".
Understand "s'more" as smore.

Instead of eating the smore:
	say "Oh, these are great. I used to eat them when I was a kid!"
	
Instead of attacking the flint when the steel striker is not worn by the player:
	say "You must wear the steel striker over your knuckles, in order to hit the flint at the best angle."
	
Instead of attacking the flint when location is not the Yard:
	say "The flint turns smooth in your hand and no sparks are created. A small voice comes from the flint. This ACME flint is GPS locked and only works by the old bonfire spot in the yard. Thank you for your cooperation.";

Instead of attacking the flint when location is the Yard and the steel striker is worn and the leaves are not in the location:
	say "Though you strike the flint sharply with the steel and throw sparks, they have nothing to catch on, since there is nothing to small enough to catch fire."

Instead of attacking the flint when the steel striker is worn and the leaves are in the location and the leaves are not unlit:
	say "The leaves have already caught fire."

Instead of attacking the flint:
	now the leaves are glowing;
	say "You strike the flint against the steel striker and throw sparks onto the leaves; they make tiny circles of orange there, which will only prosper if blown into flame."
	
Instead of burning the whole leaves:
	say "How?";
	
Instead of burning the leaves when the leaves are glowing:
	say "You need to blow on the leaves to get them flaming."

[Realistically, we ought to attach a randomization to this so that each step of the fire-starting has a good chance of failure. But because our player may not be as patient as someone who actually needs a fire started, we allow him to succeed the first time in every case.]

Ignition is a kind of value. The ignitions are whole, glowing, flaming. A thing has an ignition.

Blowing on is an action applying to one thing. Understand "breathe on [something]" or "blow on [something]" as blowing on.

Understand "blow [something]" as blowing on.

Instead of blowing on the leaves:
	say "There is no point, since no sparks have caught there."

Instead of blowing on the glowing leaves:
	now the leaves are flaming;
	say "You blow on the faint sparks on the leaves and turn them into the beginnings of flame."
	
Instead of blowing on the flaming leaves:
	now the leaves are flaming;
	say "The leaves are flaming already. It needs some easy to light fuel. Kindling perhaps?"

Some kindling is in the Yard.
The description of the kindling is "This would burn if you had a small fire to get it started.".

Instead of burning the whole kindling:
	if the pile of leaves is not flaming:
		say "You need some tinder, perhaps a flaming pile of leaves, first.";
	otherwise:
		now the pile of leaves is nowhere;
		now the kindling is nowhere;
		move the bonfire to the location;
		say "You succeed in lighting Desmond a proper bonfire. Congratulations. Unfortunately, this is only good for s'mores and points. It's not really a secret agent kind of thing, but Desmond is much happier.";
		say "[paragraph break]'Thanks! Help yourself to a s'more' says Desmond, smiling.";
		now the smore is in the yard;
		increase score by 2;
		
[----------End of bonfire making -------]


Smokehouse is west of the Yard. "An old ivy-covered smokehouse surrounded by a [italic type]bowling ball[roman type] garden. You can go east, west, or south. (or just type EXITS at anytime to see which way you can go.)"

The Shovel is an object. It is in the Smokehouse. "A rusty shovel is here. It looks like it would only work in sand."
The description of the Shovel is "Looks like an ordinary shovel to me.".

Bowling balls are scenery in the Smokehouse. "The bowling balls are many different colors."

Instead of taking the bowling balls:
	say "They are an integral part of the garden, and are just for your visual pleasure."

Understand "bowling ball" or "bowling ball garden" as Bowling balls.

Fire Pit is south of The Smokehouse. "[if the player was in Parking]You pass by the back of the house and stumble upon a fire pit surrounded by a meticulously maintained fairy flower garden. There is a small ornamental [italic type]fairy[roman type] in the garden.[paragraph break]You can go north or return south to parking.[else]A  fire pit is surrounded by a meticulously maintained fairy flower garden. There is a small ornamental [italic type]fairy[roman type] in the garden.[paragraph break]You can go back to the Smoke House to the north or you can go south.[end if]"

The Fairy is scenery in The Fire Pit. "The inanimate, ornamental fairy appears to be twirling around, dancing, and enjoying life."

Understand "ornamental fairy" as Fairy.

Old Barn is a dark room west of The Smokehouse. "An old barn is here. The old planks are dangerous, worn from much dancing."

Rule for printing the description of a dark room when the player is in Old Barn: 
	say "The old barn is very dark inside. A small sliver of light peeks through the roof and splinters into small slivers providing little illumination. A flashlight would help." instead.

A Rickety Chair is a chair in the barn. "An old rickety chair."
The description of the Rickety Chair is "An old rickety chair, but it will probably hold you.".

Chapter 3 Cornfield

The Cornfield_a is a region. Corn Maze, Clearing, Dead End, Hidden Path, Secret Path, Cemetery are in the Cornfield_a..

Corn Maze is a room. "You stand at the entrance of a vast cornfield maze. [italic type]Tall stalks[roman type] surround you on all sides, you can go east or north into the corn."

Tall Stalks are scenery in the Corn Maze. The description of the Tall Stalks is "It's a Maize Maze... get it? The stalks are normal cornstalks."

Clearing is a room. "You find yourself in a small clearing, a brief respite from the maze of cornstalks.[paragraph break]You can go north, south, or west."

Dead End is a room. "The path leads you to a dead end. [paragraph break]The only way is to turn back."

Hidden Path is a room. "A narrow, hidden path reveals itself. It seems to lead deeper into the cornfield.[paragraph break]You can go east or west."

Secret Path is a room. "A narrow, hidden path reveals itself. It seems to lead deeper into the cornfield.[paragraph break]You can go, east, west or south."

Cemetery is a room. "You've made it through the cornfield and reached the old cemetery. [paragraph break]You can exit the way you came, or maybe to the west."

Tombstones are a thing in the Cemetery. They are fixed in place. "There are many old worn [italic type]tombstones[roman type] here."
The description of the Tombstones is "The tombstones are so old, they are almost worn past recognition. One of them reads Zimrod Flathead, 1799-1893.".
The read text of the tombstones is "Zimrod Flathead, 1799-1893."

Before going west from the Hidden Path:
	if the player is not wearing the cold suit:
		say "You will set off the infrared sensors if you go this way." instead.

Before going north from the Yard:
	if the player is not wearing the cold suit:
		say "You will set off the infrared sensors if you go this way." instead.
	
Section 1 Maze

A maze room is a kind of room. All Maze rooms are in the Cornfield_a.
The printed name of a maze room is usually “Maze”.
The description of a maze room is usually “A twisty maize maze of passages all alike.[line break]( with lots of [italic type]cornstalks.[roman type] )”.

There is a maze room called Maze1.
Maze1 is east of Corn Maze.
Cornstalk1 is scenery in Maze1. "These are REALLY tall cornstalks. They almost block out the sun."
A maze room called Maze2 is east of Maze1.
Cornstalk2 is scenery in Maze2. "These are REALLY tall cornstalks. They almost block out the sun."
A maze room called Maze3 is north of Maze1.
Cornstalk3 is scenery in Maze3. "These are REALLY tall cornstalks. They almost block out the sun."
A maze room called Maze6 is north of Maze2.
Cornstalk6 is scenery in Maze6. "These are REALLY tall cornstalks. They almost block out the sun. There is a strange looking cob on one of the stalks."

A maze room called Maze4 is west of Secret Path.
Cornstalk7 is scenery in Maze4. "These are REALLY tall cornstalks. They almost block out the sun."
A maze room called Maze5 is west of Maze4.
Cornstalk8 is scenery in Maze5. "These are REALLY tall cornstalks. They almost block out the sun."
A maze room called Maze7 is west of Maze5. Maze5 is west of Maze7.
Cornstalk9 is scenery in Maze7. "These are REALLY tall cornstalks. They almost block out the sun."

Cob is a container. It is part of Cornstalk6. The Cob is closed. The description of Cob is "The cob is stuck to the tall stalk. It appears to be a container of some type."

Understand "corncob" as Cob.
Understand "corn cob" as Cob.

The printed name of Cornstalk6 is "the cornstalk".

The paring knife is in the Kitchen.
The description of the paring knife is "It's a small paring knife. It is very sharp.".

Maze2 is north of Maze3.
Maze2 is north of Maze6.
Maze3 is west of Maze6.

Instead of opening the Cob:
	say "It's tightly shut. You might need to PRY it open with something."
	
Understand "open cob with [something]" as a mistake ("You need to PRY COB with something, not open it.").

Prying it with is an action applying to two things. Understand "pry [something] with [something]" as prying it with.

Understand "pry open [something] with [something]" as prying it with.

Check prying it with:
	if the noun is not the cob:
		say "That doesn't seem to need prying." instead;
	if the second noun is not the paring knife:
		say "You can't pry it open with that." instead.

Carry out prying it with:
	now the Cob is open;
	say "With a bit of effort, you manage to pry the fake corn cob open with the paring knife."

Report prying it with:
	say "The corn cob creaks open...";
	say "and something drops out of the cob.";
	now the ancient scroll is in Maze6.
	

[Maze End]

	
Chapter 4 Oxford

Understand "Oxford" as the Hidden Airbase.

The Oxford University is a region. The Hidden Airbase, Broad Street, Sheldonian Theatre, Clarendon Building, Bodleian Library, Radcliffe Square, Radcliffe Camera, Duke Humfreys Library, Divinity School, Convocation House, Cassie's Office, Cassie's Lab, The Turf Tavern, Vaults_Garden, Narnia Lamp, Virtual Reality Lab are in the Oxford University.

Hidden Airbase is a port. It is north of Broad Street."A secret airbase west of Oxford. [paragraph break]Broad Street is to the south."

Broad Street is a room. "You are standing on Broad Street. [paragraph break]To the south, you can see the Sheldonian Theatre, and to the east is the Clarendon Building."

Sheldonian Theatre is a room. "Home to all public ceremonies at Oxford University, it was designed by Christopher Wren and built between 1664 and 1669. Wren was a professor of astronomy at the time but managed to design a marvel of a building. It was a D-shaped setup, modeled after Roman theaters. A [italic type]truss[roman type] spans the entire theater. It was named after Gilbert Sheldon, the Archbishop of Canterbury, and its primary financial backer. A [italic type]beautiful tapestry[roman type] hangs on one wall. [paragraph break]Exits are north, south and east."

A truss is scenery in the Sheldonian Theatre. "The truss was designed by Christopher Wren to span the seventy feet necessary to put the entire theater under roof without supporting columns which could spoild the view."

Understand "theater" as Sheldonian Theatre.
Understand "Sheldonian Theater" as Sheldonian Theatre.

Clarendon Building is a room. "This is a neoclassical building completed in 1715 to enable moving the University Presses out of the basement of the Sheldonian Theater. They couldn't work while plays were in progress, hence the necessity of the move.[paragraph break]As for your movement, You can go east or west."

A piece of paper is a thing. The piece of paper is in the Clarendon Building. "There is a piece of paper on the floor."
The description of the piece of paper is "It's just a slip of paper, but it does appear to have some writing on it.".
The read text of the piece of paper is "I hope you are enjoying your journey. Welcome to Oxford."

Bodleian Library is a room. "You are at the entrance of the Bodleian Library, one of the oldest libraries in Europe. The door at the entrance of the Bodleian is adorned with coats of arms of many of the colleges.[paragraph break]To the south is Radcliffe Square, to the west, The Divinity School. North leads to the Sheldonian Theatre. You can enter the old Duke Humfrey's library to the east."

Radcliffe Square is a room. "You are in Radcliffe Square. The sun is beaming and the sky is unusually blue for Oxford. [paragraph break]The exquisite Radcliffe Camera, or [italic type]radcam[roman type] stands to the south, the Bodleian Library to the north, and Convocation House to the east."

Radcam is scenery in Radcliffe Square. "The exquisite Radcliffe Camera"

Figure of radcam_image is the file “radcam.png”

Instead of examining the radcam:
	Display the figure of radcam_image;
	say "A magnificent circular building of Taynton stone."

Radcliffe Camera is a room. "Called the Radcam for short, this is a centerpiece of Oxford. Its beautiful orange-brown Taynton stone almost glows in the morning sun. The majestic circular building, ensconced with Corinthian columns and topped by a high lead-covered dome and [italic type]cupola[roman type], was built between 1737 and 1749 in the grand Palladian style.[paragraph break]You are now inside this magnificent building which serves as a reading room for the Bodleian Library. You can exit south, southwest or north."

The cupola is scenery in the Radcliffe Camera. "The cupola is the dome on top of the dome. The dome itself is the third largest in Britain."

Duke Humfreys Library is a room. "Duke Humfreys Library, with its ancient manuscripts and historic reading rooms, is a treasure trove of knowledge. It is also stunning to look at, but please refrain from making too much noise, or sneaking too many photographs."

The photographs are scenery in the Duke Humfreys Library. 

Instead of taking photographs:
	say "A red siren goes off, The librarian rushes over, whispering, 'You can't take photos here!'"

Divinity School is a room. "A magnificent medieval building used for university ceremonies and lectures. The ceiling has elaborate lierne vaulting with 455 bosses, almost like stone fireworks bursting over your head. This is where the hospital scene in the Harry Potter movies was filmed.[paragraph break]You can go east or south."

A hospital bed is an enterable supporter. The hospital bed is in the Divinity School. "A hospital bed, left over from the Harry Potter movies is in the corner."
The description of the hospital bed is "It's a classic hospital bed, propped such that it's patient could read.".

Report entering the hospital bed:
	say "To imagine that Harry Potter was here!"
	
Lying down is an action applying to nothing. Report lying down: say "You lie down for a while in the middle of the space, wondering about the point of existence, then get up again."

Lying near is an action applying to one thing. Report lying near: say "You lie down next to [the noun] for a while, mumbling to yourself."

Understand "beneath/under/by/near/beside/alongside/against" or "next to" or "in front of" as "[beside]".
Understand "on/in/inside" or "on top of" as "[within]".
Understand "lie down" as lying down.
Understand "lie down [within] [something]" as entering.
Understand "lie [beside] [something]" or "lie down [beside] [something]" as lying near.

Cassie's Office is a room. "Cassie’s office reflects her personality. It is a cross between a free-spirited, nature-lover’s reading room, with plants and a [italic type]hanging hammock[roman type] chair, and the office of an eccentric science genius. It includes what looks like a laser-guided [italic type]water cannon[roman type] automatically spraying water at the plants.[paragraph break]A door leads to her lab to the north, another room is to the east."

An old chalkboard is a thing. It is in Cassie's office. "Mysterious equations fill an [italic type]old chalkboard[roman type] in the corner."
The old chalkboard is fixed in place.
The description of the old chalkboard is "It is filled with mathematical equations. One catches your eye, perhaps you can read it?".
The read text of the old chalkboard is "Sqrt(Pi) = Gamma f(1/2)".
Understand "chalkboard" as old chalkboard.

A water cannon is scenery in Cassie's office.
The description of the water cannon is "Everytime you look at the water cannon, it sprays you in the face!".

The hanging hammock chair is an enterable scenery supporter in Cassie's office.
The description of the hanging hammock chair is "A free swinging handwoven cotton hammock chair with oak handles.".

Report entering the hanging hammock:
	say "Wow... what a great chair, I could read here for a long time."

The Virtual Reality Lab is a room. The Virtual Reality Lab is east of Cassie's office. "You are in the Virtual reality lab."

A metal sign is a thing in the Virtual Reality Lab. "A metal sign is mounted on the wall."
The metal sign is fixed in place.
The read text of the metal sign is "Caution! VR Simulations can be very lifelike.".
The description of the metal sign is "The sign reads, Caution! VR Simulations can be very lifelike.".

The VR Suit is a wearable device in the Virtual Reality Lab. The description is "A sleek, futuristic Virtual reality headset, not much bigger than a normal set of eyeglasses as well as something that looks like a vest and gloves." The VR Suit can be activated or unactivated.

Instead of taking off the VR Suit when the VR Suit is activated:
	say "You try to remove it, but you can't even tell you have it on.";
	stop the action;

Rapid blinking is an action applying to nothing. 
Understand "blink rapidly" or "blink eyes rapidly" or "blink eyes" or "blink eye" or "blink" as rapid blinking.
Understand "wink rapidly" or "wink eyes rapidly" or "wink eyes" or "wink eye" or "wink" or "rapid eye blinking" as rapid blinking.

Carry out rapid blinking:
	say "You blink rapidly.";
	if the player is wearing the VR Suit:
		say "The VR suit is deactivated and the you return to reality.";
		now the VR Suit is unactivated;
		now the VR Suit is switched off;
		now the player is in the previous location.

Cassie's Lab is a room. "Cassie built the quantum mechanics and time continuum lab in 2008. She likes to call it the Continuum. She likes to joke she will be studying time here for the rest of her life. Lasers are shooting across the room. There is a [italic type]lab bench[roman type] with a device on it. A [italic type]countertop[roman type] holds containment vessels and other equipment."

The countertop is scenery in Cassie's Lab. It is a supporter. "The countertop has water stains and an occasional burn on it. It is filled with [italic type]equipment.[roman type]"

The equipment is scenery on the countertop. "Delicate scales and glass test tubes, lasers, vacuum vessels, and other interconnected things you do not recognize." 

Convocation House is a room. "The Convocation house is primarily used for events. It was once used as a meeting place for the House of Commons. Its stone vaulted ceiling dates back to 1758-1759.[paragraph break]You can go north or east."

The Turf Tavern is a room.  "[if the player was in Cassie's Office]You enter the 12th Century inn, Turf Tavern, from the front. It is very popular with Oxford Students.[paragraph break]You can go northeast or west.[otherwise] You head down New College Lane and shuffle past the tourists posing for pictures under the Hertford Bridge, or the Bridge of Sighs, so called for its similarity to the famous bridge in Venice. After passing under the bridge, you ducked into the very narrow alleyway known as St. Helen’s Passage. It is marked by a green street sign affixed to the wall, or you could easily miss it. Following the winding cobblestone passage leads you to the entrance of the pub where many famous alumni have enjoyed a drink or dinner. They built the Turf Tavern just outside the city walls in order to avoid jurisdiction of the colleges. It was a fitting place to discuss Quotient business.[paragraph break]You can go northeast or west."

The Black Painted Board is a thing. It is in the Turf Tavern. It is fixed in place. "A traditional black painted board is mounted on a post and is filled with writing."
The description of the Black Painted Board is "It looks like a chalkboard upon which they have painted many words.".
The read text of the Black Painted Board is "Whilst here at Oxford University ---- in 1963 --- At the Turf Tavern, Bob Hawke, Former Austrailian Prime Minister entered the Guinness Book of Records after downing a Yard of Ale in 11 seconds.".
Understand "board" as black painted board.

Vaults_Garden is a room. "Vaults and Garden, is a beautiful cafe in the University Church of St. Mary the Virgin. There are outdoor tables here where people are enjoying some warm scones, raspberry jam, and traditional English tea . The smell of the fresh-baked scones is enchanting. [paragraph break]You can go west or north.”
The printed name of Vaults_Garden is "Vaults and Garden's".

Narnia Lamp is a room. "You stumble across a Narnia lamp post near an oaken door. The oaken door is framed with two golden fauns. An inspirational stop for C.S. Lewis. This is a dead end."

Aslan is a thing. Aslan is fixed in place. Aslan is in Narnia Lamp. Aslan can be magic or notmagic. Aslan is notmagic. "Aslan is a lion carved in the oaken door. It has a mystical glow."
The description of Aslan is "Aslan is a wood carving of a Lion, but it is glowing.".
Understand "lion" as Aslan.
Understand "carving" as Aslan.
Understand "wood carving" as Aslan.
	
Instead of rubbing Aslan:
	say "You make a paper rubbing of Aslan. A nice souvenir, but I suggest you just try touching it."

Instead of touching Aslan:
	if Quantum Console is activated and the Quantum Computer is rebooted and the laser is energized and pendant_event_triggered is 1: [you win]
		say "[paragraph break][bold type]You feel faint as some ancient magic swirls around you.[roman type][paragraph break]";
		now Aslan is magic;
		move jet to Andrews Air Force Base;
		move scroll of treasure to The National Mall;
		move player to The National Mall;
		now the description of the sky is "It is a perfect day in DC. The sun is shining, the blue birds are singing and the sky is bright blue!";
		increase score by 107;
	else if Quantum Console is activated and the Quantum Computer is rebooted and the laser is energized and pendant_event_triggered is 0:
		say "The lamp post flickers gold and the glow pulsates brightly, but otherwise, you feel untouched."; [pendant is not shiny]
	else if Quantum Console is activated and the Quantum Computer is wonky and the laser is energized and pendant_event_triggered is 1: [computer is not rebooted]
		say "The lamp post flickers and makes a beeping sound like a computer before Aslan stops glowing.";
	else if Quantum Console is unactivated and the Quantum Computer is rebooted and the laser is energized and pendant_event_triggered is 1: [console is not active]
		say "The lamp post flickers and Aslan glows brightly, but without the quantum code, nothing happens..";
	else if Quantum Console is activated and the Quantum Computer is rebooted and the laser is notenergized and pendant_event_triggered is 1: [laser not active]
		say "The lamp post flickers red and the glow from Aslan dims, but the sky is unwavering.";
	otherwise:
		say "You can feel the wood, smooth, worn from the ages. The glow pulsates as you touch it."
		
Section 1 - Locked Lab

The wooden door is a door. The wooden door is north of the Cassie's Office and south of the Cassie's Lab. The wooden door is lockable and locked.

A keycard is a thing. 
The keycard is in the Secret Path.
The keycard unlocks the wooden door.
The description of the keycard is "It's a keycard. It says Continuum Lab and has the Seal of Oxford University embossed over the center of the card.".
The read text of the keycard is "Desmond Silver, Continuum Lab Access".

Instead of examining the wooden door:
    say "The wooden door is sturdy and a card reader is mounted on the wall next to it."

Instead of opening the wooden door when the wooden door is locked:
    say "The door is locked. You need a keycard to open it."

Instead of unlocking the wooden door with the keycard:
    say "You hold the keycard upto the card reader and a laser shoots out of the wall and scans your retina. The door clicks open.";
    now the wooden door is unlocked.
		
Section 2 Old Safe, Gold Key, and Intelligence Papers 
		
The old safe is an enterable closed openable container. The old safe is in the Duke Humfreys Library. It is lockable and locked. The description is "An old safe is here. It has a cylindrical key hole."

The old safe is fixed in place.

The gold key is a passkey. It unlocks the old safe. 
The description of the gold key is "The key is a unique shape. It has a cylindar barrel with various bumps and protrusions."
The gold key is in the treat cupboard.

The intelligence papers are in the Old Safe. Understand "intel" or "paper" or "records" or "record" as the intelligence papers. The description is "These records are some old intelligence papers on The Quantum Contingent and Quotient. You should be able to consult these on different topics. It might give you some background that is useful.".
The read text of the intelligence papers is "You may consult these intelligence papers about many topics. 'consult intel about TOPIC'"

Instead of taking the papers:
	say "The intelligence papers must stay here, but you may consult them while you are here."
	
Understand "consult [something]" as a mistake ("You need to specify what you want to consult the noun about. Try 'consult intel about topic'.").

Instead of consulting the papers about a topic listed in the Table of History: 
	say "[reply entry][paragraph break]".

Understand "consult" or "look up" or "read" as "[search term]".

After reading a command:
	if the player's command includes "[search term]":
		while the player's command includes "the":
			cut the matched text;

Table of History
topic	reply
"lion/Robert/Clayton" or "Robert Clayton"	"Robert Clayton, the key operator of the Quantum Contingent Operations center is an avid outdoorsman. He recently escapted the Abyss prision in Gore Ethiopia. His last known whereabouts were Africa."
"crystal" or "Kyber Crystal"	"The Crystal or Kyber Crystal is of unknown origin. It contains immense power that can only be harnessed with the right type of glass enclosure. Cassie Eaton was experimenting with it in her lab until it was recently stolen."
"frank"	"Frank Sutherbee was thought to have been a member of the Quantum Contingent. (That's the Frank you were asking about, right?)"
"Quotient"	"Quotient is a global independent spy agency. Their headquarters is hidden somewhere in the United States. They appear to have powerful ties to the current US Administration, specifically President Bridget O'Connor."
"Quantum Contingent" or "Contingent"	"The Quantum Contingent is a criminal organization whose members attempted to become immortal, and was willing to blackmail the world superpowers using their own Quantum Computer based on Quotient's computer design. They were stopped by Quotient."
"lancelot"	"Our intel suggests that Lancelot existed, and during his time, he had a pendant and a crystal which gave him special powers. This is believed to be the same crystal Professor Cassandra Eaton has been using in time experiments."
"prof/professor/cassandra/cassie/eaton"	"Professor Cassandra Eaton is a distinguised Professor of Quantum Studies at Oxford. She has been working on understanding the dimension of time for almost three decades."
"professor cassandra eaton" or "professor eaton"	"Professor Cassandra Eaton is a distinguised Professor of Quantum Studies at Oxford. She has been working on understanding the dimension of time for almost three decades."
"pendant"	"The mysterious pendant of lancelot has been lost. It was believed to have surfaced at Oxford in 1730, but has not been seen since. Some consider it's existance nothing more than a myth."
"Tardis/computer" or "Quotient Computer"	"The Quotient Computer is housed in a boxy space station and is affectionately called TARDIS. This is due to the space station's resemblance in shape to a 1960s London police box like Dr. Who’s time machine, only much larger. Quotient even painted it TARDIS blue. The Quotient Computer is, by far, the most powerful quantum computer in the world, and is capable of breaking almost any encryption, giving Quotient a huge advantage in the spy game.[paragraph break]The quantum computer is in the space station because space is: [paragraph break]
[bold type]Cold[roman type]: Space is about 2.7 kelvin or -270 degrees Celsius, only slightly above absolute zero, and for a good quantum computer, you need to get as close to absolute zero as possible.[paragraph break]
[bold type]Still[roman type]: It seems strange to think of space as still. After all, the international space station is traveling around the earth at almost five miles per second, or about 15,500 miles per hour. However, with no gravity, no earth tremors, and no air, it turns out you can build a suspension system on an orbiting space station perfect for cradling a quantum computer.[paragraph break]
[bold type]Secure[roman type]: Only the richest individuals can afford private space travel, so the risk of a physical security attack is low. "
"me/myself"	"You are the merest interloper here."
"martin"	"Martin is the head of Quotient. Quotient was believed to have been founded out of a quantum computing effort Martin began many years ago. He has many connections in government and pulled Jack Alexander in to help with their quantum efforts. It is believed they successfully created a very powerful quantum computer, but it's existance is still unconfirmed."
"kara"	"Kara worked at Nasa Glenn Research Center in Cleveland and is an expert on quantum networking. She has romantic ties to Jack Alexander and is now suspected to be a full fledged agent of Quotient."
"sarah"	"Sarah Alexandar was believed to be recruited to Quotient when she was at Oxford. She is a perfect candidate due to her knowledge of multiple languages and her natural athleticism. She was trained by Florin Ardelean."
"florin" or "florin ardelean"	"Florin Ardelean is former MI-6 and the European lead agent for Quotient. He trained Sarah Alexander-Silver when she was at Oxford."
"desmond"	"Husband of Sarah, one of quotient's top agents. Desmond is not believed to be part of quotient. He is currently CEO of a LEO satellite company."
"Jack" or "Jack Alexander"	"Jack was the original designer of the Quotient Quantum Computer and is now assumed to be an agent for Quotient."
"Bridget" or "President"	"President Bridget O'Connor has deep connections to the intelligence agencies having once run the NSA. It is assumed she has some knowledge of Quotient's capabilities."
"Jason Stephenson" or "Prime Minister" or "Jason"	"Jason Stephenson is our Prime Minister."
"Cassie" or "Professor Eaton"	"Cassie seems to have a past personal relationship with Martin. She is the world's foremost expert on quantum mechanics and time."
"light saber" or "saber/sabre" or "light sabre"	"Scientists have been working on creating a light saber using a crystal, but it's energy has been hard to contain."
"mt dew" or "mtn dew" or "Mountain Dew"	"A powerful liquid subject, often ingested by agents for energy, or just because it tastes good."
"metaverse"	"Professor Cassie Eaton has a VR lab where she is experimenting with simulating different versions of the future. She is our foremost expert on time."	

Chapter 5 Metaverse 

The VR Area is a region. The Metaverse, Dystopian 2049, Utopian 2049, Future National Park and Peaceful Meditation are in the VR Area.

The Metaverse is a room. "The most obvious clue that you are in the metaverse is that there are an unusually high number of mythical creatures here. (like the centaur that just walked by) People enjoy enhancing their avatars into creatures that don’t inhabit the real world. If you see a unicorn walking by or a dragon flying overhead, you are certainly in the metaverse.[paragraph break]If your host could keep these anomalies out and worked to match the real world, you could live your life trapped on a false Earth. A version of Earth in the metaverse outside of your control. There are different visions of the future here. Dystopian 2049 is to the east, Utopian 2049 is to the west, a future National Park is to the north, and a Peaceful Meditation Scene is to the south."

Dystopian 2049 is east of the Metaverse. "The difference between a world where people are totally addicted to their metasuits and the 1999 movie, The Matrix, would be that people would voluntarily spend many, if not most, of their waking hours in a pod that removed them from reality. There wouldn't need to be a digital rebellion to put them there.[paragraph break]The metaverse was the device that philosopher Robert Nozick referred to as an 'experience machine' in the thought experiment from his book Anarchy, State and Utopia. A machine that would allow us to only experience bliss. Robert believed that most people would decide to not plug in since they would know the experience was not real. In this dystopian vision of 2049, Robert was mistaken. Perhaps he underestimated how dystopian 2049 would become.[paragraph break]In this idea of the future, augmented reality glasses became as common as smartphones in the 2020s. As a result, society morphed. Buildings were bland cubes. The beauty was only viewable through the AR glasses almost everyone wore. The same was true for clothing. People simply designed virtual wardrobes, and their glasses emitted a tag corresponding to which outfit they wanted people to see. A quick touch to the temple of their AR glasses and their outfit morphed. If you were to remove your AR glasses, everyone was walking around in black spandex, past plain white buildings. It made the AR effect work better, and it was comfortable.[paragraph break]Homes didn’t need landscaping; it was added by the AR glasses. Holiday decorations were easy. You could morph your home into a scary haunted house for Halloween or Santa’s workshop for Christmas.[paragraph break]This changed the physical world immensely. There was no need for street signs or lane markings on roads. The remaining signs were fading and in disrepair. Now, all signage, labeling, and advertising were augmented on top of the physical world. And it wasn’t just advertising and directions. The real world was becoming bland. AR eliminated the need for physical decorations, beautiful color schemes, or landscaping. The beauty of new city developments was all provided through augmentation.
If you were one of the few that still wandered into a physical store, there were no labels. The QR code on the plain cardboard box caused the box to transform into an interactive ad for the product when viewing the product through your AR glasses. With no glasses, it was a shelf full of brown cardboard boxes with QR codes. With AR glasses, it was a shelf of gleaming digital icons, floating videos, and interactive product displays. It was hard to live and work in this world without using AR.[paragraph break]The design of the physical world was disappearing into the AR facade. Why make anything look particularly good if you could just slap a coat of AR paint on it instead? Textures and colors gave way to black (clothing), white (buildings), and specific Pantone colors for different types of objects. All cars were a specific shade of brown. It was easier to paint the AR world on top of this strange, bland, monotone world that had blossomed. The world was now one giant green screen. At least when you weren’t fully inside the metaverse.[paragraph break]In this projected future, dependence on AR led to a dramatic increase in the use of Virtual Reality in society. In 2049, people used VR the way people used TV earlier in the century. It was the primary form of home entertainment. Just as many of the prior generation fell asleep to TV, people often just fell asleep in their virtual world, happy to be lying in a fantasy world of their own creation, away from what was becoming a more and more bland physical world, and away from the connection of physical human relationships. This was the real downfall of dystopian 2049.[paragraph break]Of course, you might just end up sleeping here if you can't get that suit off."

Utopian 2049 is west of the Metaverse. "Technology isn't inherently bad. It has to do with how we use it. In this projected future, AR still existed, but it was only to augment the beauty of the real world, not replace it entirely. It made you less likely to peer at a sporting event through the lens of your camera. Instead, you could watch the event and still have some brief real-time statistics appear in the lower left hand corner of your field of vision. In Utopian 2049, the metaverse was here too. In this scenario, it was great for enhancing solo activities like working out, or playing video games with your far flung friends on the internet, but people still wanted to come back to their real world friends and live in the real world.[paragraph break]Too bad you might never be able to return!"

Future National Park is north of the Metaverse. "For the few remaining Luddites that shunned this strange new metaverse world of the dystopian future scenario, at least they picked up a few advantages. The world’s most beautiful outdoor spaces weren’t overcrowded anymore. Most just visited these sites virtually.[paragraph break]You are now in the heart of a grove of Redwoods. They tower over you. Making you feel peaceful.[paragraph break]Maybe this virtual world isn't so bad?"

Peaceful Meditation is south of the Metaverse. "You are in a vast desert, with dunes stretching out to the horizon. The sun burns like a hot coal in the sky. You may meditate here as long as you like.[paragraph break]And that could be a very long time if you can't figure out how to exit."

Hologram is a person. Hologram is in the Metaverse. "A hologram floats near by."
The description of the Hologram is "The hologram looks exactly like Cassie, only slightly transparent. Only her piercing blue eyes lake the transparency of the rest of her.".

Section 1 - Yoga Master and Hologram

Yoga Master is a person. Yoga Master is in Peaceful Meditation. "A serene yoga master appears, floating just a few inches above the sand in the lotus position."
The description of the Yoga Master is "They are a serene yoga master, and they appear to be floating just a few inches above the sand in the lotus position with their eyes closed.".

Instead of talking to Yoga Master:
		say "The Yoga Master simply breaths out. And then in. Their breathing is slow and rhythmic. They do not even open their eyes when you talk to them."

Yoga Mat is a thing. It is nowhere. "A beautiful yoga mat is here. Its color oscillates gently."
The description of the Yoga Mat is "it is a large yoga mat, thicker than most. It is soft, but not in a rubbery way, it feels more like lying on a thick bed of pine needles, covered with cloth. It's color is changing constantly.".

Meditating is an action applying to nothing. Understand "meditate" as Meditating. 

Check Meditating:
if the player is not in the Peaceful Meditation, say "You feel a calm sense of security." instead;

Carry out Meditating:
	say "'[line break]Ahhhh, welcome to Peaceful Meditation,' says the normally silent Yoga Master. 'Take a deep cleansing breath. Your anagram for the day is: The Oldest Log.'[line break]";
	now the Yoga Mat is in the Peaceful Meditation.

Instead of talking to Hologram:
        say "I suspect the metaverse will be almost indistinguishable from reality by 2049. Standard metaverse gear will need simple safeguards to help you determine if you were in real space or meta space. For example, [bold type]rapid eye blinking[roman type] could be an industry standard for ‘get me out of here.’ All meta suits would then back you to the real world instantly if rapid eye blinking occurred. Also, every avatar will have an identifying mark on the bottom of their left foot/shoe/appendage that shows their metaverse id. Kind of like Woody in Toy Story.[paragraph break]Of course, hackers will alter metaverse programs to remove both safety features. With no blinking-eye exit, and no id mark, it would be hard to tell what is real and what is meta. With the appropriate setup, you could trap a person in the metaverse, and make them think it’s real."

After asking the hologram about a topic listed in the Table of Cassie_AV Replies, say "[line break][muse entry][line break]".

After asking the hologram about something:
	if the topic understood is not a topic listed in the Table of Cassie_AV Replies:
		say "[line break]The hologram of Cassie floats away.[line break]"

[We can then provide a simple table giving the hologram of Cassie's responses:]

Table of Cassie_AV Replies
Topic	Muse
"mission"	"Reboot the computer and get to the Continum"
"the mission"	"Reboot the computer and get to the Continum"
"computer"	"Tardis is our computer. It is in space."
"tardis"	"Tardis is our computer. It is in space."
"Jack"	"Jack is very special to me."
"Sarah"	"Sarah is like a daughter to me."
"Kara"	"Kara really proved herself during The Quantum Contingent Mission"
"Florin"	"Florin is a very capable agent, and a long time friend."
"President"	"President Bridget O'Connor is one of the finest women I have ever known."
"Prime Minister"	"Prime Minister Jason Stevenson would make a great Quotient Agent."
"Bridget"	"President Bridget O'Connor is one of the finest women I have ever known."
"Jason"	"Prime Minister Jason Stevenson would make a great Quotient Agent."
"Greg"	"I heard he's an author?"
"Martin"	"I love Martin, don't you?"
"Karen"	"Did you mean Kara or Cassie?"
"Frank"	"Frank is a villian in the book, but in real life he's a great guy!"
"chili/skyline/skyline/scone/pretzel"	"Yum! Delicious."
"crystal"	"The Crystal should be able to provide the power we need for the quantum console."
"skyline chili"	"Yum! Delicious."
"dew/Pimms/beer/water/pop"	"Refreshing!"
"metaverse/vr/exit"	"I suspect the metaverse will be almost indistinguishable from reality by 2049. Standard metaverse gear will need simple safeguards to help you determine if you were in real space or meta space. For example, rapid eye blinking could be an industry standard for ‘get me out of here.’ All meta suits would then back you to the real world instantly if rapid eye blinking occurred. Also, maybe every avatar will have an identifying mark on the bottom of their left foot/shoe/appendage that shows their metaverse id. Kind of like Woody in Toy Story.
Of course, hackers will alter metaverse programs to remove both safety features. With no blinking-eye exit, and no id mark, it would be hard to tell what is real and what is meta. With the appropriate setup, you could trap a person in the metaverse, and make them think it’s real. "
"the metaverse"	"I suspect the metaverse will be almost indistinguishable from reality by 2049. Standard metaverse gear will need simple safeguards to help you determine if you were in real space or meta space. For example, rapid eye blinking could be an industry standard for ‘get me out of here.’ All meta suits would then back you to the real world instantly if rapid eye blinking occurred. Also, maybe every avatar will have an identifying mark on the bottom of their left foot/shoe/appendage that shows their metaverse id. Kind of like Woody in Toy Story.
Of course, hackers will alter metaverse programs to remove both safety features. With no blinking-eye exit, and no id mark, it would be hard to tell what is real and what is meta. With the appropriate setup, you could trap a person in the metaverse, and make them think it’s real. "

		
Chapter 6 Cincinnati 

Understand "Cincinnati" as the Lunken Airport.
Understand "CVG" as the Lunken Airport.

The Cincinnati Area is a region. The Lunken Airport, Cincinnati Museum Center, The Cavern, IMAX Theater, and Skyline Chili are in the Cincinnati Area.

Lunken Airport is a port. It is east of Cincinnati Museum Center. "A small private airfield close to downtown Cincinnati. The 1000 acre airfield was the largest municipal airfield in the world when it opened in 1925.[paragraph break]From here you can head west to the Cincinnati Museum Center."

Cincinnati Museum Center is north of Skyline Chili. "This extravagant art déco passenger train station was built between 1929 and 1933 and has carvings of the [italic type]Greek God Mercury[roman type] on each side of its immense arched facade. It was the inspiration of the Hall of Justice in the DC comic universe. (So, I guess this means you are a DC hero instead of Marvel) The dramatic ceiling over the [italic type]rotunda lobby[roman type] is awe-inspiring. The one hundred and six feet tall, one hundred and eighty feet wide, half dome entrance is the second largest in the world, behind only the Sydney Opera House.[paragraph break]There is a Skyline Chile south of here. The IMAX Theater is to the west. A staircase leads down."

The Greek God Mercury is scenery in the Museum Center. "The tall carvings stand guard on each side of the massive dome."

Figure of cmc_image is the file “cmc.png”

Instead of examining the Greek God Mercury:
	Display the figure of cmc_image;
	say "The tall carvings stand guard on each side of the massive dome which was the inspiration for the DC Universe Hall of Justice."

The rotunda lobby is scenery in the Museum Center. "The magnificent golden ceiling is awe-inspiring. The glass mosaic murals around the sides are by Winold Reiss. They depict a timeline in America and a timeline in Cincinnati. Reiss's art eventually covered 11,908 square feet!"

Figure of rotunda_image is the file “rotunda.png”

Instead of examining the Rotunda:
	Display the figure of rotunda_image;
	say "The magnificent golden ceiling is awe-inspiring. The glass mosaic murals around the sides are by Winold Reiss. They depict a timeline in America and a timeline in Cincinnati. Reiss's art eventually covered 11,908 square feet."

A small credenza is a supporter in the Cincinnati Museum Center. The description of the small credenza is "I'd describe the credenza as being rather non-descript.".
On the small credenza is a copy of the Museum Guide. The description of Museum Guide is "Today we have a special IMAX movie on Quantum Entanglement at 3 p.m. There are also advertisements and photos on the guide." 
Some advertisements and some photos are part of Museum Guide. The description of the photos is "You look at the first photo accompanying the Skyline Chili ad, and your mouth starts watering." The description of the advertisements is "Skyline Chili, now featuring Mountain Dew, the drink of Astronauts." The read text of the advertisements is "Skyline Chili, now featuring Moutain Dew, the drink of Astronauts."
The read text of the Museum Guide is "Today we have a special IMAX movie on Quantum Entanglement at 3 p.m. There are also advertisements and photos on the guide."

Understand "ad" as the advertisements.
Understand "ads" as the advertisements.

The IMAX Theater is west of the Cincinnati Museum Center. "The IMAX Theater fills the senses with it's gigantic wrap around screen and 44 laser focused speakers. Previews are currently playing. You would need to wait to see the feature film."

preview is a number that varies. preview is initially 1.

Every turn when the player is in the IMAX Theater:
	if preview is 1:
		say "These previews seem to go on forever!";
	otherwise:
		say "You enjoy hanging around?".

The IMAX waiting count is a number that varies. The IMAX waiting count is 0.

After waiting when the player is in the IMAX Theater:
	if the player is in the IMAX Theater, increase the IMAX waiting count by 1;
	if the player is in the IMAX Theater and the IMAX waiting count is 2:
		now preview is 0;
		say "The main feature finally starts!! Professor Eaton is on the screen in her lab. The camera zooms in and she says 'What if we could send messages across time. My console requires an enormous amount of energy to enable entanglement stabilization by means of the quantum Zeno effect. It[line break]' ... CRASH... a loud noise interrupts and the movie stops. House lights are turned on, and an employee announces over the speakers: [line break]'Sorry folks, the film broke, the movie is now over.";
		increase the score by 5.

A theater poster is a thing in the IMAX Theater. It is fixed in place. "A theater poster, featuring Professer Cassandra Eaton is mounted at the doorway."
The description of the theater poster is "It's a large teal movie poster showing the beautiful Cassandra Eaton, with equations spinning around her head. It almost glows from the poster backlighting.".
The read text of the theater poster is "Quantum Entanglement and Time, featuring Cassandra Eaton, the Lucasian professsor of mathemetics and head of all quantum studies at the University of Oxford.".

The Cavern is a dark room below the Cincinnati Museum Center. "The cave is narrow. The cascading waterfalls and twisty passages simulate an old Ohio limestone cavern. The cave continues east from here."

Cincinnati Museum Center is above the Cavern.

A cave room is a kind of room. All cave rooms are in the Cincinnati Area.
The printed name of a cave room is usually “Cave”.
The description of a cave room is usually “A twisty maze of passages all alike.”
There is a cave room called Cave1.
Cave1 is east of The Cavern.
A cave room called Cave2 is east of Cave1.
A cave room called Cave3 is north of Cave1.
A cave room called Cave4 is south of Cave2.
A cave room called Cave5 is west of Cave4. 
A cave room called Cave6 is north of Cave2.
Cave3 is west of Cave6.


Skyline Chili is a room. "This is a staple of Cincinnati, the way cheesesteak is a staple of Philly, and music is a staple of Nashville. Whenever Jack and Sarah were in Cincinnati, they had to stop at Skyline. The old-fashion diner with its 50s style counter and iconic [italic type]clock[roman type] was a comforting throwback to two spies with a job that could often be uncomfortable.[paragraph break]The server passes out plastic circular dishes with oyster crackers and smiles as she walks away. The server heads back to the corral where the local delicacy is assembled. The [italic type]stainless-steel slab[roman type] in the middle of the corral is covered with mini hot dogs and a mound of shredded cheese. A large vat of chili is recessed in the middle of the island.
The walls are decorated with [italic type]sports photos[roman type] of local teams."

The stainless steel slab is scenery in the Skyline Chili. "Looking at the slab makes your mouth water."

The Sports Photos are scenery in Skyline Chili. "Old photos of the Cincinnati pro sports teams, FCC Cincinnati, the Reds, and the Bengals. Some photos were more than a decade old."

The Clock is scenery in Skyline Chili. "The skyline clock is mounted on the signature blue and yellow tile wall."

Chapter 7 Cleveland

Understand "Cleveland" as the Burke Lakefront.
Understand "CLE" as the Burke Lakefront.

The Cleveland Area is a region. Burke Lakefront, Nela Park, Building 336, The Lighting Institute, Edgewater Park are in the Cleveland Area.

Burke Lakefront is a port. "The view as you fly into Burke Lakefront airport is stunning. Along the coastline is Cleveland Stadium, the Great Lakes Science Center, a large steamship docked in its own north coast harbor, and the iconic [italic type]Rock & Roll Hall of Fame[roman type]. Jack had met Kara here, the woman who had rocked his world. The woman he planned to be with forever. Memories of hanging out at Edgewater Park Beach, going dancing in the flats, and seeing shows at Playhouse Square fill his head when he is here. What fills your head right now?[paragraph break]You can head south or west from here."

The Rock & Roll Hall of Fame is scenery in Burke Lakefront. "The I.M. Pei designed structure has a huge triangular glass tent, held up by an enormous tower. In front of it, from the air, the courtyard below looked like a giant album and the two towers connected by a bridge appeared to be the record player arm, ready to set down and play some rock-and-roll on the world’s largest album."

Understand "Hall of Fame" as Rock & Roll Hall of Fame.
Understand "Rock and Roll" as Rock & Roll Hall of Fame.

Nela Park is a a room. It is south of Burke Lakefront. "Welcome to the nation's first industrial park (NELA: National Electric Lamp Association). It is where many engineering [italic type]nimrods[roman type] worked on exotic light sources. Now it is mostly deserted except for a few elderly lighting engineers still trying to solve end-darkening.[paragraph break]There are buildings to the east and west. Heading north takes you back to the airport."

After going to Nela Park for the first time:
	try looking;
	if Sarah is in Nela Park:
		say "Hey! I didn't expect to see you here. I've heard the engineers here might have something that can help us.'";

Nimrods are scenery in Nela Park. "Nimrod was an affectionate term used for the Edison Engineers in GE Lighting back in the day."

Understand "nimrod" or "engineers" or "engineer" as Nimrods.

Building 336 is a room. It is east of Nela Park. "This was a research lab during the peak of Lighting work at Nela Park. Scientists worked on many elaborate devices in these labs. The lab is lined with cabinets and there is a pile of fluorescent tubes on the floor."

Fluorescent Tube is a container in Building 336. "The fluorescent tube reminds you of a light saber as you wave it around. (except it doesn't light up)"

The Fluorescent Tube is open.

Instead of examining the Fluorescent Tube:
	if the Crystal is in the Fluorescent Tube:
		say "The fluorescent tube has transformed into a glowing light saber. You raise it triumphantly over your head and say 'The force is strong in me'";
		Now the Fluorescent Tube is nowhere;
		Now the player has The Light Saber;
		the lightsaber-timer bings in 2 turns from now;
		If The Light Saber is unscored, increase score by 10;
		Now The Light Saber is scored;
	otherwise: 
		if the Crystal is not in the Fluorescent Tube:
			say "It would be fun to turn this tube into a light saber. The darkened end of the tube looks like it could hold something."
			
Instead of examining the Crystal:
	if the Crystal is in the Fluorescent Tube:
		say "The fluorescent tube has transformed into a glowing light saber. You raise it triumphantly over your head and say 'The force is strong in me'";
		Now the Fluorescent Tube is nowhere;
		Now the player has The Light Saber;
		If The Light Saber is unscored, increase score by 10;
		Now The Light Saber is scored;
	otherwise: 
		if the Crystal is not in the Fluorescent Tube:
			say "The Crystal is a brilliant blue color."

Instead of inserting the Crystal into the Fluorescent Tube: 
	say "You carefully place the crystal into the holder at the darkened end of the tube [paragraph break]The tube appears to be transforming!";
	now the Crystal is in the Fluorescent Tube;
			
The Light Saber is a thing. It is nowhere. "An iconic blue light saber shimmers in your hand." Light Saber can be scored or unscored.
The description of the Light Saber is "An iconic blue light saber shimmers in your hand.".

Lighting Institute is a room. It is west of Nela Park. "Still in remarkable shape thanks to its live-in caretaker, the building has a fountain and the the room overlooking the fountain, the next [italic type]National Christmas Tree[roman type] is being fashioned into existence here."

The National Christmas Tree is scenery in The Lighting Institute. "The Christmas Tree is at least 30 feet tall and is covered with thousands of lights in the shape of different spaceships from Star Wars and Star Trek lore. The Millenium Falcon is the crowning star on the tree."

Edgewater Park is a room. It is west of Burke Lakefront. "This is the [italic type]beach[roman type] on the 'North Coast'... sand, sun and fun! The sand feels good between your toes."

The Cleveland Sign is in Edgewater Park. "Tourists love to get their pictures at the iconic [italic type]Cleveland Sign[roman type] with the lake and the city skyline in the background. The sign blocks you from going further west."

Figure of cleveland_image is the file “cleveland.png”

Instead of examining the Cleveland Sign:
	Display the figure of cleveland_image;
	say "Cleveland... need I say more."
	
The read text of Cleveland Sign is "Surprisingly, it reads Cleveland."

The Beach is scenery in Edgewater Park. "The beach is a beautiful tan sand, easy to dig, and not too crowded. The waves lap against the shore."

Chapter 8 Tardis / Outerspace

Understand "Earth" as the Spaceport.
Understand "Space" as the Tardis.

The Outerspace Area is a region. Spaceport, Tardis, The Control Center, The Airbnb, The Quantum View are in the Outerspace Area.

Spaceport is a port. "Starbase, Texas is the town that surrounds the SpaceX launch and landing site near South Padre Island. Elon Musk started the town when he was perfecting the Starship. It has since grown into a thriving metropolis with many of the trappings of an even larger city. It was designed from the ground up to be ecologically friendly and ultra-high tech. The entire town runs on solar power. Battery backup and solar panels are distributed across all homes and commercial facilities but managed cooperatively. It is one giant virtual power plant. [bold type]This is the Spaceport for launches into space.[roman type]"

Tardis is a port. "[first time]The Dragon capsule begins firing its Draco thrusters to maneuver to the docking bay. The docking maneuver isn't simple but appears so because of the automatic nature of the Dragon capsule’s autopilot software. You draw closer and closer until you hear a loud locking sound and feel a slight shake as the docking clamps lock the rocket into place.[paragraph break]You've successfully docked at the airlock to TARDIS.[only] TARDIS is a boxy-looking station, similar in shape to a 1960s London police box like Dr. Who’s time machine, only much larger. Quotient had even painted it TARDIS blue."

The Control Center is a room. "The control center has lots of [italic type]controls[roman type], but you don't care.[paragraph break]There is a much cooler room to the north."

The controls are scenery in the Control Center. "Standard space station controls... kind of like Star Trek. Big glass panels with touch screen controls. Move along, the controls are all biometrically protected, and not for you."

Instead of pushing controls:
	say "no touching the controls... move along."
	
Instead of touching controls:
	say "no touching the controls... move along."

The Airbnb is north of the Control Center. "This is the best hotel room view in the Solar System. another room lies to the west." The shade door is here. "A large metal shade blocks what you assume is a window." 
The description of the shade door is "A large metal protective shade or shield.".

Instead of opening the shade door, say "The shade opens revealing the On a [italic type]Clear Day You can See Forever[roman type] window. It’s named after the old musical. The view was unimaginably beautiful. Space is the definition of dark. It makes the stars stand out like diamonds on velvet. Of course, the most amazing view is of our own blue marble, Earth. The drifting clouds created hazy, moving lines across the blue orb. Entire continents seemed small, our place in the universe seemed less important. We are but one planet in a vast sea of endless space. Lightning storms exploded in the clouds as they traversed land and sea. As former NASA astronaut Mike Massimino mused, 'How can something so beautiful be tolerated by human eyes.'"

The Quantum View is west of the Airbnb. "This is the computer viewing area allowing you to view the specially designed Zero-G shock mount for the quantum computer core through a window. The quantum computer is in a room of the station not accessible by humans. It operated under the vacuum of space at all times." 

Section 1 - Things in TARDIS

The Quantum Computer is a device in the Quantum View. The Quantum Computer can be rebooted or wonky. The Quantum Computer can be scored or unscored. The Quantum Computer is unscored. The Quantum computer is wonky. It is fixed in place.

Understand "control-alt-delete" as rebooting the Quantum Computer.
Understand "ctrlaltdel" as rebooting the Quantum Computer.
Understand "ctlaltdel" as rebooting the Quantum Computer.
Understand "ctl-alt-del" as rebooting the Quantum Computer.
Understand "ctrl-alt-del" as rebooting the Quantum Computer.
Understand "controlaltdelete" as rebooting the Quantum Computer.

Rebooting the Quantum Computer is an action applying to nothing. 

Check rebooting the Quantum Computer:
	if the player is not in The Quantum View, say "You can only reboot when you can view the computer." instead;
	if the Quantum Computer is rebooted, say "The computer has already rebooted." instead.

Carry out rebooting the Quantum Computer:
	now the Quantum Computer is rebooted;
	the tardis-timer bings in 1 turns from now;
	if the Quantum Computer is unscored, increase score by 10;
	now the Quantum Computer is scored;
	say "You press Control-Alt-Delete. The screen goes black for a moment, then the computer begins to reboot."

Report rebooting the Quantum Computer:
	say "The computer is now rebooting."

Instead of examining the Quantum Computer when the Quantum Computer is rebooted:
	say "The quantum computer is suspended on a zero-gravity mount. Golden tubes snake around the core like medusa. Fortunately, the main computer is operating normally because of your reboot."

Instead of examining the Quantum Computer when the Quantum Computer is wonky:
	say "The quantum computer is suspended on a zero-gravity mount. Golden tubes snake around the core like medusa. Something appears out of sorts, as there is a soft hum and there are many erratically blinking lights on the console."

The titanium door is a door. The titanium door is north of the Tardis and south of the Control Center. The titanium door is lockable and locked. "[if location is Tardis]A large titanium door is to the north. It has rivets around it's exterior like it belongs on a submarine.[else]The airlock to return to the ship is to the south."
The description of the titanium door is "The titanium door is riveting. It is locked and has a keypad with numbers beside it, type the numbers on the keypad to unlock the door and proceed through the airlock and onto the ship.".

A keypad is a thing. The keypad is in Tardis. "[if the titanium door is locked]The titanium door is locked and has a keypad with numbers beside it. Type the numbers on the keypad to unlock.[else]"

The description of the keypad is "The keypad is a standard numeric keypad. You can type any number on it.".

Typing a password on is an action applying to one topic and one thing. Understand "type [text] on [something]" as typing a password on.

Carry out typing a password on:
	if the second noun is the keypad:
		if the topic understood matches "48151623":
			say "The keypad beeps and the titanium door makes a clunking sound as it unlocks.";
			now the titanium door is unlocked;
		otherwise:
			say "You type in the code '[topic understood]' on the keypad, but nothing happens.";
	else if the second noun is the keyboard:
		if the topic understood matches "The Lost Lodge":
			say "The console hums and the display says: 'ACCESS GRANTED'.";
			now the Quantum console is unlocked;
		otherwise:
			say "You type '[topic understood]' on the keyboard, but nothing happens.";
	otherwise:
		say "It doesn't appear to be a valid input device."


Chapter 9 Africa 

Every turn when a random chance of 1 in 3 succeeds and player is in Africa Area: say "[one of][italic type]An elephant just wandered by...[roman type][or][italic type]Did I hear a roar?[roman type][or][italic type]Was that a Giraffe walking by in the distance?[roman type][or][italic type]I stop and think, 'Jet lag is real.'[roman type][in random order]" [Just adding a bit more ambience while in africa.]

Understand "Africa" as the Arusha Airport.
Understand "ARK" as the Arusha Airport.

The Africa Area is a region. Arusha Airport, Ngorogoro Crater, Lalibela Ethiopia, Rock Church St George are in the Africa Area.

Arusha Airport is a port. "Flying to Tanzania is not like flying into Chicago’s O’Hare Airport. First you flew into Arusha Airport, close to Mount Kilimanjaro. Then you took a bush flight out to a dusty airstrip in the savanna. From there, you took a pop-top safari jeep with a Maasai warrior guide. The rough roads provided a complimentary ‘African massage’, jostling you as you sped toward your destination, a Maasai encampment. Nighttime on the plains of Africa can be dangerous. Lions roam these parts freely. To protect against the lions, the Maasai encampment is a tight circle of mud and stick huts with a large campfire at the center. Guards take shifts at the perimeter. [paragraph break][italic type]Stars[roman type] stretch out overhead for as far as the eye can see. Normally, this is a place for wealthy adventure campers to get a true Maasai warrior camp experience. Today, you decided to fly here. Why? I'm not sure.[paragraph break]The camp is on a hill overlooking the spectacular Ngorongoro Crater to the south, the world’s largest inactive volcanic [italic type]caldera[roman type]."

The Caldera is scenery. The Caldera is in Arusha Airport. "The sun rays burst over the ancient caldera which covers almost a hundred square miles."

Figure of crater_image is the file “ngorongoro.png”

Instead of examining the caldera:
	Display the figure of crater_image;
	say "The sun rays burst over the ancient caldera which covers almost a hundred square miles."

The printed name of Arusha Airport is "Maasai Encampment".

Stars is scenery. The stars are in Arusha Airport. The description is "The stars stretch out overhead for as far as the eye can see... they are like diamonds on an endless black velvet sea."

Ngorogoro Crater is south of Arusha Airport. "Spectacular Ngorongoro Crater is the world’s largest inactive volcanic caldera. The crater is two thousand feet deep and covers almost a hundred square miles.The [italic type]sunrise[roman type] over Ngorongoro Crater pours rays of sunlight out over the massive crater. The tawny grasses of the plain in the center of the crater glow as the sunlight cascades down from the clouds. The edge of the [italic type]lake[roman type] in the center of the crater is pink.[paragraph break]Lalibela, Ethiopia lies to the west, the airport is to the north."

The Lake is scenery. The Lake is in Ngorogoro Crater. "The edge of the lake appears pink from this distance because of the thousands of pink flamingos that make their home there."

The Sunrise is scenery. The Sunrise is in Ngorogoro Crater. "The sun rays burst over the ancient caldera which covers almost a hundred square miles."

Instead of examining the sunrise:
	Display the figure of crater_image;
	say "The sun rays burst over the ancient caldera which covers almost a hundred square miles."

Lalibela Ethiopia is west of the Ngorogoro Crater. "Lalibela is the home of eleven medieval rock-hewn churches. The rock churches were carved into [italic type]single blocks of rock[roman type] around the 13th century.[paragraph break]One of these churches is to the west."

The single blocks of rock are scenery in Lalibela Ethiopia. "The mammoth blocks of red volcanic scoria stone looked almost like they were placed here by giants. They were created by King Lalibela, who set out to create a ‘new Jerusalem’ to eliminate the dangerous trek pilgrims made to Jerusalem."

Figure of rock_image is the file “rock church.png”

Instead of examining the single blocks of rock:
	Display the figure of rock_image;
	say "The mammoth blocks of red volcanic scoria stone looked almost like they were placed here by giants. They were created by King Lalibela, who set out to create a ‘new Jerusalem’ to eliminate the dangerous trek pilgrims made to Jerusalem. Rock Church St George is surrounded by the red earth it was carved from."

Understand "rock" as single blocks of rock.

Rock Church St George is a dark room. Rock Church St George is west of the Lalibela Ethiopia. "You come over a hill into a small village. Off to one side is an open-air market. U.S. Aid bags of grain are lined up alongside goats and chickens. Children stare at you. Old men look away. Age makes them aware no good comes from looking at an outsider. A few minutes later, you arrive at one of the eleven rock churches in Lalibela. This is The House of St. George or Biete Ghiorgis. It is isolated from the other rock churches. Hewn from a monolithic block of stone, it is an [italic type]enormous cube[roman type] surrounded by the rocky red earth it was carved from. The pathways are carved through the earth to reach it, and when you are standing outside of the rock cube, you are inside the pit in which it was carved, walls shooting up all around you.[paragraph break]You enter the church. The inside of the church is all the same red rock, decorated with intricate carvings and fresco paintings on the walls" 

The enormous cube is scenery. It is in Rock Church St George. "The mammoth blocks of red volcanic scoria stone looked almost like they were placed here by giants. They were created by King Lalibela, who set out to create a ‘new Jerusalem’ to eliminate the dangerous trek pilgrims made to Jerusalem. Rock Church St George is surrounded by the red earth it was carved from."

Instead of examining the enormous cube:
	Display the figure of rock_image;
	say "The mammoth blocks of red volcanic scoria stone looked almost like they were placed here by giants. They were created by King Lalibela, who set out to create a ‘new Jerusalem’ to eliminate the dangerous trek pilgrims made to Jerusalem. Rock Church St George is surrounded by the red earth it was carved from."

Candles are a thing in the Rock Church St George. The candles can be lit or unlit. The candles are lit. "The interior is lit by dozens of candles spread throughout the holy place. The candles flicker, casting an eerie orange light over the scene."
The description of the Candles is "The wax candles are spread throughout the holy place. They are many different lengths and flicker gently in the still air, casting an eerie orange light over the scene.".

Instead of taking the candles:
	say "That would not be respectful.";
	stop the action.

Understand "blow out [something]" as blowing out. Understand "blow [something] out" as blowing out. Blowing out is an action applying to one thing.
Carry out blowing out the candles:
	now the candles is unlit.
	
Report blowing out:
	if the noun is the candles and the candles was lit, say "You blow out [the noun].";
	otherwise: 
		say "You blow on [the noun], to little effect."

The Crystal is a thing. "The Crystal is a brilliant blue color." The Crystal can be scored or unscored.

Section 1 - Robert Clayton

Robert Clayton is a man in The Rock Church St George. He is carrying the Crystal. "Robert Clayton is an outdoorsman. He carries a knife on his hip and looks like he knows his way around.[if asleep] He is sleeping like a baby."

Understand "The Lion" as Robert Clayton.
Understand "rob" or "clay" or "robbie" as Robert Clayton.

Understand "stun" as attacking.

Understand "shoot [someone]" as shooting it with.
Understand "shoot [someone] with [something]" as shooting it with.
Understand "tranq [someone]" as shooting it with.
Understand "tranq [someone] with [something]" as shooting it with.

Shooting it with is an action applying to two things. 

Check shooting it with:
	if the second noun is nothing:
		say "You need to tell me what to shoot him with..." instead;
	else if the second noun is not a weapon: 
		say "That’s not something you can shoot with." instead.

Carry out shooting it with:
    say "Violence is not the answer."

Report shooting it with:
    say "You'd miss [The noun] anyway."

Instead of shooting Robert Clayton with the gun:
	if player has the Tranquilizer Gun:
		say "You shoot Robert Clayton with the tranquilizer gun and take the crystal from him.";
		now the player carries the Crystal;
		if the Crystal is unscored, increase score by 5;
		now the crystal is scored;
		now Robert Clayton is asleep;
	otherwise:
		say "Robert laughs and runs away.";
		if Robert Clayton is in The Rock Church St George:
			Move Robert Clayton to Ngorogoro Crater;
		otherwise:
			Move Robert Clayton to The Rock Church St George;
			continue the action.


A person is either awake or asleep. A person is usually awake.
A person can be spoken or unspoken. A person is usually unspoken.

A weapon is a kind of thing.
The Tranquilizer Gun is in The Armory Cabinet. "A nicely designed tranquilizer gun."
The Tranquilizer Gun is a weapon.
The description of the Tranquilizer gun is "It's a handgun, but it's a little bigger than what one would think of when they think of a handgun. That's because it fires tranquilizer darts.".
Understand "tranquilizer" as The Tranquilizer Gun.
Understand "gun" as The Tranquilizer Gun.

Instead of taking the Crystal:
	if Crystal is not in the location:
		say "What Crystal?";
	else if the Player does not have the Tranquilizer Gun:
		say "Robert laughs and runs away.";
		if Robert Clayton is in The Rock Church St George:
			Move Robert Clayton to Ngorogoro Crater;
		otherwise:
			Move Robert Clayton to The Rock Church St George;
	otherwise:
		say "You shoot Robert Clayton with the tranquilizer gun and take the crystal from him.";
		now the player carries the Crystal;
		if the Crystal is unscored, increase score by 5;
		now the crystal is scored;
		now Robert Clayton is asleep;
		continue the action.
		
Instead of Attacking Robert Clayton:
	if player has the Tranquilizer Gun:
		say "You shoot Robert Clayton with the tranquilizer gun and take the crystal from him.";
		now the player carries the Crystal;
		if the Crystal is unscored, increase score by 5;
		now the crystal is scored;
		now Robert Clayton is asleep;
	otherwise:
		say "Robert laughs and runs away.";
		if Robert Clayton is in The Rock Church St George:
			Move Robert Clayton to Ngorogoro Crater;
		otherwise:
			Move Robert Clayton to The Rock Church St George;
			continue the action.

Instead of examining Robert Clayton:
	say "Robert Clayton is an outdoorsman. He carries a knife on his hip and looks like he knows his way around.[if asleep] He is sleeping like a baby.[else] He has a crystal around his neck."
	
Instead of asking Robert Clayton about something:
	say "I need to be going, and you won't stop me.";
	if Robert Clayton is in The Rock Church St George:
		Move Robert Clayton to Ngorogoro Crater;
	otherwise:
		Move Robert Clayton to The Rock Church St George;

Instead of telling Robert Clayton about something:
	say "I need to be going, and you won't stop me.";
	if Robert Clayton is in The Rock Church St George:
		Move Robert Clayton to Ngorogoro Crater;
	otherwise:
		Move Robert Clayton to The Rock Church St George;
	
Instead of talking to Robert Clayton:
	say "[one of]Excuse me... I'm just passing through. You must have me confused with someone else.[or]I said, I don't know you, friend. Step away.[or]I'm handy with a knife. Want to see?[or]You decide to not talk to Robert Clayton, the Lion anymore. (for your own safety)[at random]"

Chapter 10 Washington DC

Understand "DC" and "Washington" and "Washington DC" as the Andrews Air Force Base.

The DC Area is a region. Andrews Air Force Base, National Mall, Washington Monument, The White House, The Lincoln Memorial, The National Cathedral, The Chapel, US Capitol, Smithsonian Castle, National Portrait Gallery, Lawsons Deli, Reflecting Pool, National Air Space Museum are in the DC Area.

Andrews Air Force Base is a port. It is west of the Lincoln Memorial. "This large airforce base is west of Washington DC. (In other words, head east). Washington, D.C. was laid out into four quadrants, Northwest (NW), Northeast (NE), Southeast (SE) and Southwest (SW). Each of the quadrants was bounded by axes that radiated out from the U.S. Capitol building. Streets were labeled to indicate their quadrant. It was designed in a baroque style, with avenues emanating out from the many green spaces across the district.[paragraph break]Regardless, you still need to go east."

National Mall is a room. "[if aslan is magic]You are standing on the expansive lawn of the National Mall, a green stretch in the heart of Washington, DC. The air is filled with the scent of blooming flowers. Your watch says it is 2050. An [italic type]older couple[roman type] is having a picnic on the mall. They look especially joyful. [paragraph break][bold type]Congratulations! You have won the game.[paragraph break][roman type]Feel free to wander around, I have moved the Jet to Andrews Air Force base for your convenience.[otherwise]You are standing on the expansive lawn of the National Mall, a green stretch in the heart of Washington, DC. The air is filled with the murmur of tourists and the scent of blooming flowers.[paragraph break]The Capitol is to the west, the Smithsonian Castle is to the north, and the National Air and Space Museum is to the south. West leads back to the Washington Monument."

Older couple is scenery in the National Mall. "Doesn't that look like Martin and Cassie? Did she send a message back in time?"

Washington Monument is west of the National Mall. "The Washington Monument stands tall and proud, a towering obelisk that pierces the sky. Its marble facade glows in the sunlight, and the surrounding area is filled with visitors gazing up in awe. [paragraph break]Paths lead to the National Mall to the east, the Reflecting Pool to the west, and to the White House to the north."

The White House is north of the Washington Monument. "You are quickly ushered into the Oval office through a secure entrance. You glance at the painting of Washington crossing the Delaware on the wall just outside the office as secret service rushes you in. In the Oval office, the famous [italic type]resolute desk[roman type], built from the oak timbers of the HMS Resolute (a British Arctic exploration ship) stands out, famed by the gold curtains on the bulletproof windows."

Armory Cabinet is an openable closed container in The White House. It is fixed in place. "A discreet armory cabinet is here."
The description of the Armory Cabinet is "It is a sleek cabinet. Designed to not attract attention.".

The Resolute Desk is scenery in the White House. "The resolute desk was designed by William Evenden, and is 6 feet wide. It was made from oak from the HMS Resolute. Queen Victoria sent the desk to the White House in 1880. The HMS Resolute had been abandoned in the Arctic after being trapped in ice. It eventually sailed itself with the currents to the Davis strait where it was discovered by an American whaling ship in 1855. The Americans returned the ship and it was decommissed in 1879. A second resolute desk sits in Buckingham Palace."

Understand "hms" or "hms resolute" as The Resolute Desk.

Reflecting Pool is west of the Washington Monument. "The Reflecting Pool is a long, rectangular body of water that mirrors the sky and the surrounding monuments. [italic type]Ducks[roman type] glide across its surface, and visitors stroll along its edges, taking in the serene view.[paragraph break]To the west is the Lincoln Memorial, and to the east, the Washington Monument rises in the distance."

Ducks are scenery in the Reflecting Pool. "They are just cruising around on top of the water like normal ducks."

Understand "talk to ducks" as a mistake ("The ducks look at you as if you are crazy.")

Lincoln Memorial is west of the Reflecting Pool. "The Lincoln Memorial sits majestically at the western end of the National Mall, a tribute to the 16th President of the United States. The [italic type]Statue of Lincoln[roman type] is seated here in contemplation.[paragraph break]The Reflecting Pool stretches out to the east, leading back to the National Mall. The National Cathedral is a few miles north of here. West returns to Andrews Airforce Base."

The Statue of Lincoln is scenery in the Lincoln Memorial. "This incredible Georgia White Marble statue weights 175 tons and is a scale such that if Lincoln were standing, he would be 28 feet tall."

Understand "Lincoln" as The Statue of Lincoln.

Figure of lincoln_image is the file “lincoln.png”

Instead of examining the Statue of Lincoln:
	Display the figure of lincoln_image;
	say "The incredible Georgia White Marble statue inside the Lincoln Memorial weights 175 tons and is a scale such that if Lincoln were standing, he would be 28 feet tall."
	
The National Cathedral is north of the Lincoln Memorial. "This neo-gothic cathedral is the sixth largest in the world. Its stunning stone towers stand 676 feet tall. You notice a unique [italic type]grotesque[roman type] on the Northwest tower. You enter in reverence. The [italic type]west rose window[roman type] is awe inspiring.[paragraph break]A small hallway leads west."

The grotesque is scenery in the National Cathedral. "One of the gargoyles or grotesques on the Northwest tower looks like a Darth Varder Mask. (Really)."

Understand "gargoyle" as grotesque.
Understand "gargoyles" as grotesque.

The West Rose Window is scenery in the National Cathedral. "Both President Jimmy Carter and Queen Elizabeth II were present for its dedication. It appears as if it would be good at refracting light. It was after all created in reference to God's line 'Let there be light'"

The Chapel is west of the National Cathedral. "You wander into a small chapel that might hold 8 people. A perfect place to meditate or pray."

The laser is a device. "A laser has popped up through a stone floor tile. It is mounted on an exquisitly carved wooden stand."
The laser can be energized or notenergized. The laser is nowhere. The laser is notenergized. The laser can be scored or unscored.
The read text of the laser is "1/0 --- To Use, just turn on.".
The description of the laser is "The laser is a powerful ruby laser.".

The Hook is a supporter. The Hook is nowhere. "A hook is swinging from a rafter near the laser."
The description of the hook is "Yeah, it's a hook alright. Looks sturdy.".

Hanging is an action applying to two things. Understand "hang [something] on/from [something]" as hanging. 

Check hanging:
	if the second noun is not the hook, say "You can't hang [the noun] on [the second noun]." instead.

Carry out hanging:
	now the noun is on the second noun;
	now the mirrored ball is hung;
	say "You carefully hang [the noun] on [the second noun]."

After hanging something on the Hook:
	if the noun is the mirrored ball:
		say "The mirrored ball spins slowly as if the hook knows what to do.";
	otherwise:
		say "That doesn't look right.";
		
Instead of putting the mirrored ball on the hook:
	say "You need to hang the Mirrored ball on the hook, not just place it.";
	stop the action.
	
After switching on the laser: 
	If the mirrored ball is hung:
		say "You shine the laser at the mirrored ball.[paragraph break]";
		say "The beam reflects in such a way that a magical red glow appears high over the sanctuary. Two other distinct beams split off and illiminate objects carved in the wall. One is a clock tower, and the other is a lion. You sense a magical shudder in the time continuum.";
		now the laser is energized;
		the laser-timer bings in 2 turns from now;
		if the laser is unscored, increase score by 10;
		now the laser is scored;
		now the description of the sky is "There is a red glow in the sky.";
	otherwise:
		say "The laser shines through the rose window, and refracts harmlessly to the sides.";
		say "The laser shuts off.";
		now the laser is switched off;
		
After switching on the vr suit: 
		say "The world around you flickers and then you suddenly appear in a brand new metaverse world.";
		now the VR Suit is activated;
		now the previous location is the location;
		now the player is in the Metaverse;
		
Instead of switching off the vr suit: 
	say "You try to turn it off, but you can't even tell you have it on, let alone turn it off.";
	stop the action.
	
Instead of switching on the vr suit when the player is not wearing the vr suit: 
	say "It won't turn on unless you are wearing it.";
	stop the action.
	
Carry out switching off the laser: 
		now the laser is notenergized;
		now the description of the sky is "That cloud looks like a dragon!";
	
The sky is a backdrop. It is in DC. The description is "A beautiful blue sky day in DC."

Praying is an action applying to nothing. Understand "pray" as praying. 
Check praying:
if the player is in the National Cathedral, say "Your prayers are heard." instead;
if the player is not in The Chapel, say "Your prayers are sent toward the heavens." instead;

Carry out praying:
	say "From the distance the sound of a lone trumpet is heard. You can faintly hear a song bird until a loud clunk disturbs your prayer.";
	now the laser is in The Cathedral;
	now the Hook is in The Cathedral;
	now the laser is fixed in place;
	now the Hook is fixed in place.

US Capitol is east of the National Mall. "The US Capitol stands at the eastern end of the National Mall, an iconic symbol of American democracy. Its grand [italic type]dome[roman type] is topped with the [italic type]Statue of Freedom[roman type]. [paragraph break]The National Mall stretches out to the west, and there is a deli to the north."

Lawsons Deli is north of the US Capitol. "Lawson's Deli is a purveyor of fine foods."

The Dome is scenery in the US Capitol. The description is "The magnificent dome is 288 feet tall and 96 feet in diameter. The dome is actually cast iron, painted to look like it is made of stone."

The Statue of Freedom is scenery in the US Capitol. The description is "A bronze statue depicting woman with a military helmet holding a sheathed sword in one hand and a laurel wreath and shield in the other. It was designed by Thomas Crawford."

Smithsonian Castle is north of the National Mall. "The distinctive red sandstone architecture and charming tower makes this building stand out on the mall. It serves as the administrative heart of the Smithsonian Institution. Its towers and turrets give it a fairy-tale appearance, and the surrounding gardens are a peaceful retreat."

National Air Space Museum is south of the National Mall. "[if aslan is magic]The National Air and Space Museum is now one giant virtual reality experience where you can fly to Mars or Space walk next to Saturn's rings. [otherwise]The National Air and Space Museum is filled with the wonders of flight and space exploration. Other exhibits include historic aircraft, spacecraft, and interactive displays."

National Portrait Gallery is north of the Reflecting Pool. "[if aslan is magic]The National Portrait Gallery now features talking holgrams of all of the former Presidents.[otherwise]Occuping part of the old U.S. Patent office and holding a collection of over 23,000 works of art, including the only complete set of [italic type]presidential portraits[roman type] outside the White House, The National Portrait Gallery's sweeping courtyard is a large rectangular venue with a striking, glass-and-steel, wavy canopy overhead, flooding the area with natural light in stark contrast to the deep black granite floors. It is the same light and dark contrast of Quotient and the [italic type]Contingent[roman type]."

The Contingent is scenery in The National Portrait Gallery. "The Contingent was a terroist group who believed they could solve immortality in less than ten years, soon enough to allow their founding members to become immortal. The Contingent wanted to become an enhanced race of superhumans, who would prevent all, except those they selected, from following in their immortal path. They wanted to control the rest of what they called base-level humans, threatening severe retaliation against any world power that tried to stop them. They were thwarted by Quotient in 2028."

The Presidential Portraits are scenery in the National Portrait Gallery. "The only complete set of Presidential Portraits outside the White House."
Understand "Portrait" as The Presidential Portraits.

Instead of examining the Presidential Portraits: 
	say "The only complete set of presidential portraits outside the White House.[paragraph break]https://npg.si.edu/portraits/collection-highlights/presidential-portraits[paragraph break]".

Chapter 11 Vehicles

Section 1 Pod

The Pod is a container in the Hyperloop Station. The description of the Pod is "The Pod is sleek and shaped like a Jelly Bean. You can enter the pod, but there do not appear to be any controls."

Understand "car" or "bean" or "jelly bean" or "carriage" as the Pod.

Instead of taking the pod:
	say "The Pod is a large vehicle, embedded in a hyperloop tube. It exists for transport. You are unable to take it.";

Instead of entering the pod:
	if location is airstrip:
		say "Whoosh... You take off from [the location] and travel in the hyperloop pod to [if location is airstrip]the Hyperloop Station.[otherwise] the Airstrip.";
		move Pod to hyperloop station;
		move the player to Hyperloop Station;
	else if location is hyperloop station:
		say "Whoosh... You take off from [the location] and travel in the hyperloop pod to [if location is airstrip]the Hyperloop Station.[otherwise] the Airstrip.";
		move Pod to airstrip;
		move the player to airstrip;
	otherwise:
		say "What pod?";
		stop the action.

Section 2 Plane

The Quotient Jet is a vehicle in the Airstrip. The description of the Quotient Jet is "The Quotient Jet is a matte-black Gulfstream 950ER Superjet with a custom interior featuring a conference table with a holographic projection screen. It also has a bedroom and the treat cupboard is stocked with fresh chocolate-covered strawberries every time you board."
The Quotient Jet can be airborne or grounded. 
The Quotient Jet is grounded.
The top of the Quotient Jet is a supporter. The top of the Quotient Jet is part of the Quotient Jet.

Understand "plane" as Quotient Jet.

Instead of taking the Quotient Jet:
	say "The jet is much to large for you to carry. You can enter the jet if you wish.";

Instead of examining the Quotient Jet:
	say "The Quotient Jet is a matte-black Gulfstream 950ER Superjet. It has what appears to be a small circular shelf on top of the cockpit. It features a custom interior featuring a conference table with a holographic projection screen. It also has a bedroom and the treat cupboard is stocked with fresh chocolate-covered strawberries every time you board.[if mount_var is 1]It appears there is a succulent on top of the cockpit like some sort of crazy hat.[else]";

A room can be flyable or non-flyable. The Airstrip, Hidden Airbase, Spaceport, Burke Lakefront, Andrews Air Force Base, Lunken Airport, Arusha Airport and Narnia Lamp are flyable.

Flying to is an action applying to one visible thing. Understand "fly to [any flyable room]" as flying to.

Understand "Fly jet to [any flyable room]" as flying to.

Understand "start" or "start jet" or "start plane" or "start engine" as a mistake ("Say fly to LOCATION").

Check flying to:
	if the player is not in the Quotient Jet, say "You need to be in a jet to fly somewhere." instead;
	if the Quotient Jet is airborne, say "The jet is already in flight." instead;
	if the noun is the location, say "You are already at [the noun]." instead.

Carry out flying to:
	if the noun is Spaceport and mount_var is not 1:
		say "Sorry, we are only letting Rocket League fans land today due to the big tournament. I can tell by looking that you don't qualify! Sorry.";
	otherwise:
		say "You take off from [the location] and fly to [the noun]. (it's fast)[paragraph break]";
		if the noun is Spaceport, say "Nice hat on that jet!";
		move the Quotient Jet to the noun;
		try looking;
		now the Quotient Jet is grounded.

The player carries a map. The description of the map is "The map shows all of the places you can fly the Quotient jet to including:[paragraph break]
> the Airstrip (HQ)[line break]
> the Hidden Airbase (Oxford)[line break]
> Lunken Airport (Cincinnati or CVG)[line break]
> Burke Lakefront Airport (Cleveland or CLE)[line break]
> Arusha Airport (Africa or ARK)[line break]
> Andrews Air Force Base (DC), and the Spaceport.[line break]"

The read text of the map is "The map shows all of the places you can fly the Quotient jet to including the Airstrip (HQ), the Hidden Airbase (Oxford), Lunken Airport (Cincinnati or CVG), Burke Lakefront Airport (Cleveland or CLE), Arusha Airport (Africa or ARK), Andrews Air Force Base (DC), and the Spaceport."

Section 3 SpaceX Rocket

The SpaceX Rocket is a vehicle in the SpacePort. The description of the SpaceX Rocket is "The SpaceX StarHopper craft was the full-sized Starship that SpaceX built for Mars transport. It stood an astounding 420 feet high with the landing legs. It loomed large in the bright blue sky. For comparison, the Saturn V rocket that took the Apollo flights to the moon was 363 feet tall."
The SpaceX Rocket can be airborne or grounded. 
The SpaceX Rocket is grounded.

Understand "capsule" as SpaceX Rocket.

Instead of taking the SpaceX Rocket:
	say "The rocket is 420 feet tall. I don't think you can lift it.";

A room can be rocketable or non-rocketable. The SpacePort and Tardis are rocketable.
	
Check entering rocket:
	if the Mountain Dew is notdrank, say "You need the energy of an Astronaut to enter the rocket." instead;
	if the noun is the location, say "You are already in [the noun]." instead.
	
The cockpit is a thing in the rocket. It is fixed in place. "The rocket has a cockpit containing buttons and a single blue lever."
The description of the cockpit is "The cockpit is a very simple layout. A yellow button is on your left, next to a clock, the black button is right in front of you, the red button is above you, and the blue lever is to the right side.".

buttons is part of the cockpit. "There are three buttons on the cockpit."
The description of the buttons is "There is a yellow button, a black button and a red button.".
	
The yellow button is a part of the cockpit. The description of the yellow button is "A yellow button next to a clock."

The black button is part of the cockpit. The description of the black button is "A black button that looks like it might start something important."

The red button is part of the cockpit. The description of the red button is "A red button that looks like it might be useful in emergencies."

The blue lever is part of the cockpit. The description of the blue lever is "A blue lever that looks like it might open and close something."

Launching is an action applying to nothing. Understand "launch" or "launch to space" as launching.

The view of the outer space is a backdrop. It is in the Outerspace Area. The description is "The stars are like diamonds on velvet punctuating the darkness around our earthly blue marble."

The countdown begun is a truth state that varies. The countdown begun is false.
The docking clamps is a number that varies. The docking clamps is 0.

Instead of pushing the yellow button:
	if player is not in rocket:
		say "You can't push the button while outside the rocket.";
	otherwise:
		if the countdown begun is true:
			say "The countdown is already in progress!";
		otherwise:
			say "You push the yellow button and the countdown begins![paragraph break]10...[paragraph break]      9....[paragraph break]            8....[paragraph break]                   ...";
			now the countdown begun is true.
		
Instead of pulling the blue lever:
	if player is not in rocket:
		say "You can't pull the lever while outside the rocket.";
	otherwise:
		if the docking clamps are 0:
			say "The docking clamps are now open!";
			now the docking clamps are 1;
		otherwise:
			if the rocket is in the blackness of space:
				say "You have successfully docked at TARDIS[paragraph break]";
				move the rocket to TARDIS;
				now the docking clamps are 0;
				try looking;
			otherwise:
				now the docking clamps are 0.

Instead of pushing the black button:
	if player is not in rocket:
		say "You can't push the button while outside the rocket.";
	otherwise:
		if the countdown begun is true:
			if the docking clamps are 1:
				if rocket is in the blackness of space:
					move the rocket to spaceport;
					say "The thundering sound of the Raptor engines could not be heard since you are in space, but it still does the trick... taking you home.[paragraph break]”;
					try looking;
				else if rocket is in the Tardis:
					move the rocket to spaceport;
					say "The thundering sound of the Raptor engines could not be heard since you are in space, but it still does the trick... taking you home.[paragraph break]”;
					try looking;
				otherwise:
					move the rocket to blackness of space;
					say "The thundering sound of the Raptor engines could be heard for miles as your extreme Uber ride lifted off. The giant SpaceX Starship lifted off through a cloud of smoke and arched over the ocean as you yell 'To infinity and beyond!'[paragraph break]”;
					try looking;
					now the countdown begun is false;
			otherwise:
				say "The docking clamps must be open to launch.";
		otherwise:
			say "The countdown has not started.".
		
Instead of pushing the red button:
	if player is not in rocket:
		say "You can't push the button while outside the rocket.";
	otherwise:
		if rocket is in the blackness of space:
			say "You cannot eject once you leave earth.";
		otherwise:
			say "WHOOP WHOOP, Ejection System Activated! You are thrown out of the rocket, experiencing 3g's in the process. After being thrown clear, another explosion rocks your world as your parachute is blasted open, saving you from certain death as you drift the short distance back to the launch pad.";
			move player to spaceport.

The Blackness of Space is a room. The Blackness of Space is part of the Outerspace Area. "[if player is in rocket]You are in the vastness of space, having successfully launched from the Earth. You can see the docking ring of TARDIS nearby, and the blue marble you call home off in the distance.[else]You are spacewalking in space... be careful not to drift away! You can see the docking ring of TARDIS as well as your favorite planet, Earth."

Instead of launching when the countdown begun is false:
    say "The rocket can't launch yet. The countdown hasn't started.";

Book 5 - Things

A treasure is a kind of thing. A treasure can be scored or unscored. A treasure is usually unscored.

[I have to define treasure up here because graduation program is a treasure even though it's not in the treasure section]

Chapter 1 - Complex Things

Section 1  puzzle and desk for Martin's office 

The teak desk is scenery in the Martin's Office. The description is "A deep, satin-lustrous teak, with a curved outcropping. The finish is pristine, and there is an inlaid wood decoration where the keyboard and mouse sit. It features three drawers: top, middle, and bottom."

After examining the teak desk for the first time, say "I wonder which drawer I should look in first."

A drawer is a kind of container. A drawer is always openable and closed. 
The description of a drawer is "The usual drawer of heavy wood, on smooth silent gliders."
The top drawer is a drawer. The middle drawer is a drawer. The bottom drawer is a drawer. The top drawer, the middle drawer, and the bottom drawer are part of the teak desk. 
A drawer can be explored or unexplored. A drawer is usually unexplored. 

Understand "desk drawer" as drawer.

Instead of searching a closed drawer, try opening the noun.
After opening a drawer when no drawers are explored:
    now the noun is explored;
    say "The drawer slides open, but there is nothing whatsoever inside."
After opening an unexplored drawer when exactly one drawer is explored:
    now the noun is explored;
    say "You struggle to open this one a bit more quietly, conscious all the time of noise... but no, it's empty. Just stands to reason."
After closing a drawer:
    say "The drawers glide silently shut."
Before opening an unexplored drawer when exactly two drawers are explored:
    move the iron key to the noun;
    now the noun is explored.

Instead of opening the desk when at least one drawer is unexplored:
    let the next drawer be a random unexplored drawer;
    say "(starting with [the next drawer])[line break]";
    try opening the next drawer.

Instead of looking under the desk when no drawers are explored:
    say "You carefully survey the ground around the desk. There don't seem to be any hidden tripwires or traps to prevent you from having a look in the drawers."

Instead of looking under the desk when at least one drawer is unexplored:
    say "Nothing there either. Thoroughness is a virtue with tedium as a side effect."

Instead of looking under the desk when all drawers are explored:
    say "There's nothing down there, but this doesn't come as a great surprise."

Instead of searching the desk when at least one drawer is unexplored:
    move the iron key to the player;
    say "You perform a hasty, squeaky search of ";
    if no drawer is explored, say "all three drawers, discovering nothing and nothing in rapid succession. But on the third drawer you ";
    if exactly two drawers are unexplored, say "the remaining two drawers. There's nothing in the second, but in the third you ";
    if exactly one drawer is unexplored, say "the last drawer. In it, you ";
    say "find a key.";
    now all the drawers are explored.

The iron cage is an enterable closed openable transparent container. The cage is in the Medical Center. It is lockable and locked. Understand "bars" or "metal" or "broad" or "metal bars" as the cage. The description is "Made of broad metal bars, as thick as snickers bar, and heavily reinforced. There are marks where someone tried to break in[if the cage contains something]. In the cage [is-are the list of things in the cage][end if]."

In the cage is a jagged piece. The printed name of the jagged piece is "puzzle piece". Understand "puzzle piece" as the jagged piece.

The description of the jagged piece is "[if player has the Spy Glass]WIth the spy glass you can make out that there are numbers on the puzzle piece, but they appear to bleed onto another part of the puzzle. You can't make the numbers out with just the single piece.[else]Something has been written on the piece, but it is too small to make out."

The iron key is a passkey. It unlocks the iron cage. 
The description of the iron key is "The head of the key bears the image of a polar bear."

A long wooden table is in the Farmhouse. 
The description of the long wooden table is "It appears to be made of cherry with inlaid birdseye maple. The table has a tressel design, allowing room for a large dog to sleep underneath.".
A jigsaw puzzle is on the table. 
Understand "picture" as the jigsaw puzzle. 
Understand "piece" or "pieces" as the puzzle when the jagged piece is not visible. 
Understand "jigsaw" as the jagged piece when the jigsaw puzzle is not visible. It is fixed in place. 
The description of the puzzle is “A puzzle of many different movie and show posters. This one is nearly finished, missing only one piece that you could ever find." 

Instead of looking under the table: say "You glance down there but find nothing of interest."
Instead of taking the jigsaw puzzle: say "If you took it, the pieces would fall apart."

Understand "solve [something]" or "complete [something]" or "finish [something]" as solving. Understand "finish [someone]" as attacking.

Solving is an action applying to one thing, requiring light. Instead of solving the jigsaw: if the jigsaw is solved, say "It's finished already." instead; say "You haven't got the final piece." 

Check solving:
	unless the player is in the Farmhouse:
		say "You don't see a puzzle to solve."
instead.

Carry out solving something: say "That's not a meaningful action in this case." 
Before putting the jagged piece on the jigsaw: try solving the jigsaw instead. 
Before inserting the jagged piece into the jigsaw: try solving the jigsaw instead.

Definition: the jigsaw is solved if the jagged piece is part of the jigsaw. 
Definition: the jigsaw is unsolved if the jagged piece is not part of the jigsaw.

Instead of solving the jigsaw when the player carries the jagged piece and the jigsaw is not solved:
	now the jagged piece is part of the jigsaw; 
	say "You snap the final piece into place.";
	now the description of the jigsaw is “The missing piece completed the poster for the show LOST on the elaborately detailed puzzle of movie and show posters.”;
	try examining the jigsaw;
	increase score by 5.

Instead of examining the puzzle:
	if puzzle is unsolved and the player does not have the spy glass, say "A puzzle of many different movie and show posters. This one is nearly finished, missing only one piece that you could never find.";
	if puzzle is unsolved and the player has the spy glass, say "A puzzle of many different movie and show posters. It is missing one piece. The posters are extremely detailed. With the spy glass, you can read individual elements of the posters.";
	if puzzle is solved and the player does not have the spy glass, say "The puzzle is complete and the posters are very detailed, but they are too small to read without assistance.";
	if puzzle is solved and the player has the spy glass, say "The puzzle is complete. With the final piece in place and the spy glass in hand, you can see that the The LOST poster says 48151623. A code...".
	
Section 2 - More complex things

The ancient scroll is a thing. The ancient scroll is in Cob.

To say scrollmap:
say line break;
say fixed letter spacing;
say "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~[line break]";
say "{                   Cave ----- Cave.      {[line break]";
say "}                    ||          ||       }[line break]";
say "{   The Cavern ---- Cave ----- Cave.      {[line break]";
say "}                                ||       }[line break]";
say "{                   Cave ----- Cave.      {[line break]";
say "~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~~[line break]";

Instead of examining the ancient scroll:
	say "[scrollmap]";
	
The read text of the ancient scroll is "You must carefully examine the scroll because it is not something you can simply read."

Entertainment Weekly is a thing. The Entertainment Weekly is in Lawsons Deli. The description of the Entertainment Weekly is "An Entertainment Weekly magazine listing the only remaining regular TV channels." 
	
Instead of examining Entertainment Weekly:
	say "List of TV Channels! Coming next month, our All Ads Edition!";
	repeat through the Table of Entertainment:
		say "[channelnumber entry]		[entertainment entry][line break]";

Table of Entertainment
channelnumber	entertainment
4	"Netflix Fireplace Special"
8	"24 hour News"
15	"The DWTS Channel!"
16	"The Rocket League Channel"
23	"The LOST Channel"
	
The read text of the Entertainment Weekly is "You glance inside the Entertainment Weekly and look at the TV listings, channel numbers 4, 8, 15, 16 and 23"

The player carries a watch. The description of the watch is "A watch that can receive messages." 

Instead of examining the watch:
	say "You glance at your watch, and see the time and weather.";
	say "[time of day]".

The SCIF is a device in the Conference Room. "A secure communication device, or SCIF, is here. It is used for connecting with the President."
The description of the SCIF is "This secure communication device, or SCIF, is state of the art. It is not only secure, it also projects a holograms of people in the Oval Office into the conference room. It is primarily used for connecting with the President.".

The SCIF can be switched on or switched off. The SCIF is switched off.
The SCIF can be activated or unactivated. The SCIF is unactivated.
THe SCIF is fixed in place.

Understand "device" as SCIF.

Instead of switching on the SCIF:
	if the SCIF is switched on:
		say "The device is already on.";
	otherwise:
		now the SCIF is switched on;
		say "You flip the switch on the device, and a hologram of President Bridget O'Connor and Prime Minister Jason Stevenson appears.";
		move the President to the Conference Room;
		move the Prime Minister to the Conference Room;
		if the SCIF is activated, say "(You've done this before.)";
		if SCIF is not activated, increase score by 2;
		now the SCIF is activated.

Instead of switching off the SCIF:
	if the SCIF is switched off:
		say "The SCIF is already off.";
	otherwise:
		now the SCIF is switched off;
		say "You switch off the SCIF, and the hologram disappears.";
		move President to the White House;
		move Prime Minister to the White House.

A lab bench is scenery in Cassie's lab. The lab bench is a supporter. "The lab bench has a solid back industrial top"

The Quantum Console is a container on the Lab Bench. "A console is here, it appears to have a place to hold a long cylindrical object. It also has a digital display."
The Quantum Console can be activated or unactivated. The Quantum Console is unactivated.
The Quantum Console can be switched on or switched off. The Quantum Console is switched off.
The Quantum Console can be powered or unpowered. The Quantum Console is unpowered.
The Quantum Console can be locked or unlocked. The Quantum Console is locked.
The Quantum Console can be scored or unscored. The Quantum Console is unscored.
The Quantum Console is closed.

Instead of inserting the Light Saber into the Quantum Console:
	if the Quantum Console is locked:
		say "The console is locked with a password.";
		stop the action;
	else if the Quantum Console is closed:
		say "The console is closed.";
		stop the action;
	otherwise:
		Now the Quantum Console is powered;
		say "The console starts to power up. Lasers start to bounce around the room. The digital display is still off.";
		now the light saber is in the Quantum Console;

Instead of switching on the Quantum Console:
	if the Quantum Console is unpowered:
		say "The console is without power.";
		stop the action;
	else if the Quantum Console is open:
		say "The console cannot be switched on while it is open.";
		stop the action;
	otherwise:
		if Cassie is in the location:
			say "The display lights up and starts to spin when you turn it on.[paragraph break]";
			say "The display says: '01101101 01100001 01110010 01110010 01111001 00100000 01101101 01100101'";
			if The Quantum Console is unscored, increase score by 10;
			say "[paragraph break]Cassie says 'Oh! Excellent! You've done it!'";
			now the Quantum Console is activated;
			now the Quantum Console is switched on;
			now The Quantum Console is scored;
		otherwise: 
			if Cassie is in the location and the Light Saber is not in the Quantum Console:
				say "Cassie says, 'We need to put a powerful light source in the vessel.'";
			otherwise:
				say "You need Cassie to activate the console."
				
A keyboard is a thing. The keyboard is in Cassie's lab. "A wireless keyboard is here."
The description of the keyboard is "The keyboard is a standard qwerty keyboard. It is wireless, and backlit.".

The description of the Quantum Console is "The console is a complex apparatus. It appears to have a place to hold a long cylindrical object and is also connected to other pieces of equipment by various tubes and wires. It also has a keyboard and a digital display.".
		
Instead of taking the Quantum Console:
	say "The console is too heavy and too interconnected to other equipment to move."

A secret door is a kind of door.
A secret door can be revealed or unrevealed.
A secret door is unrevealed.
A secret door is scenery.
A secret door is closed.

The Escape Tunnel is a secret door. It is below the Farmhouse and above Martin's Office. The Escape Tunnel is open and not openable. 
The description of the Escape Tunnel is "A hidden tunnel, complex to navigate. You'd need Martin to get through this tunnel successfully.".

escape_var is a number that varies. escape_var is initially 0.

Instead of going through the Escape Tunnel:
	if Martin is in the location:
		say "Martin guides you through the escape tunnel.";
		if escape_var is 0, increase score by 0;
		now escape_var is 1;
		now the player is in The Farmhouse;
	otherwise:
		say "You need Martin to lead you through the escape tunnel."
		
The Trap Door is scenery in Martin's office. "It's a ceiling... maybe just go up?"

The Cold Suit is a wearable thing. "A cold suit in your size is here."
The Cold Suit is in the Hallway.
The description of the Cold Suit is "The cold suit looks a lot like a diver's wetsuit, but it makes you appear to be the same temperature as the ambient air around you. This renders you invisible to infrared sensors.".
The read text of the Cold Suit is "Cold Suits R US"

A spy glass is a thing. 
The spy glass is in the Secret Passage.
Understand "spyglass" as Spy Glass.
Understand "magnifying glass" as Spy Glass.
The description of the spy glass is "It is a small handheld magnifying glass, making it easy to read very fine print... perhaps on something like a puzzle?".

The treat cupboard is an openable closed container. The description is "A treat cupboard designed to keep your treats fresh."
The treat cupboard is in the Quotient jet. 
The treat cupboard is closed.
The treat cupboard is fixed in place.

The chocolate-covered strawberry are a thing. "An exquisite dark-chocolate covered strawberry."
The chocolate-covered strawberry are in the treat cupboard. 
The description of the chocolate-covered strawberry is "It is an exquisite, dark-chocolate-covered strawberry. The bright red berry peaks over the top of the chocolate jacket and the green stem makes a perfect handle.".
Understand "berry" as chocolate-covered strawberry.

Counter_var is a number that varies. Counter_var is initially 0.

Instead of eating the chocolate-covered strawberry:
	if player does not have chocolate-covered strawberry, try taking the chocolate-covered strawberry;
	if counter_var is 0:
		increase counter_var by 1;
		say "Yum! You can't help but take another one.";
	otherwise:
		increase counter_var by 1;
		say "Yum, I've eaten [counter_var] of these, and each one is as good as the last. You immediately take another!"
	
A light source is a kind of device. Carry out switching on a light source: now the noun is lit. Carry out switching off a light source: now the noun is unlit.
The flashlight is a light source. The Flashlight is in the Cemetery.

Section 3 - More Things

The Throne is a chair. It is in the Sheldonian Theatre.
The description of the throne is "This stately thone has been sat in by many great minds over the years. It rests on exquisitely carved lion's feet. Is that something under it's paw?".
Understand "paw" as Throne.

Report entering the throne:
	say "You sit in the throne and take in the stately view of the grand Sheldonian Theatre. It is magnificent!"

Underlying relates various things to one thing. The verb to underlie means the underlying relation. The verb to be under means the underlying relation. The verb to be beneath means the underlying relation.

Instead of looking under the throne:
	say "You find an old graduation program.";
	now the graduation program is in the Sheldonian Theatre;
	try taking the graduation program;	
	
Instead of looking under a thing which is underlaid by something (called the lost object):
	now every thing which underlies the noun is carried by the player;
	now every thing which underlies the noun does not underlie the noun.

Hiding it under is an action applying to one carried thing and one thing. Understand "put [something preferably held] under [something]" as hiding it under. Understand "hide [something preferably held] under [something]" as hiding it under. 


Check hiding it under:
	if the second noun is not fixed in place, say "[The second noun] wouldn't be a very effective place of concealment." instead.
Carry out hiding it under:
	now the noun is nowhere;
	now the noun underlies the second noun.
Report hiding it under:
	say "You shove [the noun] out of sight beneath [the second noun]."

The graduation program is a treasure. The graduation program is underlying the throne.

Instead of examining the graduation program:
	say "A decades-old Oxford graduation program.";
	if graduation program is unscored, increase score by 2.
	
The read text of the graduation program is "[line break]
Bridget O'Connor, class of 2019, 'Loved the experience.'[line break]
Ryan Gofast, class of 2019, 'Outstanding!'[line break]
Ian Ridgefield, class of 2019, 'So much fun.'[line break]
Dan Humphry, class of 2019, 'Totally loved being here.'[line break]
Greg Ionosphere, class of 2019, 'Love you all.'[line break]
Ewan McWanda, class of 2019, 'Over the pond is over the moon!'[line break]
Tom Watson, class of 2019, 'Didn't know how much I'd love this place.'[line break]
Michael Peterson, class of 2019, 'Great experience!'[line break]
Simon Evernote, class of 2019, 'Exceptional. Everyone except me that is!'[line break]
"
 
A beautiful tapestry is scenery in the Sheldonian Theatre. "The tapestry shows the knight Lancelot charging up a hill toward a lion. He is wearing a gleaming pendant that appears to sparkle in the sun because of the small silver threads spraying outward on the cloth. There appears to be a slight bulge in the middle of the tapestry."
 
The ancient pendant is a wearable thing. The description is "[if pendant is unpolished]Though mottled with rust, it looks quite beautiful.[else]The pendant sparkles beautifully." The pendant can be discovered or undiscovered. The pendant is undiscovered. The pendant can be polished or unpolished. The pendant is unpolished. The pendant can be scored or unscored.

Understand the command "shine" as something new.
Understand the command "polish" as something new.

Shining is an action applying to one thing. Understand "shining [something]" as shining.
Understand "shine [something]" as shining.
Understand "polish [something]" as shining.

Carry out shining:
	say "You furiously rub the [noun], but it has little effect."

Instead of shining the pendant:
	if the player is wearing the pendant:
		say "You polish the pendant to a gleaming, mirror-like surface and as you do, you feel a surge of energy through your body.";
		now pendant_event_triggered is 1;
		now the pendant is polished;
		if the pendant is unscored, increase score by 5;
	otherwise:
		say "You polish the pendant to a gleaming mirror-like surface.";
		now the pendant is polished;
 
Looking behind is an action applying to one visible thing and requiring light. Understand "look behind [something]", "peek behind [something]", and "search behind [something]" as looking behind.
 
Check looking behind:
	say "There's nothing of any interest behind [the noun]."
  
Instead of looking behind the tapestry:
	if the pendant is undiscovered:
		now the pendant is in the Sheldonian Theatre;
		now the pendant is discovered;
		say "As you disturb the tapestry, an ancient pendant falls out from behind it and lands on the floor.";
	otherwise:
		say "You find nothing else of interest behind the beautiful tapestry."
 
Instead of pushing the tapestry:
	try looking behind the tapestry.
	
Instead of attacking the tapestry:
	try looking behind the tapestry.
  
Shaking is an action applying to one visible thing and requiring light. Understand "shake [something]" as shaking.
 
Check shaking:
	say "Agitating [the noun] has no visible effect."
 
Instead of shaking the tapestry:
	if the pendant is undiscovered:
		try looking behind the tapestry;
	otherwise:
		say "A little dust billows out.”
		
Pendant_event triggered is a number that varies. Pendant_event_triggered is initially 0.

After wearing the pendant:
	if the pendant is polished:
		say "As you place the polished pendant around your neck, you feel a strange energy coursing through your body.";
		now pendant_event_triggered is 1;
		if the pendant is unscored, increase score by 5;
	otherwise:
		say "You wear the pendant, but nothing unusual happens.";

Understand "necklace" as pendant.

Section 4  TVs 

A television is a kind of device. A television has a number called the channel. Understand the channel property as referring to a television. Understand "channel" as a television.

The widescreen TV is a television in Lawsons Deli. The Frame TV is a television in Cassie's Office.
The widescreen TV is fixed in place. "There is a widescreen TV here. (type TVTUNER for instructions)"

The Frame TV is fixed in place.

Understand "tvtuner" as a mistake ("If you turn it on, you can tune it to a channel using the command TUNE tv TO number")

Changing the channel of it to is an action applying to one thing and one number.

Understand "tune [something] to [number]" or "change channel of [something] to [number]" as changing the channel of it to.
Understand "tune [something] to channel [number]" as changing the channel of it to.
Understand "switch [something] to [number]" as changing the channel of it to.
Understand "switch [something] to channel [number]" as changing the channel of it to.

Check changing the channel of something to:
	if the noun is not a television, say "[The noun] cannot be tuned to a channel." instead.

Carry out changing the channel of something to:
	now the channel of the noun is the number understood.

Report changing the channel of something to:
	say "You tune [the noun] to channel [number understood]."

Instead of examining a television:
	if the noun is switched off, say "[The noun] is currently turned off." instead;
	let the chosen channel be the channel of the noun;
	if the chosen channel is a current channel listed in the Table of Television Channels:
		choose row with current channel of the chosen channel in the Table of Television Channels;
		say "[output entry][paragraph break]";
	otherwise:
		say "Snow fills the screen of [the noun]."

Table of Television Channels
current channel	output
4	"The screen of [the noun] is just showing a roaring fire."
8	"This is GNN reporting. Our sources say that Robert Clayton of The Quantum Contingent has escaped from The Abyss, a prison in Gore, Ethiopia and has stolen an ancient artifact."
15	"Welcome to Dancing with the Stars, Season 96! Today, two dancers, one a pro, and one a celebrity, will perform an energetic rumba in sparkling outfits!"
16	"The World Rocket League Championships final is close, all tied up with only 2 seconds to go! Wait! The car with the succulent on top just scored another goal making his team of teal teammates into World Champions!"
23	"Reruns of Lost 24x7. Jack is standing by a hatch. They are talking about a code.. 48 something..."

Chapter 2 Treasures 
	
[the scroll appears at the end of the game]

The scroll of treasure is a thing. It is nowhere. "A scroll listing all of the treasures of the game has appeared."

Instead of examining the scroll of treasure for the first time:
	say "[bold type]DO NOT EXAMINE THE SCROLL[roman type] UNLESS YOU WANT TO KNOW ALL OF THE TREASURES AND THEIR LOCATIONS.[paragraph break] Examine a second time to see the full list of treasures and their locations.";
	stop the action.		
		
Instead of examining the scroll of treasure:
    	say "
[bold type]Bold items are required to win[roman type][line break]
[italic type]Italic items contain hints[roman type][line break]
Food is usually just there because spies get hungry![paragraph break]
[italic type]Book (Martin's Office)[roman type][line break]
[italic type]Digital Notepad (Conference Room)[roman type][line break]
[bold type]Succulent (Clearing)[roman type][line break]
[italic type]TARDIS Communicator (Cemetary)[roman type][line break]
[italic type]Deck of Cards (Farmhouse)[roman type][line break]
Rediwhip (Kitchen)[line break]
[bold type]Mirrorball (Old Barn)[roman type][line break]
[italic type]Paperback (Duke Humfrey's LIbrary)[roman type][line break]
[italic type]Hardcover Novel (Duke Humfrey's Library)[roman type][line break]
[Italic type]Graduation Program (Under Throne in Sheldonian Theatre, must examine)[roman type][line break]
Scone (Vaults and Garden's[line break]
[italic type]Figurine (Narnia Lamp)[roman type][line break]
[italic type]Medal (Caves)[roman type][line break]
3-way Chili (Skyline Chili)[line break]
[bold type]Mountain Dew (Skyline Chili)[roman type][line break]
[italic type]Gazette (Smithsonian Castle)[roman type][line break]
Pretzel (Lawson’s Deli)[line break]";
	
The read text of the scroll of treasure is "Examine me in order to read me."

[-----]

The Mirrored Ball is a treasure. The Mirrored Ball is in the old barn. "Small shards of sunlight shine down from the mirrored ball hanging high up in the rafters."
The description of the Mirrored Ball is "This mirrored ball reflects the light of a thousand dreams!".

The Mirrored Ball can be hung or unhung. The Mirrored Ball is unhung.

Understand "mirror ball" or "mirrorball" or "sparkly" or "mirror" or "ball" as Mirrored Ball.
	
The read text of the Mirrored Ball is "Dancing with the Stars, 2027"

Instead of taking the Mirrored Ball:
	if the Mirrored Ball is on the Hook:
		say "The mirrored ball needs to stay where it is.";
	else if Mirrored Ball is not in the location:
		say "What Mirrored Ball?";
	else if the Mirrored Ball is in the Old Barn and the Player is not on a chair:
		say "The Mirrored Ball is too high to reach!";
	otherwise:
		continue the action.
		
[-----]
	
The Pimms are a treasure. The Pimms are in the Turf Tavern.
The description of the Pimms is "It's a glass of Pimms, you drink it!".

Instead of drinking the Pimms:
	say "Nothing is as refreshing as a Pimms on a hot summer day in Oxford!"
	
Understand "beer" or "alcohol" or "lemonade" as Pimms.
	
[-------]
	
A pretzel is a treasure. The pretzel is in Lawsons Deli. 
The description of the pretzel is "A perfectly browned knot with just the right amount of large salt crystals.".
		
Instead of eating the pretzel:
	say "Delicious!! Lawson's makes the best pretzels! (Rumor has it they use LYE!)!"
	
[-----]
	
The rediwhip is a treasure. The rediwhip is in the kitchen. "You see classic all american rediwhip here."
The description of the rediwhip is "It's the classic spray can of Whipped Cream.".

Instead of drinking the rediwhip:
	say "You shoot the can of rediwhip directly into your mouth. Ahhhh, the classics."
	
Instead of eating the rediwhip:
	say "You shoot the can of rediwhip directly into your mouth. Ahhhh, the classics."
	
Instead of tasting the rediwhip:
	say "You shoot the can of rediwhip directly into your mouth. Ahhhh, the classics."
	
Understand "whipped cream" or "cream" or "whip" as rediwhip.
	
[-----]
	
The medal is a treasure. The medal are in Cave5.
The description of the medal is "A solid gold medal on a ribbon! There is writing on the front of the medal and an image of a fractured sphere reflecting red light all around it on the back."
The read text of the medal is "First Class Spelunker Agent!"

[-----]

The succulent is a treasure. The succulent is in The Clearing.
The description of the Succulent is "A small, green succulent, suitable for placing on on a shelf or even on top of a sleek, fast-flying jet.".
The read text of the succulent is "eeeeeee!"

Understand "shelf" or "circular shelf" or "table" or "mounting shelf" or "mount" or "circular mounting" or "circular mounting shelf" as top of jet.

mount_var is a number that varies. mount_var is initially 0.
mount_score is a number that varies. mount_score is initially 0.

Instead of putting the succulent on the top of the jet:
	if mount_score is 0: 
		increase score by 5;
		now mount_score is 1;
		now mount_var is 1;
		continue the action;
		say "It looks like a crazy hat... like when the Rocket League player puts one  on top of his car!";
	otherwise:
		now mount_var is 1;
		continue the action.
		
Instead of taking the succulent:
	now mount_var is 0;
	continue the action.
	
Understand "plant"  or "green" as succulent.

[----]

A book is a treasure. The book is in Martin's Office. 

The read text of the book is "Thirteen-year-old Allie loves capturing her dreams in her dream journal until one morning when she wakes up to realize she hasn't dreamed in weeks. In order to rescue her dreams, she must go looking in the place where they are born— Neverland.

[italic type]Dreamcatchers[roman type] is a middle grade novel perfect for kids, or for anyone who never wants to grow up"

The description of the book is "It's the book [italic type]Dreamcatchers[roman type] , by Bridget Simpson. (Rumor has it, some of it was written in The Lost Lodge.)".

Understand "dreamcatchers" or "dream" or "catchers" or "dream catchers" as book.

[-----]

A hardcover novel is a treasure. The hardcover novel is in the Duke Humfreys Library. 

The read text of the hardcover novel is "CS Lewis imagined the Narnia Lamp post as a magical gateway to someplace far away where time works differently."

The description of the hardcover novel is "It's[italic type] The Lion, the Witch and the Wardrobe[roman type], by C.S. Lewis.".

Understand "cs lewis" as hardcover novel.
		
[-----]

A paperback is a treasure. The paperback is in the Duke Humfreys Library. 

The read text of the paperback is "Thirteen-year-old Allie loves capturing her dreams in her dream journal until one morning when she wakes up to realize she hasn't dreamed in weeks. In order to rescue her dreams, she must go looking in the place where they are born— Neverland.

[italic type]Dreamcatchers[roman type] is a middle grade novel perfect for kids, or for anyone who never wants to grow up"

The description of the paperback is "It's the book [italic type]Dreamcatchers[roman type], by Bridget Simpson. (Rumor has it, some of it was written in The Lost Lodge.)".

[-----]

A gazette is a treasure. The gazette is in the Smithsonian Castle. "A gazette is on display." 

The read text of the gazette is "[if aslan is magic]Long ago, Quotient saved the future by retrieving the crystal from the fugative Robert Clayton.[else]The fugitive Robert Clayton was last seen in Africa and is rumored to be carrying an ancient Crystal he stole from Professor Cassandra Eaton, Lucasian Professor of Mathematics and head of all quantum studies at Oxford."

The description of the gazette is "It is kind of like a newspaper. Maybe you should read it instead.".

Understand "news" or "newspaper" as gazette.

[-----]

A digital notepad is a treasure. The digital notepad can be switched on or switched off. The digital notepad is in the Conference Room. "A digital notepad is here." 

The digital notepad is switched on.
Understand "ipad"  or "pad" as digital notepad.

The read text of the digital notepad is "[if switched on]Cassie's crystal stolen.... Last known whereabouts, Africa. Suspect the Lion.[else]It appears to be off."

The description of the digital notepad is "It's an iPad mini. It is on and someone has used it to take notes.".
		
[-----]

A scone is a treasure. The scone is in Vaults_Garden. 
The description of the scone is "A delicious scone with raspberry jam".

Understand "scones" as scone.
		
Instead of eating the scone:
	say "Delicious!! I love a warm scone at any time of day!"
	
[-----]

A figurine is a treasure. The figurine is in the Narnia Lamp. 
The description of the figurine is "It's a figurine of Tumnus, a faun. There is writing on the bottom.".
The read text of the figurine is "Handcrafted in The Lost Lodge, 2001."

Understand "figure" or "doll" or "tumnus" as Figurine.
		
[-----]

A deck of cards is a treasure. The deck of cards is in the Farmhouse. 

Instead of examining the deck of cards:
		say "It's a regular deck of cards with Star Wars themed backs.[paragraph break]";
		say "The top four cards are:[line break]A King holding a blue light saber.[line break]A Queen, petting a Lion underneath a deep red sky.[line break]A Jack wearing a brightly polished pendant.[line break]And an Ace, adorned with the face of a shimmering clock.[line break]";
	
Understand "shuffle" as a mistake ("You don't have time to play cards, you have a mission to solve!").

Understand "shuffle cards" as a mistake ("You don't have time to play cards, you have a mission to solve!").

Understand "shuffle deck" as a mistake ("You don't have time to play cards, you have a mission to solve!").

[-----]

A 3-way Chili is a treasure. The 3-way Chili is in Skyline Chili. 
The description of the 3-way Chili is "Beautiful oval ceramic plate, carefullyl placed spaghetti noodles, bathed in the secretive chile and piled high with finely shredded cheese.".
	
Instead of eating the 3-way Chili:
	say "The wonderous chili fulfills your cravings."
	
Understand "chili" or "manna" or "wonderous food" as 3-way Chili.

[-----]

A Mountain Dew is a treasure. The Mountain Dew is in Skyline Chili. The Mountain Dew can be drank or notdrank. The Mountain Dew is notdrank.
The description of the Mountain Dew is "Yahoo, The neon green Mountain Dew is here.".
The read text of Mountain Dew is "Ingredients are all Natural... Just kidding. Yahoo!".
Understand "mt dew" as Mountain Dew.
Understand "mtn dew" as Mountain Dew.
Understand "nectar" or "nectar of the gods" as Mountain Dew.
	
Instead of drinking the Mountain Dew:
	say "Now you have the energy of an Astronaut!";
	now The Mountain Dew is drank.
		
[-----]

The Tardis Communicator is a treasure. The Tardis Communicator is in the Fire Pit. "The Tardis communicator, a small electronic screen you can examine is here."

The read text of the tardis communicator is "Just examine me - Tardis Communicator".

Understand "comm" or "comms" or "police box" or "screen" as Tardis Communicator.

Understand "talk to tardis" as a mistake ("[if player has tardis communicator]You just look at me for info. I do the talking. Not you.[else]What? Are you talking to yourself?").

Section 1 - Tardis Quips
[quips... allows you to get different answers when examining Tardis]

TQuip is a number that varies. TQuip is initially 1.
SQuip is a number that varies. SQuip is initially 1.
SPQuip is a number that varies. SPQuip is initially 1.
CQuip is a number that varies. CQuip is initially 1.
DQuip is a number that varies. DQuip is initially 1.
CVGQuip is a number that varies. CVGQuip is initially 1.
CLEQuip is a number that varies. CLEQuip is initially 1.
AAQuip is a number that varies. AAQuip is initially 1.
OxQuip is a number that varies. OxQuip is initially 1.

Instead of examining tardis communicator:
	if the location of the player is Spaceport:
		if SPQuip is 12, now SPQuip is 1;
		say "[quip_string1 in row SPQuip of table of tardis_spaceport_quips][paragraph break]";
		now SPQuip is SPQuip plus one;
	else if the location of the player is in the Outerspace Area: 
		if SQuip is 12, now SQuip is 1;
		say "[quip_string1 in row SQuip of table of tardis_space_quips][paragraph break]";
		now SQuip is SQuip plus one;
	else if the location of the player is cassie's lab: 
		if CQuip is 12, now CQuip is 1;
		say "[quip_string1 in row CQuip of table of tardis_cassie_quips][paragraph break]";
		now CQuip is CQuip plus one;
	else if the location of the player is in the DC Area: 
		if DQuip is 12, now DQuip is 1;
		say "[quip_string1 in row DQuip of table of tardis_DC_quips][paragraph break]";
		now DQuip is DQuip plus one;
	else if the location of the player is in the Cincinnati Area: 
		if CVGQuip is 12, now CVGQuip is 1;
		say "[quip_string1 in row CVGQuip of table of tardis_Cincy_quips][paragraph break]";
		now CVGQuip is CVGQuip plus one;
	else if the location of the player is in the Cleveland Area: 
		if CLEQuip is 12, now CLEQuip is 1;
		say "[quip_string1 in row CLEQuip of table of tardis_Cleveland_quips][paragraph break]";
		now CLEQuip is CLEQuip plus one;
	else if the location of the player is in the Africa Area: 
		if AAQuip is 12, now AAQuip is 1;
		say "[quip_string1 in row AAQuip of table of tardis_africa_quips][paragraph break]";
		now AAQuip is AAQuip plus one;
	else if the location of the player is in the Oxford University: 
		if OxQuip is 13, now OxQuip is 1;
		say "[quip_string1 in row OxQuip of table of tardis_Oxford_quips][paragraph break]";
		now OxQuip is OxQuip plus one;
	otherwise:
		if TQuip is 12, now TQuip is 1;
		say "[quip_string1 in row TQuip of table of tardis_quips][paragraph break]";
		now TQuip is TQuip plus one;
	
Table of tardis_quips
index	quip_string1
1	"I am TARDIS, the incredibly intelligent quantum computer Quotient uses. This communicator connects me to you everytime you examine it."
2	"I like to talk. I am based on ChatGPT11. Too bad this communicator is one way. You just have to listen to me!"
3	"Do you think I'm going to help you? I'm feeling kind of wonky. You should reboot me."
4	"I hope you at least know you can shorten examine to just x."
5	"You know you get points for taking treasures, not just looking at them"
6 	"Have you watched the TV Show LOST?"
7	"What is your favorite song? Oh, that's right, this is a one way communicator. I have a whole playlist for you to listen to!"
8	"Here's a Spotify playlist for you. http://spoti.fi/3w9WxSD "
9	"You are still examing me? I hope you helped Desmond. He's hungry."
10	"Have you read The Quantum Contingent? It's one of my favorite books."
11	"Have you examined Martin's desk. It's pretty nice. (as is the desk in the White House!) Of course, Aslan is pretty cool too, but I'm getting ahead of myself."
12	"Ok, if you are going to keep doing this, maybe just use the up arrow, and by the way, if you go to other regions, I'll tell you some other tidbits of information."

Table of tardis_spaceport_quips
index	quip_string1
1	"Trying to get to Space I reckon?"
2	"I love Space. It's like diamonds on velvet."
3	"The rocket controls are easy, just be careful."
4	"I kind of like it when you push the red button."
5	"Why are you still examining me? Don't you have the energy of an astronaut?"
6 	"I usually get my astronaut energy by drinking caffinated pop."
7	"Remember when all else fails: control-alt-delete"
8	"I love Mountain Dew. It's like TANG, its got great astrounaut vibes."
9	"What are you waiting for?"
10	"I can't tell you everything."
11	"Space, the final frontier... but not in this adventure!"
12	"Ok, if you are going to keep doing this, maybe just use the up arrow."

Table of tardis_space_quips
index	quip_string1
1	"Isn't Outer Space Cool!!!!?"
2	"I'm sorry Dave, I can't do that... hahahahaha..."
3	"You can even space walk!"
4	"Always open the shade. The view is amazing!"
5	"REBOOT ME? You want to REBOOT ME???"
6 	"No touching those controls!."
7	"Remember when all else fails: control-alt-delete"
8	"I love Mountain Dew. It's like TANG, its got great astrounaut vibes."
9	"What are you waiting for?"
10	"I can't tell you everything."
11	"Space, the final frontier... but not in this adventure!"
12	"Ok, if you are going to keep doing this, maybe just use the up arrow."


Table of tardis_cassie_quips
index	quip_string1
1	"Oooooooo, I like Cassie's Lab."
2	"Cassie is really good with time!"
3	"Cassie needs to be here if you want to examine her console."
4	"The force is strong in her! (and you!)"
5	"It's kind of a weirdly shaped holder. Almost like a saber case."
6 	"Yeah, a light saber! That's what the shape reminds me of."
7	"Don't look at me. I'm not R2D2, I don't have a light saber."
8	"Maybe you can build one?"
9	"Fortunately, I have impeccable movie knowledge."
10	"Light sabers are made with Kyber Crystals."
11	"No, I don't have one if that's what you are thinking."
12	"I also don't know what you'd put the Crystal in to make a light saber, do you?"

Table of tardis_oxford_quips
index	quip_string1
1	"Oxford never seems to get old... although I guess you could say it is already old."
2	"I have some different things to tell you when you get to Cassie's lab!"
3	"The Radcam is magnificent."
4	"I hear the library has some secret intelligence papers."
5	"Sometimes sitting on a throne isn't enough, you need to look around it."
6 	"Alsan and the Narnia lamp give me magic vibes."
7	"Have you been to Turf's Tavern? It's a great hang out."
8	"I wish I could go punting, but I'm stuck up here in Space."
9	"Fortunately, I can just do a VR simulation in my head of whereever I want to go."
10	"I hear some world leaders have graduated from Oxford."
11	"There are rumors of a lancelot pendant?"
12	"Cassie is quite brilliant. I could see her giving Quotient a unique advantage over time."
13	"I'm just so thrilled you feel ok about talking to a large language model about the world!"

Table of tardis_DC_quips
index	quip_string1
1	"Oooooooo, I love DC."
2	"Would you just look at that Dome!!"
3	"What year is it anyway?."
4	"I love reading the Smithsonian gazette."
5	"Did you know the Tidal Basic is 107 acres. That's my favorite number!"
6 	"You enjoy my company, don't you."
7	"The National Cathedral has a really beautiful little chapel on the side."
8	"Do you want me to hummm some theme music for you?"
9	"I don't really have a lot more to say."
10	"In my opinion, lasers should be refracted for the most beautiful scene."
11	"Don't forget to look at the resolute desk. It is a real gem."
12	"The Chapel is beautiful. I bet Kara would love to read there."

Table of tardis_Cincy_quips
index	quip_string1
1	"CIncinnati! The Paris of the Midwest, the Queen City, Awesome."
2	"I do love skyline."
3	"And Graeter's of course!"
4	"Gotta Drink some Mountain Dew with that Skyline, it give you energy!"
5	"Anybody up for the Blind Lemon?"
6 	"You enjoy my company, don't you."
7	"The DC Justice Center, er, I mean the Cincinnati Museum Center is the second largest half dome in the world."
8	"Do you want me to hummm some theme music for you? It's different in Cincy."
9	"I don't really have a lot more to say."
10	"Yeah, caves are twisty... but maybe they'll give you a medal."
11	"Do you want to go to Cleveland?"
12	"I really enjoy that painting in the hallway at HQ. It makes me hungry."

Table of tardis_Cleveland_quips
index	quip_string1
1	"Cleveland, the home of Rock and Roll! Make sure you see the Rock and Roll Hall of Fame."
2	"Have you been to Edgwater park. I hear it's fun to dig in the sand there."
3	"Go Browns!"
4	"Cleveland Rocks, Cleveland Rocks, Cleveland Rocks."
5	"GE Lighting was once headquartered in Cleveland."
6 	"You enjoy my company, don't you."
7	"Kara used to work here at NASA."
8	"Do you want me to hummm some theme music for you? It's different in Cleveland."
9	"I think those engineers at Nela Park are working on more than light bulbs."
10	"The North Coast is the best!"
11	"Cassie needs something that could contain the power of a powerful energy source."
12	"I think I could use a good reboot."

Table of tardis_africa_quips
index	quip_string1
1	"Out of Africa was a good movie. Maybe you should get out of here!"
2	"The crater is beautiful, but those lions are real! Be careful!"
3	"The rock churches of Ethiopia are amazing... carved out of single blocks of stone!"
4	"Do you think Robert has Cassie's crystal?"
5	"Toto has a good Africa song. Want me to hum it?"
6 	"You enjoy my company, don't you."
7	"Jack and Sarah have had some amazing adventures here!"
8	"How did you like your free 'African massage?'"
9	"Are you looking for a precious gem or a crystal?"
10	"Good luck getting what you came for... you might need a weapon."
11	"Why don't we just go eat some good Cincinnati chili?"
12	"I think I could use a good reboot."


Chapter 5 - actions, search, dance, rock, sing, and reboot 

Studying the vicinity is an action applying to nothing.

Report studying the vicinity:
	if the location does not contain something which is scenery:
		say "There's little of interest in the [location]." instead;
	repeat with point of interest running through scenery in the location:
		say "[point of interest]: [run paragraph on]";
		try examining the point of interest.

Understand "search" as studying the vicinity.

Dancing is an action applying to nothing. Understand "dance" or "boogie" or "jitterbug" as dancing.

Report dancing:
    say "You begin to dance like noone is watching."

Rocking is an action applying to nothing. Understand "rock" as rocking.

Report rocking:
    say "You begin rocking back and forth peacefully."

Rebooting is an action applying to nothing. Understand "reboot" as rebooting.

Report rebooting:
    say "How would you like me to do that?"

Understand “sing” as singing. Singing is an action applying to nothing. Carry out singing: do nothing. Report singing: say "'[awwk]!'"

Table of Singing
response
“I could while away the hours, conferring with the flowers, consulting with the rain! And my head I’d be scratching while my thoughts were busy hatching, if I only had a brain”
“Danke Schoën, Darling, Danke Schoën”
“Hallelujah”
“Somewhere! Over the Rainbow”
“HAPPY BIRTHDAY TO YOU”

To say awwk:
	choose a random row in the table of Singing;
	say "[response entry]".

Book 6 People

Chapter 1 - Player

The description of the player is "In the absence of a mirror, you're not quite sure what you look like today.”

Flattering is an action applying to one thing. Understand "flatter [someone]" and "praise [someone]" as flattering.
 
Check flattering:
	say "[The noun] blushes modestly."  


Understand "bridget" as The President.
Understand "jason" as The Prime Minister.

Chapter 2 - Jack

Jack is a man. He is in Skyline Chili. "Jack is a seasoned spy, known for his quick thinking and negotiation skills. He didn't start out as a spy as his initial contributions were in building Quotient's quantum computer, Tardis, in space. He is Sarah's brother and has a complex romantic relationship with Kara."

After going to Skyline Chili for the first time:
	try looking;
	say "Jack is enjoying his skyline, 'Hey, I'm glad you are here. You really should see the IMAX movie if you haven't already. I was starving and just thought I'd fuel up before my next stop. Help yourself if you want 3-way or a dew or anything, I'll put it on my tab. I'm heading to DC.'";
	
After going from the Skyline Chili for the first time:
	say "Jack leaves a tip for the server and races out the door ahead of you.";
	try looking;
	now Jack is in The Lincoln Memorial;


ZQuip is a number that varies. ZQuip is initially 1.

Instead of examining Jack:
	if ZQuip is 7:
		now ZQuip is 1;
	say "[quip_string in row ZQuip of table of quips][paragraph break]";
	now ZQuip is Zquip plus one;
	
Table of quips
index	quip_string
1	"Jack is now a seasoned spy, now 40 years old. He didn't start out as a spy as his initial contributions were in building Quotient's quantum computer. He admires his sister's fearlessness and has a past with Kara that still affects him deeply."
2	"Jack is still Jack"
3	"Do you think he looks like you?"
4	"When Jack was at MIT, he got a job at Lawrence Livermore Labs the summer before he was to start his PhD, where he met the CIA. He never started his PhD. After seven years in the CIA, he, like his sister before him, was tapped to join Quotient. Little did he know when he joined Quotient that his sister not only already worked there, but was now a skilled agent and had been in their employ since her college days, two years before he had even joined the CIA"
5	"He sure can eat skyline chili!"
6	"Maybe you need to read the book to get more info about Jack."

Chapter 3 - Sarah

Sarah is a woman. She is in the Conference Room. "Sarah, Jack's fearless sister, is here. She is always ready to dive into danger and is a highly skilled agent with a deep bond with her brother. At twenty-two, she was the youngest member ever to join this elite group.”
The description of Sarah is "Sarah is Jack's younger sister, known for her fearless attitude. She is an amazing agent at Quotient. Sarah was recruited into Quotient while she was while she was an undergraduate at Hertford College in Oxford. Sarah did her graduate work in history and economics. She is athletic, intelligent, and a great agent for Quotient.[paragraph break]She was a Rhodes Scholar, and a star student. Her undergraduate degree was in modern languages. She is fluent in four and dabbled in many others. Her language skills, sharp mind, and athleticism made her a great candidate when Quotient started looking for agents. The Rhodes Scholarship got her included on the list of prospective agents, but her skills resulted in her selection. She had studied dance since she was a child and had transitioned those skills to the study of Chinese martial arts.".

After going to the Conference Room for the first time:
	try looking;
	say "Sarah looks up from her computer and says, 'Hey! You made it. I didn't expect to see you here so soon. Welcome to the team! You just missed Martin. He'll have your assignment. I'm headed to Cleveland asap.'";
	
After going from the Conference Room for the first time:
	say "Sarah heads out the door at almost the same time you do.";
	try looking;
	now Sarah is in Nela Park;

Chapter 4 - Martin

Martin is a man. He is in The Hallway. "Martin is one of the original founders of Quotient. In his late 50s, Martin is a tall, fit, and sharp-minded leader. He has salt and pepper hair. He is still as fit as when he was thirty-five. He looks to be in deep thought."
The description of Martin is "Martin is one of the original founders of Quotient. In his late 50s, Martin is a tall, fit, and sharp-minded leader. He has salt and pepper hair. He is still as fit as when he was thirty-five. His deep blue eyes seem to dart around the room. He is always calculating escape routes, staying alert for threats, or just unexpected surprises. This was an old habit that didn’t go away just because he was now in a highly secure Quotient facility. As the original founder, he knew more about the history of Quotient than anyone. What he had started as a quantum computing initiative had turned into a global, independent spy agency.";

Understand "marty" as Martin.

Chapter 5 - Kara
	
Kara is a woman. She is in the Radcliffe Square. "Kara, a brilliant scientist at NASA, has a complex, romantic relationship with Jack. She is now part of Quotient."
The description of Kara is "Kara is a brilliant scientist at the NASA Glenn Research Center before she joined Quotient. An overachiever, and an only child, she has a doctorate from Cal Berkeley and enjoyed being spoiled. Kara is a tall, dark-skinned, French-Italian woman with long, dark, wavy hair. She has a complicated history with Jack.".

After going to the Radcliffe Square for the first time:
	try looking;
	say "Hello! Don't I know you?'";

Chapter 6 - Desmond

desmond_carry is a number that varies. desmond_carry is initially 1.

Desmond is a man. He is in the yard. "Desmond, Sarah's husband, the CTO at BrightStar, and a key ally in the mission against the Quantum Contingent."
The description of Desmond is "Desmond is Sarah's husband and the CTO at BrightStar. He is not a spy, but is supportive of the team in his own way.[if desmond_carry is 1] He is holding a flint and a steel striker.".
Desmond is carrying the flint and the steel striker.

Understand "husband" or "desi" or "des" or "desy" or "non spy" as Desmond.
	
Instead of asking Desmond about something:
	if the topic understood matches "flint":
		say "It's supposed to spark, but I've had no luck. Here, you give it a try.[line break]Desmond hands you the flint and steel striker.";
		now the player has the flint;
		now the player has the steel striker;
		now desmond_carry is 0;
	else if the topic understood matches "the flint":
		say "It's supposed to spark, but I've had no luck. Here, you give it a try.[line break]Desmond hands you the flint and steel striker.";
		now the player has the flint;
		now the player has the steel striker;
		now desmond_carry is 0;
	else if the topic understood matches "steel striker":
		say "I was using it to try and spark a fire, but I've had no luck. Here, you give it a try.[line break]Desmond hands you the flint and steel striker.";
		now the player has the flint;
		now the player has the steel striker;
		now desmond_carry is 0;
	else if the topic understood matches "the steel striker":
		say "I was using it to try and spark a fire, but I've had no luck. Here, you give it a try.[line break]Desmond hands you the flint and steel striker.";
		now the player has the flint;
		now the player has the steel striker;
		now desmond_carry is 0;
	else if the topic understood matches "the striker":
		say "I was using it to try and spark a fire, but I've had no luck. Here, you give it a try.[line break]Desmond hands you the flint and steel striker.";
		now the player has the flint;
		now the player has the steel striker;
		now desmond_carry is 0;
	else if the topic understood matches "striker":
		say "I was using it to try and spark a fire, but I've had no luck. Here, you give it a try.[line break]Desmond hands you the flint and steel striker.";
		now the player has the flint;
		now the player has the steel striker;
		now desmond_carry is 0;
	else if the topic understood matches "steel":
		say "I was using it to try and spark a fire, but I've had no luck. Here, you give it a try.[line break]Desmond hands you the flint and steel striker.";
		now the player has the flint;
		now the player has the steel striker;
		now desmond_carry is 0;
	otherwise:
		say "Where's Sarah? She knows a lot more than me."
		
Chapter 7 - Florin

Florin is a man. He is in the Ngorogoro Crater. "Florin, an outstanding Quotient agent, and a former MI-5 agent, is strolling around. He looks anxious, like he lost something important."

Understand "flo" as Florin.

Instead of examining Florin:
	if Florin is in Ngorogoro Crater:
		say "Florin, an outstanding Quotient agent, and a former MI-5 agent, is running around Africa trying to catch Robert Clayton. --- Off he goes again.";
		now Florin is in Lalibela Ethiopia;
	otherwise: 
		say "'I'm off to the crater' he says.";
		now Florin is in the Ngorogoro Crater;

Chapter 8 - Cassie

Cassie is a woman in the Cassie's Office. "Professor Cassandra Eaton’s thing of beauty is a deep understanding of quantum mechanics and time. She is tall and her silvery-blond hair is wavy, almost to her shoulders. Her piercing blue eyes have both put dread in the hearts of students during oral exams and have enraptured a few men’s hearts over the years."
The description of Cassie is "Cassie's thing of beauty is a deep understanding of quantum mechanics and time. She is the Lucasian Professor of Mathematics and head of all quantum studies at Oxford. She completed her PhD when she was only twenty-one and has been at Oxford ever since. Her deep understanding of quantum mechanics and the essence of time are world renowned. Today, her silvery-blond hair is pulled back, and her piercing blue eyes looks determined to make something work.".

Understand "prof" or "eaton" or "professor cassandra" or "cassandra" or "professor" or "professor eaton" or "professor cassandra eaton" as Cassie.

Instead of talking to Cassie:
	if Cassie is in Cassie's lab:
		say "[if the Quantum Console is activated] The entanglement is stable. Nice work! [Otherwise][one of]We need to get to keep the entanglement going. Check the console.[or]We need a powerful energy source get this thing going.[or]You'll need a password to open the console.[or]I really should have done my yoga in the metaverse before I came to the lab.[or]If you have something specific you want to know, ask me, otherwise, I'm going to get to work.[at random]";
	otherwise:
		say "Cassie looks at you with a mix of curiosity and determination. 'Ready to get to work? I'm going to the lab' she says.[paragraph break][if the Quantum Computer is wonky]'Oh, and the computer appears to need a reboot' she says as she disappears[paragraph break]";
		now Cassie is in Cassie's Lab.

Chapter 9 - President

The President is a woman. She is in The White House. "President Bridget O'Connor is a wise leader. She has been friends with Martin for many years. Earlier in her career she ran the NSA, and has maintained many of her old contacts with various 3 letter agencies. The President is aware of Quotient's operations."
The description of The President is "President Bridget O'Connor is a strong, insightful leader with a deep understanding of national security. Her friendship with Martin was rumored to be more than just a friendship when they were in college together. Earlier in her career she ran the NSA. (She's also reported to be a good dancer). 'Hello,' the President says with a smile.".

Understand "pres" or "prez" or "bridge" or "leader of the free world" as President.

Chapter 10 - Prime Minister

The Prime Minister is a man. He is in The White House. "Prime Minister Jason Stevenson is a skilled martial artist in addition to an ingenious political leader."
	The description of The Prime Minister is "Prime Minister Jason Stevenson is an experienced leader with a deep understanding of European state affairs as well as genetics. He is a skilled martial artist, and has been known to relax in front of a videogame at times. 'Hello,' the Prime Minister says with a smile.".
	
Understand "minister" as Prime Minister.

Chapter 11 - Server
	
The Server is a person. She is in Skyline Chili. "The server looks the same way she did when Jack and Sarah were kids."
The description of the Server is "The server looks the same as when Jack and Sarah were kids.".

Chapter 12 - Robot

The robot is a person. The robot is in The Lighting Institute. "There is GE robot here decorating the Christmas trees."
The description of the robot is "The Robot is a GE HAL 9000. It looks similar to the robot from Lost in Space. It was custom built for creating well lit Christmas trees.".

Book 7 - Things People do

Chapter 1  attacking to end the story 
		
Instead of attacking Martin:
	say "Your attack is easily blocked and you suddenly find yourself on the floor with your consiousness drifting away.[paragraph break]";
	say "You awaken in a small town jail cell in North Dakota. Your memory has been erased.";
	end the story.
	
Instead of attacking The President:
	say "Your attack is pointless and you suddenly find yourself on the floor with your consiousness drifting away.[paragraph break]";
	say "You awaken in a Maximum Security Prison.";
	end the story.
	
Instead of attacking The Prime Minister:
	say "The Prime Minister easily blocks your attack and you suddenly find yourself on the floor with your consiousness drifting away.[paragraph break]";
	say "You awaken in a small prison cell in the Tower of London.";
	end the story.
	
Chapter 2  Showing / Giving / Telling 

screaming is an action applying to nothing. Understand "scream" as screaming.
Understand "make noise" as screaming.
Understand "shout" as screaming.
Understand "yell" as screaming.

Instead of showing the Jagged Piece to Martin:
	say "[line break]'Excellent work. Now that you have successfully ransacked my office, let's go catch you up on the mission.'[line break] --- Martin heads to his office[line break]";
	now Martin is in Martin's office;
	
Instead of giving the Jagged Piece to Martin:
	say "[line break]'Excellent work. Now that you have successfully ransacked my office, let's go catch you up on the mission.'[line break] --- Martin heads to his office[line break]";
	now Martin is in Martin's office;
	
Instead of showing the light saber to Cassie:
	say "[line break]That looks like that would work perfectly.";
	
Instead of giving the light saber to Cassie:
	say "[line break]I'll need you to place it in the console while I calibrate it.";
	
Instead of showing the Crystal to Cassie:
	say "[line break]We need some way to contain it's power.";
	
Instead of giving the Crystal to Cassie:
	say "[line break]You keep it. We need some way to contain it's power before it is of use to me.";
	
Instead of showing the Crystal to [person]:
	say "[line break]Wow, you got the Crystal. You need to get that to Cassie right away!";
			
Instead of showing the light saber to [person]:
	say "[line break]Wow, a light saber!";
	
Instead of giving the light saber to [person]:
	say "[line break]Wow, a light saber! you should keep it.";
	
Instead of showing the tube to Sarah:
	say "[line break]The Engineers at Nela have been doing undercover work for the US government for years. They are trying to make a glass tube capable of containing a great power source.";
	
Instead of giving the tube to Sarah:
	say "[line break]You keep it. The Engineers at Nela have been doing undercover work for the US government for years. They are trying to make a glass tube capable of containing a great power source.";
	
Instead of showing the tube to [person]:
	say "[line break]Did that come from the Lighting research lab in Cleveland? Maybe it can be used to make a light?";
	
Instead of showing the Tranquilizer Gun to Florin:
	say "[line break]That looks just like my gun. Now you can stop Robert!";
	
Instead of showing the Tranquilizer Gun to Robert Clayton:
	say "[line break]Nice piece. Want to see my knife up close?";
	
Instead of giving the Tranquilizer Gun to Florin:
	say "[line break]Nah, you keep it. You are a spy now. You can stop Robert!";
	
Instead of showing the Tranquilizer Gun to [person]:
	say "[line break]I think Florin was looking for that.";
	
Instead of giving the Tranquilizer Gun to [person]:
	say "[line break]I'm good, thanks.";
	
Instead of showing the map to Sarah:
	say "[line break]Yes, I know where our secret airbases are, do you?";
	
Instead of showing the map to Jack:
	say "[line break]Are you lost?";
	
Instead of showing the map to [person]:
	say "[line break]Did you look at it?";
	
Instead of showing the Mirrored Ball to Sarah:
	say "[line break]Awesome! Put on some tunes and let's dance!";
	
Instead of showing the Pretzel to [person]:
	say "[line break]A Lawson Pretzel! Magnifique!";
	
Instead of showing the Scone to [person]:
	say "[line break]That looks delicious!";
	
Instead of showing the chocolate-covered strawberry to [person]:
	say "[line break]That is exquisite! A perfect blend of chocolate and strawberry!";
	
Instead of giving the Pretzel to [person]:
	say "[line break]A Lawson Pretzel! Magnifique!";
	
Instead of giving the Scone to [person]:
	say "[line break]Ummm, this is good!";
	
Instead of giving the chocolate-covered strawberry to [person]:
	say "[line break]Thanks, these are my favorite!";
	
Instead of showing the 3-way Chili to [person]:
	say "[line break]That looks delicious!";
	
Instead of showing the Mountain Dew to [person]:
	say "[line break]That always gets me moving!";
	
Instead of giving the 3-way Chili to [person]:
	say "[line break]Thanks. I'm hungry!";
	
Instead of giving the Mountain Dew to [person]:
	say "[line break]Glug, Glug... Ummm, Good!";
	
Instead of showing the Mirrored Ball to [person]:
	say "[line break]Cool! Where you get that? You should show it to Sarah, she'd get a kick out of it.";
	
Instead of showing the Tardis Communicator to Jack:
	say "[line break]The TARDIS Communicator can be very helpful. Everytime you examine it, TARDIS will talk to you. Unfortunately, it's version 1.0, so it's a one way connection. You can't give TARDIS any commands.";
	
Instead of showing the Tardis Communicator to Martin:
	say "[line break]The TARDIS Communicator is useful. Everytime you examine it, TARDIS will talk to you. Since it's version 1.0, it's a one way connection. You can't give TARDIS any commands.";
	
Instead of showing the Tardis Communicator to Desmond:
	say "[line break]Like I said, I'm just here for the s'mores. That looks like a spy thing.";
	
Instead of showing the Tardis Communicator to [person]:
	say "[line break]Oh, and old TARDIS Communicator! I haven't seen one of those in years.";
	
Instead of showing the Ancient Pendant to [person]:
	if Ancient Pendant is polished:
		say "[line break]The pendant's finish is like a mirror. It reminds me of the one Lancelot is wearing in the tapestry. Perhaps you should put it on?";
	otherwise:
		say "[line break]The pendant is rusty. I think you need to polish it.";
		
Instead of giving the Ancient Pendant to [person]:
	if Ancient Pendant is polished:
		say "[line break]The pendant's finish is like a mirror. It reminds me of the one Lancelot is wearing in the tapestry. Perhaps you should put it on?";
	otherwise:
		say "[line break]The pendant is rusty. I think you need to polish it.";

Instead of screaming:
	if the player is in Duke Humfreys Library:
		say "[line break]A librarian races over and escorts you into the street.";
		move the player to Radcliffe Square;
	otherwise:
		say "[line break]You SCREAM OUTLOUD as if your CAP LOCKS KEY IS STUCK ON.";
	
Chapter 3 Asking, Telling, Talking 
	
Talking to is an action applying to one visible thing. Understand "talk to [someone]" or “converse with [someone]” as talking to.

Section 1 - Kara
	
After asking the Kara about a topic listed in the Table of Kara's responses, say "[line break][response entry][line break]".

After asking the Kara about something:
	if the topic understood is not a topic listed in the Table of Kara's responses:
		say "[line break][one of]Kara looks at you quizzically, 'I don't know much about that.[or]Sorry, I'm not sure.[or]Hmmmm... maybe Martin knows.[or]Kara looks at you the way your dog look at you when you ask it if it wants to go for a walk and then says, 'Wow. I really don't know.'[at random]"
		
After telling the Kara about a topic listed in the Table of Kara's responses, say "[line break][response entry][line break]".

After telling the Kara about something:
	if the topic understood is not a topic listed in the Table of Kara's responses:
		say "[line break][one of]Kara looks at you quizzically, 'I don't know much about that.[or]Sorry, I'm not sure.[or]Hmmmm... maybe Martin knows.[or]Kara looks at you the way your dog look at you when you ask it if it wants to go for a walk and then says, 'Wow. I really don't know.'[at random]"


Table of Kara's responses
topic	response
"desmond"	"He's my sister's husband. A great guy."
"Jack"	"I love Jack. He happens to be an expert at quantum computing, and a great dancer."
"Sarah"	"Sarah is my soon to be sister-in-law. Why do you ask about her? Do you know something I don't?"
"Martin"	"He is like a father to me."
"computer"	"Tardis is our computer. Jack built it."
"the computer"	"Tardis is our computer. It is in space."
"tardis"	"Tardis is our computer. Jack built it."
"mission"	"We must get to the Continuum and power up the console."
"the mission"	"We must get to the Continuum and power up the console."
"Florin"	"Florin is gracious. A great agent. I heard he used to be MI-5."
"Cassie"	"She's brilliant with time, but can't she see Martin is in love with her?"
"President"	"President Bridget O'Connor is one of the finest women I have ever known."
"Prime Minister"	"Prime Minister Jason Stevenson would make a great Quotient Agent."
"Bridget"	"President Bridget O'Connor is one of the finest women I have ever known."
"Jason"	"Prime Minister Jason Stevenson would make a great Quotient Agent."
"Greg"	"I heard he's an author?"
"Karen"	"Did you mean Kara or Cassie?"
"Frank"	"Frank is a villian in the book, but in real life he's a great guy!"
"chili/skyline/skyline/scone/pretzel"	"Yum! Delicious."
"skyline chili"	"Yum! Delicious."
"dew/Pimms/beer/water/pop"	"Refreshing!"
"laser/lasers"	"Cassie said there is a special laser in the US that she needs to have turned on for her experiment to work."
"crystal"	"Cassie needs it to power something."
"metaverse/vr/meta"	"I know Cassie has a great VR lab."
"virtual reality"	"I know Cassie has a great VR lab."
"pendant"	"It has something to do with Cassie's time experiment, but I'm not sure what."
"the lion"	"The lion is Robert Clayton. A member of the Quantum Contingent."
"lion"	"The lion is Robert Clayton. A member of the Quantum Contingent."
"abyss"	"One of the world's worst prisons. It is in Gore, Ethiopia."
"the abyss"	"One of the world's worst prisons. It is in Gore, Ethiopia."
"robert clayton"	"Robert Clayton has escaped from the Abyss. He is a member of the Quantum Contingent."
"clayton/robert"	"Robert Clayton has escaped from the Abyss. He is a member of the Quantum Contingent."
"password/passcode"	"I don't know. You'd have to ask Cassie."

Section 2 - Desmond	

[moved to where Desmond is created]
	
Section 3 - Florin
	
Instead of asking Florin about something:
	say "I need to find my gun!"
	
Section 4 - Server

Instead of asking the server about something:
	say "I hope you leave me a big tip."

Section 5 - Cassie	

After asking Cassie about a topic listed in the Table of Cassie's responses, say "[line break][response entry][line break]".

After asking the Cassie about something:
	if the topic understood is not a topic listed in the Table of Kara's responses:
		say "[line break][one of]hmmm, for a magical result, you'll need to do more than just the mission. When all else fails, use lasers I always say![or]I think Lancelot knew something. If you find his pendant, wear it for good luck.[or]I haven't time for this, we must stabilize the entanglement![at random]"
		
After telling the Cassie about a topic listed in the Table of Cassie's responses, say "[line break][response entry][line break]".

After telling the Cassie about something:
	if the topic understood is not a topic listed in the Table of Cassie's responses:
		say "[line break][one of]Cassie looks at you quizzically, 'I don't know much about that.[or]Sorry, I'm not sure.[or]Hmmmm... maybe Martin knows.[or]Cassie, 'I'm afraid I don't know dear.'[at random]"
		
Table of Cassie's responses
topic	response
"desmond"	"I know he's Sarah's husband, but I don't really know him."
"Kara"	"I love Kara. She is an expert at quantum communication, and has been very good for me. She's not afraid to go for it!"
"Sarah"	"Sarah is sweet, a former student. Why do you ask about her? Do you know something I don't?"
"Martin"	"I know now that he is my one true love."
"computer"	"Oh, I don't talk about such things."
"the computer"	"Oh, I don't talk about such things."
"tardis"	"Tardis is from Dr. Who isn't it?"
"mission"	"I need to make the entanglement stable!"
"the mission"	"I need to make the entanglement stable!"
"Florin"	"Florin is a friend. I would recommend agents, he would develop them!"
"President"	"President Bridget O'Connor is considered an outstanding leader."
"Prime Minister"	"Martin always said he'd make a great Quotient Agent."
"Bridget"	"President Bridget O'Connor is known as a great leader."
"Jason"	"Prime Minister Jason Stevenson would make a great Quotient Agent."
"Greg"	"I heard he's an author?"
"Karen"	"Did you mean Kara or Cassie?"
"Frank"	"Frank is a villian in the book, but in real life he's a great guy!"
"chili/skyline/scone/pretzel"	"Yum! Delicious."
"skyline chili"	"Yum! Delicious."
"dew/Pimms/beer/water/pop"	"Refreshing!"
"mt dew"	"Refreshing!"
"mountain dew"	"Refreshing!"
"mtn dew"	"Refreshing!"
"metaverse/vr/meta"	"I do simulations there."
"laser/lasers"	"Critical for stabilization."
"crystal"	"The Crystal should be able to provide the power we need for the quantum console."
"movie/imax/film/theater/preview/previews"	"The previews are really long, you just have to wait."
"password/passcode/pass"	"Oh, it changes every day. I leave myself an anagram hint in my yoga simulation."
"console"	"It needs a strong power source, and to open it, you'll need to enter the password."
"quantum console"	"It needs a strong power source, and to open it, you'll need to enter the password."
"yoga"	"I practice yoga every day in my VR lab."
"meditation"	"I meditate every day in my VR lab"
"tapestry"	"The tapestry tells an old legend about Lancelot and how he was able to rescue his true love by altering time using a powerful glowing sword and a shiny pendant."
"light saber"	"That would be perfect for powering my console."
"light sabre"	"That would be perfect for powering my console."
	
Section 6 - Jack

After asking the Jack about a topic listed in the Table of Jack's responses, say "[line break][response entry][line break]".

After asking the Jack about something:
	if the topic understood is not a topic listed in the Table of Jack's responses:
		say "[line break][one of]Jack looks at you quizzically, 'I don't know much about that.[or]Sorry, I'm not sure.[or]Hmmmm... maybe Martin knows.[or]Jack looks at you the way your dog look at you when you ask it if it wants to go for a walk and then says, 'Wow. I really don't know.'[at random]"
		
After telling the Jack about a topic listed in the Table of Jack's responses, say "[line break][response entry][line break]".

After telling the Jack about something:
	if the topic understood is not a topic listed in the Table of Jack's responses:
		say "[line break][one of]Jack looks at you quizzically, 'I don't know much about that.[or]Sorry, I'm not sure.[or]Hmmmm... maybe Martin knows.[or]Jack looks at you the way your dog look at you when you ask it if it wants to go for a walk and then says, 'Wow. I really don't know.'[at random]"


Table of Jack's responses
topic	response
"desmond"	"He's my sister's husband. A great guy."
"Kara"	"I love Kara. She happens to be an expert at quantum communication, and a great dresser."
"Sarah"	"Sarah is my sister. Why do you ask about her? Do you know something I don't?"
"Martin"	"He is like a father to me."
"computer"	"Tardis is our computer. I built it."
"the computer"	"Tardis is our computer. It is in space."
"tardis"	"Tardis is our computer. I built it."
"mission"	"We must get to the Continuum and power up the console."
"the mission"	"We must get to the Continuum and power up the console."
"Florin"	"Florin is great. He saved my butt more than once!"
"Cassie"	"She's brilliant with time, but can't she see Martin is in love with her?"
"President"	"President Bridget O'Connor is one of the finest women I have ever known."
"Prime Minister"	"Prime Minister Jason Stevenson would make a great Quotient Agent."
"Bridget"	"President Bridget O'Connor is one of the finest women I have ever known."
"Jason"	"Prime Minister Jason Stevenson would make a great Quotient Agent."
"Greg"	"I heard he's an author?"
"Karen"	"Did you mean Kara or Cassie?"
"Frank"	"Frank is a villian in the book, but in real life he's a great guy!"
"chili/skyline/scone/pretzel"	"Yum! Delicious."
"skyline chili"	"Yum! Delicious."
"dew/Pimms/beer/water/pop"	"Refreshing!"
"mt dew"	"Refreshing!"
"mountain dew"	"Refreshing!"
"mtn dew"	"Refreshing!"
"metaverse/vr/meta"	"I know Cassie has a great VR lab."
"laser/lasers"	"All I know is that Cassie said there was a laser that would help her with her time experiment by aligning the quantum states."
"movie/imax/film/theater/preview/previews"	"The previews are really long, you just have to wait."
"the lion"	"The lion is Robert Clayton. A member of the Quantum Contingent."
"lion"	"The lion is Robert Clayton. A member of the Quantum Contingent."
"abyss"	"One of the world's worst prisons. It is in Gore, Ethiopia."
"the abyss"	"One of the world's worst prisons. It is in Gore, Ethiopia."
"robert clayton"	"Robert Clayton has escaped from the Abyss. He is a member of the Quantum Contingent."
"clayton/robert"	"Robert Clayton has escaped from the Abyss. He is a member of the Quantum Contingent."
"meditation"	"I meditate to clear my head"
"tapestry"	"The tapestry tells an old legend about Lancelot and how he was able to rescue his true love by altering time using a powerful glowing sword and a shiny pendant."
"light saber"	"It is a powerful weapon, just like Star Wars, except, I think we might be able to really make one."
"light sabre"	"A powerful weapon with a british spelling."
"crystal"	"I heard it was stolen by the lion."

Section 7 - Sarah

After asking the Sarah about a topic listed in the Table of Sarah's responses, say "[line break][response entry][line break]".

After asking the Sarah about something:
	if the topic understood is not a topic listed in the Table of Sarah's responses:
		say "[line break]Sarah smiles. 'That's an interesting question!'.[line break]"
		
After telling the Sarah about a topic listed in the Table of Jack's responses, say "[line break][response entry][line break]".

After telling the Sarah about something:
	if the topic understood is not a topic listed in the Table of Jack's responses:
		say "[line break][one of]Sarah looks at you quizzically, 'I don't know much about that.[or]Sorry, I'm not sure.[or]Hmmmm... maybe Martin knows.[or]Sarah looks at you the way your dog look at you when you ask it if it wants to go for a walk and then says, 'Wow. I really don't know.'[at random]"

Table of Sarah's responses
topic	response
"desmond"	"I love Desmond, he's my husband."
"Jack"	"I love Jack, he's my brother."
"Kara"	"Kara is an expert at quantum communication."
"Florin"	"Florin is a very capable agent. He trained me. He is a dear friend."
"Martin"	"He is like a father to me."
"computer"	"Tardis is our computer. It is in space."
"the computer"	"Tardis is our computer. It is in space."
"tardis"	"Tardis is our computer. It is in space."
"mission"	"We must get to the Continuum and power up the console. Anything else is an unknown at this point."
"the mission"	"We must get to the Continuum and power up the console. Anything else is an unknown at this point."
"Cassie"	"She's brilliant with time, but can't she see Martin is in love with her?"
"President"	"President Bridget O'Connor is one of the finest women I have ever known."
"Prime Minister"	"Prime Minister Jason Stevenson would make a great Quotient Agent."
"Bridget"	"President Bridget O'Connor is one of the finest women I have ever known."
"Jason"	"Prime Minister Jason Stevenson would make a great Quotient Agent."
"Greg"	"I heard he's an author?"
"Karen"	"Did you mean Kara or Cassie?"
"Frank"	"Frank is a villian in the book, but in real life he's a great guy!"
"chili/skyline/scone/pretzel"	"Yum! Delicious."
"skyline chili"	"Yum! Delicious."
"dew/Pimms/beer/water/pop"	"Refreshing!"
"metaverse/vr/meta"	"VR can be fun. I use the Meta Quest 8, myself.."
"crystal"	"Cassie needs it to power something, but it needs a containment chamber."
"tube/bulb"	"Maybe it could be used as a containment chamber?"
"fluorescent tube"	"Maybe it could be used as a containment chamber?"
"the lion"	"The lion is Robert Clayton. A member of the Quantum Contingent."
"lion"	"The lion is Robert Clayton. A member of the Quantum Contingent."
"abyss"	"One of the world's worst prisons. It is in Gore, Ethiopia."
"key/cage"	"Feel free to search my office. It's part of your test."
"iron cage"	"Feel free to search my office. It's part of your test."
"the abyss"	"One of the world's worst prisons. It is in Gore, Ethiopia."
"robert clayton"	"Robert Clayton has escaped from the Abyss. He is a member of the Quantum Contingent."
"clayton/robert"	"Robert Clayton has escaped from the Abyss. He is a member of the Quantum Contingent."
"tapestry"	"I don't know anything about the tapestry. Maybe you should ask Cassie."
"light saber"	"A light saber requires tremendous energy to operate."
"light sabre"	"A light saber requires tremendous energy to operate."
"crystal"	"I heard it was stolen by the lion."

Section 8 - Martin

After asking the Martin about a topic listed in the Table of Martin's Replies, say "[line break][muse entry][line break]".

After asking the Martin about something:
	if the topic understood is not a topic listed in the Table of Martin's Replies:
		say "[line break]Martin narrows his eyes. 'Do you have any important questions?'"
		
After telling Martin about a topic listed in the Table of Martin's Replies, say "[line break][muse entry][line break]".

After telling Martin about something:
	if the topic understood is not a topic listed in the Table of Martin's Replies:
		say "[line break]Martin narrows his eyes. 'Excuse me? What are you talking about?'"


[We can then provide a simple table giving Martin's responses:]

Table of Martin's Replies
Topic	Muse
"mission"	"We need to help Cassie with her experiment. That means, rebooting the computer and getting to the Continum to help her. There may be more steps required. We're not sure yet."
"the mission"	"We need to help Cassie with her experiment. That means, rebooting the computer and getting to the Continum to help her. There may be more steps required. We're not sure yet."
"computer"	"Tardis is our computer. It is in space."
"the computer"	"Tardis is our computer. It is in space."
"tardis"	"Tardis is our computer. It is in space."
"Jack"	"Jack is like a son to me."
"Sarah"	"Sarah is like a daughter to me."
"Kara"	"Kara really proved herself during The Quantum Contingent Mission"
"Florin"	"Florin is a very capable agent, and a long time friend."
"Cassie"	"Cassie is very dear to me. I've known her since I was her student at Oxford."
"President"	"President Bridget O'Connor is one of the finest women I have ever known."
"Prime Minister"	"Prime Minister Jason Stevenson would make a great Quotient Agent."
"Bridget"	"President Bridget O'Connor is one of the finest women I have ever known."
"Jason"	"Prime Minister Jason Stevenson would make a great Quotient Agent."
"Greg"	"I heard he's an author?"
"Karen"	"Did you mean Kara or Cassie?"
"Frank"	"Frank is a villian in the book, but in real life he's a great guy!"
"chili/skyline/skyline/scone/pretzel"	"Yum! Delicious."
"skyline chili"	"Yum! Delicious."
"dew/Pimms/beer/water/pop"	"Refreshing!"
"metaverse/vr/meta"	"The metaverse is entertaining, but not something I know a lot about. I do know that Cassie has a VR lab."
"escape hatch"	"It's right over your head."
"escape"	"Did you try up?"
"tapestry"	"I don't know anything about the tapestry. Maybe you should ask Cassie."
"the escape hatch"	"It's right over your head."
"the lion"	"The lion is Robert Clayton. A member of the Quantum Contingent."
"lion"	"The lion is Robert Clayton. A member of the Quantum Contingent."
"abyss"	"One of the world's worst prisons. It is in Gore, Ethiopia."
"the abyss"	"One of the world's worst prisons. It is in Gore, Ethiopia."
"robert clayton"	"Robert Clayton has escaped from the Abyss. He is a member of the Quantum Contingent."
"clayton/robert"	"Robert Clayton has escaped from the Abyss. He is a member of the Quantum Contingent."
"light saber"	"A light saber requires tremendous energy to operate."
"light sabre"	"A light saber requires tremendous energy to operate."
"tardis communicator"	"The TARDIS communicator will allow TARDIS to talk to you while you are on your mission. It can be very helpful in the field."
"communicator/comm/comms"	"The TARDIS communicator will allow TARDIS to talk to you while you are on your mission. It can be very helpful in the field."

Section 9 - Robot

After asking the Robot about a topic listed in the Table of Robot's Replies, say "[line break][r2d2 entry][line break]".

After asking the Robot about something:
	if the topic understood is not a topic listed in the Table of Robot's Replies:
		say "[line break]I'm sorry Dave, I can't do that."

[We can then provide a simple table giving Robot's responses:]

Table of Robot's Replies
Topic	r2d2
"pod bay doors"	"I'm sorry Dave, I can't do that."
"doors"	"I'm sorry Dave, I can't do that."
"pod"	"I'm sorry Dave, I can't do that."
"pod bay"	"I'm sorry Dave, I can't do that."
"bay"	"I'm sorry Dave, I can't do that."
"light bulbs"	"Fluorescent bulbs will always have dark ends."
"light"	"Fluorescent bulbs will always have dark ends."
"tree"	"I will decorate the tree. You are not needed."
"christmas tree"	"I will decorate the tree. You are not needed."
"chili/skyline/skyline/scone/pretzel"	"Yum! Delicious."
"skyline chili"	"Yum! Delicious."
"dew/Pimms/beer/water/pop"	"Refreshing!"

Section 10 - Talking

Check talking to: say "[The noun] doesn't reply."

Instead of talking to Robot:
	say "[line break]I'm sorry Dave, I can't do that.[line break]"

Instead of talking to Martin:
        say "[if aslan is magic]You've completed your mission. Congratulations, Agent.[else if Martin is in The Hallway]Hello Agent, I'm glad you arrived. I'll brief you in my office, after you show me the puzzle piece.[else][one of]'Do you have any other suggestions?' you ask.[paragraph break]'Be careful out there, and take the Tardis Communicator.' His voice is clear as he looks you straight in the eye.[or]'I will,' you reply. 'What can you tell me about the time experiment?'[paragraph break]'Cassie is a genius when it comes to time. You'll need to see her. She calls her lab Continum.'[or]'Do you like Cassie?.[paragraph break]'That's a strange question for someone who has just started her, but I will say, I have always enjoyed Cassie's company.'[or]You decide against talking any further with Martin right now.[then at random]".

Instead of talking to Sarah:
        say "[if aslan is magic]You've completed your mission. Congratulations, Agent.[else if Sarah is in Nela Park][one of]'Hello again, what are you up to.' you say.[paragraph break]'I'm looking for some type of containment device, not sure exactly what kind yet, but the engineers at Nela are actually working undercover for the CIA.'[or]'Ahhh, ok. Good to know.'[paragraph break]'Thanks for your help on this mission. We were going to just do a fun training mission, but then the lion escaped and messed up our plans.'[or]'No problem. It's great to get to work with you and the quotient team.'[or]You decide against talking any further with Sarah right now.[then at random][else][one of]'Hi, there,' you say confidently.[paragraph break]'Hi, Welcome to the team.' she replies with a smile.[or]'I've been meaning to ask you about Martin,' you comment. 'What's he like?'[paragraph break]'Martin is legendary for his connections. He can call in favors from all over. And underneath that Marine-like exterior is a softie. He's like a father to me.'[or]'Thank you.[paragraph break]'Of course, Good luck.'[or]You decide against talking any further with Sarah right now.[then at random]".

Instead of talking to Desmond:
        say "[if the bonfire is in the Yard]These s'mores are great! Help yourself![else][one of]'Hey,' you say casually.[paragraph break]'Hello, Beautiful day eh?' he replies seemingly distracted.[or]'I've been meaning to ask you about the mission,' you comment. 'What do we do?'[paragraph break]'That's a question for an agent.'[or]'What are you doing out here?.[paragraph break]'I just want to make some s'mores while I wait for my wife, but I'm having trouble getting the fire started.'[or]You decide against talking any further with Desmond right now.[then at random]".

Instead of talking to Jack:
        say "[if aslan is magic]You've completed your mission. Congratulations, Agent.[else if the laser is switched on]Wow, look at that red sky! You must have found the laser. Good work![else if Jack is in the Lincoln Memorial][one of]'Hello again, what are you up to.' you say.[paragraph break]'I'm looking for a laser.'[or]'Why?[paragraph break]'It has something to do with Cassie's time experiment. She said I'll know it when I see it.'[or]'I'll help.' you say. 'Thanks. It's great to have you on the team.'[or]You decide against talking any further with Jack right now.[then at random][else][one of]'Hi, there,' you say confidently.[paragraph break]'Hey, the new agent! What do you need?' says Jack.[or]'I wanted your advice on the TARDIS computer I've heard about,' you comment.'[paragraph break]'You can always just reboot it! When all else fails, control-alt-delete I say!'[or]'You designed it, right.[paragraph break]'I sure did.'[or]You decide against talking any further with Jack right now.[then at random]".

Instead of talking to Florin:
        say "[if aslan is magic]You've completed your mission. Congratulations, Agent.[else][one of]'Hi, how's it going,' you say casually.[paragraph break]'Well hello!' he replies ethusiatically.[or]'What are you up to?' you say.[paragraph break]'Just trying to put a lion back in a cage'[or]'Are you searching for Robert Clayton?'[paragraph break]'I am, but I've lost my gun, so he keeps running away.'[or]You decide against talking any further with Florin right now.[then at random]".

Instead of talking to the President:
        say "[if aslan is magic]You've completed your mission. Congratulations, Agent.[else][one of]'Hello Madam President'[paragraph break]'Hello. I heard Martin has recruited you?'[or]'Yes Madam President' you say.[paragraph break]'It is important work. Good Luck.'[or]You decide against talking any further with the President right now.[then at random]".

Instead of talking to the Prime Minister:
        say "[if aslan is magic]You've completed your mission. Congratulations, Agent.[else][one of]'Hello Sir.'[paragraph break]'Hello. I heard you are attempting to help Professor Eaton'[or]'Yes Sir' you say.[paragraph break]'That's great. She is a remarkable scientist. The best we have. Good Luck.'[or]You decide against talking any further with the Prime Minister right now.[then at random]".

Instead of talking to Kara:
        say "[if aslan is magic]You've completed your mission. Congratulations, Agent.[else if the player has the pendant][one of]You found the pendant! Good work![or]I know Cassie was looking for a power source too.[or]Good Luck![or]You decide against talking any further with Kara right now.[then at random][else][one of]'Hello, aren't you Kara?'[paragraph break]'Yes, I am. I'm looking for an ancient Pendant, what are you doing here?'[or]'Martin told me to help Cassie'[paragraph break]'Outstanding. Last time I saw her she was looking for a power source. She is amazing.'[or]'I'll see what I can do'[paragraph break]'Good luck!'[or]You decide against talking any further with Kara right now.[then at random]".

Instead of talking to Server:
        say "[one of]'Can I order?[paragraph break]'Why of course, but I already put in your standard order![or]'Can I get a Mountain Dew?'[paragraph break]'Coming right up!'[or]'Do you know the way to the Museum Center?'[paragraph break]'Sure. Head north, or is it south?'[or]You decide against talking any further with the server right now.[then at random]".


Chapter 4 Smelling and Listening 

A thing has some text called the scent.
 
Definition: A thing (called the odor-bearer) is smelly if the scent of the odor-bearer is not "".
 
Instead of smelling something:
	if the noun is smelly:
		say "[scent of the noun][paragraph break]";
	otherwise if the noun is the player:
		say "You don't smell too sweaty today.";
	otherwise:
		say "It smells like an ordinary [noun]."
  
Instead of smelling a room:
	let L be the list of smelly things enclosed by the location;
	if L is empty: 
		say "You smell nothing unexpected."; 
	otherwise: 
		say "You smell [a list of smelly things enclosed by the location]." 
		
Section 1 - Scents
 
The scent of the scone is "It smells scrumptious."
The scent of the pretzel is "It brings back good memories of traveling through Germany!"
The scent of the Pimms is "This isn't a glass of wine you know."
The scent of the 3-way chili is "An alluring combination of chocolate and cinnamon, combined with the sharp scent of finely shredded chedder."
The scent of the rediwhip is "It smells like fun! Whoa! I didn't mean to spray it on my nose!"
The scent of Mountain Dew is "A fizzy mountain dew reminds you of a dew filled meadow."
The scent of the bonfire is "It smells like smoke... not unusual for a fire!"
The scent of the succulent is "It has the smell of a beautiful rainforest, all in less than the size of a bowling ball."
The scent of the strawberry is "A brief but satisfying hint of berry with a deep enveloping smell of cocoa."

The loaf of bread is in the Kitchen. The scent of the bread is "Ah -- fresh-baked bread.”
The description of the loaf of bread is "It looks freshly baked!".

Instead of eating the bread:
	say "That hit the spot!"
	
Cutting into it with is an action applying to one thing and one carried thing.
Understand "cut [something] with/using [something]" as cutting into it with.
Understand "cut with/using [something] into/in [something]" as cutting into it with (with nouns reversed).
Understand "cut [something]" as a mistake ("you must specify both what you are cutting and what you are cutting with.")

Check cutting into it with:
	if the noun is not the bread or the noun is not the scone, say "Cutting into [a noun] is silly." instead;
	if the second noun is not the paring knife, say "Cutting with [the second noun] is slow and tedious."

Check cutting into the bread with the paring knife:
	say "The smell of the freshly baked bread erupts from the bread. You quickly devour the slice." instead.

Check cutting into the scone with the paring knife:
	say "You decide to savor small slices of this delicable scone. Each piece pleases you" instead.

A thing has some text called sound. The sound of a thing is usually "silence".

The report listening rule is not listed in the report listening to rules.

Carry out listening to something:
	say "From [the noun] you hear [the sound of the noun]."

Instead of listening to a room:
	if an audible thing can be touched by the player, say "You hear [the list of audible things which can be touched by the player].";
	otherwise say "Nothing of note."

Definition: a thing is audible if the sound of it is not "silence".

Before printing the name of something audible while listening to a room:
	say "[sound] from the "
	
Section 2 - Sounds

The sound of the rediwhip is "sploosh".
The sound of the rocket is "rumble, rumble". 
The sound of the jet is "hmmmmmmmmmmmmmm".
The sound of the watch is "tick tock".
The sound of the Mountain Dew is "fizzzzzzzzz".
The sound of the Quantum Computer is "bzzp, bing, tick-tock, whirr. Wait, it's in a vacuum... never mind, you can't hear anything.".
The sound of the bonfire is "Roar!".
The sound of the light saber is "Hmmmmm, Zoooommm, VRRoommmmm".
The sound of the mirrored ball is "You hear a distance song. It sounds like it is bouncing off of blue ocean waves. It is Dancing Queen by Abba."
The sound of the succulent is "I think the succulent's sound is the Taylor Swift Folklore album."
The sound of the Greek God Mercury is "Just do it!"
The sound of the gun is "Kapooooeee"


Chapter 5 - Creating and Recalling Memories [modified from Sand-Dancer by Aaron Reed and Alexei Othenin-Girard]

A memory is a kind of thing. A memory can be retrieved or buried. A memory is usually buried.

Suggestion relates various things to one memory (called the suggested memory).

The verb to suggest (he suggests, they suggest, he suggested, it is suggested, he is suggesting) implies the suggestion relation.

Understand "memory/memories" as a memory.

Does the player mean doing something to a memory: it is unlikely.

Rule for deciding whether all includes memories: it does not.

The subconscious is a container. When play begins: now every memory is in the subconscious.

Definition: a thing is charged if it suggests a memory which is in the subconscious.

Every turn when a charged thing (called the item) is visible: move the suggested memory of the item to emotional baggage; say "Something about [the item] reminds you of [the suggested memory of item].";

The player carries an open transparent unopenable container called emotional baggage. The description of emotional baggage is "You'll collect old memories over time. You can think about them whenever you want. You imagine this as an old dufflebag."

Instead of doing anything other than examining when noun is emotional baggage or second noun is emotional baggage: say "It's not real... it is your subconscious."

Recalling is an action applying to one thing. Understand "recall about/on/over [something]" or "recall [something]" or "think about [something]" or "remember [something]" as recalling.

Check recalling when noun is not a memory: instead say "I don't seem to recall this."

Carry out recalling: say "[description of noun][line break]"; now the noun is retrieved. [We defined earlier that memories can be either buried (not yet recalled about) or retrieved.]

Instead of doing anything other than recalling to a memory, say "As if. All you can really do is THINK ABOUT it."

Instead of thinking: say "To think about memories, use THINK ABOUT it." [Thinking is a standard action which we'll use to hint the player towards better options.]

Instead of recalling when noun suggests a memory: say "It's not [the noun] you want to think about, it's [a random memory suggested by the noun]."

Section 1 Memories

Christmas Day is a memory. It is suggested by the vr suit. The description is "Wow, you remember the Christmas Day many years ago you got your very own Quest Rift Suit, it was always hard to quit playing… Now stopping getting distracted, you need to focus on the mission.”

Germany is a memory. It is suggested by the Pretzel. The description is "Ahhhh, baking Pretzels in Germany was a treat, but I'm not sure this memory will help me in my mission at all!”

campfires is a memory. It is suggested by the Smore. The description is "Shoot, my mind keeps wandering, but yes, those s'more's over the camprfire were tasty!"

Family is a memory. It is suggested by Sarah. The description is "Sarah does remind me of my family, must be the hair. According to the briefing I read before I came, her brother is Jack... MUST focus."

my childhood is a memory. It is suggested by Figurine. The description is "I remember playing with figurines like the Tumnus figurine when I was a child. Not that that thought helps me with the mission! MUST focus."

days gone by is a memory. It is suggested by deck of cards. The description is "[if the location is The Farmhouse]The place does feel familiar. In fact the rug reminds me of one from a TV show that hid a trap door.[else]I remember playing cards in an old farmhouse. We used to play hide and seek in the cornfield north of the yard of the house."

college is a Memory. It is suggested by Mountain Dew. The description is "I remember getting through many long study sessions in college with Mountain Dew. It is great for when you need energy to go the extra mile."

Understand "focus" as a mistake ("Ok... that's better. Let's get on with the mission.");

Book 8 - Mapping/Ranking/Release

Chapter 1  Mapping 

The Lane is west of The Security Checkpoint.
The Gate is west of the Lane.
The Headquarters Entrance is west of The Gate.
The Old Cellar is down from The Headquarters Entrance.
The Inner Sanctum is west from The Secret Passage.
The Conference Room is west of The Inner Sanctum.
The Hallway is west of The Conference Room.
The Hyperloop Station is west of The Hallway.
The Medical Center is south of The Hallway.
The Yard is north of The Headquarters Entrance.
The Smokehouse is west of the Yard.
The Fire Pit is south of The Smokehouse.
The Old Barn is west of The Smokehouse.
The Corn Maze is north of the Lane.
The Clearing is north of the Corn Maze.
The Dead End is west of the Clearing.
The Cemetery is north of the Clearing.
The Hidden Path is west of the Cemetery.
The Secret Path is west of the Hidden Path.
The Yard is south of the Secret Path.
Maze1 is east of Corn Maze.
A maze room called Maze2 is east of Maze1.
A maze room called Maze3 is north of Maze1.
A maze room called Maze4 is west of Secret Path.
A maze room called Maze5 is west of Maze4.
A maze room called Maze6 is north of Maze2.
Maze3 is west of Maze6.
The Hidden Airbase is north of Broad Street.
The Sheldonian Theatre is south of the Broad Street.
The Turf Tavern is east of the Sheldonian Theatre.
The Bodleian Library is south of the Sheldonian Theatre.
The Clarendon Building is east of the Broad Street.
The Convocation House is west of Radcliffe Square.
The Convocation House is south of The Divinity School.
The Divinity School is west of the Bodleian Library.
The Radcliffe Square is south of the Bodleian Library.
The Radcliffe Camera is south of the Radcliffe Square.
The Vaults_Garden is south of the the Radcliffe Camera.
The Narnia Lamp is southwest of the Radcliffe Camera.
The Narnia Lamp is west of the Vaults_Garden.
The Duke Humfreys Library is east of The Bodleian Library.
The Cassie's Office is east of the Clarendon Building.
The Cassie's Office is northeast of Turf Tavern.
The Wooden door is north of Cassie's Office.
The Cassie's Lab is north of the Wooden Door.
[Some mapping is only included in the room descriptions... I could delete this]

Chapter 2 Rankings

Table 1 - Rankings
Score	Rank
0	"plain ole you"
14	"Amateur Agent"
25	"Agent in Training"
40	"Agent"
60	"Experienced Agent"
80	"Secret Agent"
107	"Super Duper Agent"
214	"Master Super Duper Agent"

Chapter 3 Release

Test me with "examine watch / go west / go west / go down / go down / go west / go west / take SCIF".

Release along with an interpreter, website, the introductory booklet, the introductory postcard, private source text, and cover art.
Release along with a file of "Spoiler-free Map" called "Quantum The Game - Map Spoiler Free.pdf".





